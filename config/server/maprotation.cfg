///////////////////////////////////////////////////////////////////////////////
//  Map rotation configuration.                                              //
///////////////////////////////////////////////////////////////////////////////


maprotationreset
maprotation "*"         $deathmatch_maps     // "Deathmatch" mode maps.
maprotation "?tdm"      $teamdeathmatch_maps // "Team Deathmatch" mode maps.
maprotation "?ctf"      $capturetheflag_maps // "Capture The Flag" mode maps.
maprotation "?elim"     $elimination_maps    // "Elimination" mode maps.
maprotation "?lms"      $elimination_maps    // "Last Man Standing" mode currently uses "Elimination" maps.
maprotation "?infect"   $infection_maps      // "Infection" mode maps.
maprotation "?invasion" $deathmatch_maps     // "Invasion" mode currently uses "Deathmatch" maps.
maprotation "?betray"   $all_maps            // "Betrayal" mode currently uses all maps.


///////////////////////////////////////////////////////////////////////////////
//  Map list generation.                                                     //
///////////////////////////////////////////////////////////////////////////////


defmaptable = [
	arch           0x33
	azag           0x7B
	azag2          0x7B
	ctf_1          0x7F
	ctf_desertwind 0x7F
	flux3          0x33
	lightspeed     0x7B
	idyll2         0x33
	nebula         0x33
	ot2            0x33
	pinelake       0x7B
	pride          0x33
	sera           0x33
	ctf_steelribs  0x7F
	tropica        0x33
	vale           0x33
	wintergreen    0x33
]


// genmaplist VARNAME MODE_BITMASK MAPLIST

genmaplist = [
	if $arg3 [] [ arg3 = $defmaptable ]
	$arg1 = (loopconcat* m 2 (/ (listlen $arg3) 2) [
		if (|| [= $arg2 0] [& $arg2 (at $arg3 (+ $m 1))]) [ at $arg3 $m ]
	])
]


// Generation of map lists based on mode masks

genmaplist all_maps             0x0  // No mask
genmaplist deathmatch_maps      0x1  // Deathmatch
genmaplist teamdeathmatch_maps  0x2  // Team Deathmatch
genmaplist capturetheflag_maps  0x4  // Capture The Flag
genmaplist elimination_maps     0x8  // Elimination
genmaplist lastmanstanding_maps 0x10 // Last Man Standing
genmaplist berserker_maps       0x20 // Berserker
genmaplist infection_maps       0x40 // Infection


// Generation of a custom maps list based on maps not included in the "official" map table,
// which are automatically categorized as "user maps".

genusermaps = [
	usermaps = [] // where custom maps by the user will be stored
	loopfiles m "data/map" "ogz" [
		if (< (indexof $allmaps $m) 0) [
			append usermaps $m
		]
	]
]

genusermaps
