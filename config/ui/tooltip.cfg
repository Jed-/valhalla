
newui "tips" [
    uiclamp.e
    uiallowinput 0
    if $uiTipName [
        if (uivisible $uiTipMenu) [] [ uiDelTip ]
        if $uiTipLive [
            .DisplayTip
        ] [
            //echo "^f0WAITING PHASE:^f2" (- $getmillis $uiTipTime)
            if (|| [!= $uiTipPosX (getpxposx)] [!= $uiTipPosY (getpxposy)]) [
                set uiTipTime $getmillis
                set uiTipPosX (getpxposx)
                set uiTipPosY (getpxposy)
            ] [ set uiTipLive (> $getmillis (+ $uiTipTime 1200)) ]
        ]
    ]
] [ uiTipFade = 0 ] [ sleep 0 [ showui "tips" ] ]

.DisplayTip = [
    //echo "^f1DISPLAY PHASE:^f2" (- $getmillis $uiTipTime)
    local xdiff ydiff x y
    xdiff = (abs (- $uiTipPosX (getpxposx)))
    ydiff = (abs (- $uiTipPosY (getpxposy)))
    if (|| [> $xdiff 16] [> $ydiff 16] [ > $getmillis (+ $uiTipTime 5000) ]) [ uiDelTip ] [
        uiTipFade = (minf 1 (+f $uiTipFade 0.25))
        x = (*f (/f $uiTipPosX $screenw) $uiaspect)
        y =     (/f $uiTipPosY $screenh)
        +f= x (*f 0.00060 $crosshairsize)
        +f= y (*f 0.00126 $crosshairsize)
        uioffset $x $y [
            uiFastImgStretched (+s ( fade $uiTipFade ) <mad:.05/.05/.05>) "hud/" "row" "" [
                uispace $uiPad:S 0 [
                    uihlist $uiPad:S [
                        uiFancyText "wide" "?" 0.6 $c_green $uiTipFade
                        uiwrapcolortext (unescape $uiTipName) 0.33 (|A! (*fA $uiTipFade)) 0.5
                    ]
                ]
            ] $uiPad:4XL
        ] ; uialign- -1 -1
    ]
]
