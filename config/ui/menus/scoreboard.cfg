
///////////////////////////////////////////////////////////////////////////////
//  Scoreboard                                                               //
///////////////////////////////////////////////////////////////////////////////

.scoreboard.update = [
	local bit cc

	if $getmode                 [ bit = (^ $bit 0x01) ; ++ cc ] // score
	if $showkills               [ bit = (^ $bit 0x02) ; ++ cc ] // frags
	if $showdeaths              [ bit = (^ $bit 0x04) ; ++ cc ] // deaths
	if $scoreboardmultiplayer [
		if $showpj              [ bit = (^ $bit 0x08) ; ++ cc ] // packet jump
		if $showping            [ bit = (^ $bit 0x10) ; ++ cc ] // ping
	]

	if (|| $arg1 [!= (getalias .sb_bitmask) $bit] [!= (getalias .sb_modemask) $bit]) [
		.sb_bitmask  = $bit
		.sb_modemask = $getmode

		local .sb_namewidth .sb_cellwidth .sb_rowwidth
		local .scoreboard.cells .scoreboard.icons
		local .scoreboard.header .scoreboard.row
		local .scoreboard.shelf.top3 .scoreboard.shelf.rest

		.sb_namewidth = $uiPad:UXS
		.sb_cellwidth = (+f $uiPad:DSS $uiPad:M)
		.sb_rowwidth  = (+f $.sb_namewidth (*f $.sb_cellwidth $cc))

		loop n 5 [ if (& $bit (<< 1 $n)) [
			append .scoreboard.cells [[uifill @@[.sb_cellwidth] 0 [@@@(at [
				[uiFancyText "" (clamp (getclientscore  $cn)    0 9999) 0.58]
				[uiFancyText "" (clamp (getclientfrags  $cn) -999 9999) 0.58]
				[uiFancyText "" (clamp (getclientdeaths $cn)    0 9999) 0.58]
				[if (scoreboardpj $cn)   [uiFancyText "" (clamp (scoreboardpj   $cn) 0 9999) 0.58] [uiFancyText "wide" "-" 0.58 $c_gray]]
				[if (scoreboardping $cn) [uiFancyText "" (clamp (scoreboardping $cn) 0 9999) 0.58] [uiFancyText "wide" "-" 0.58 $c_gray]]
			] $n)]]]
			append .scoreboard.icons (at [
				@@@(at "0 star star flag star crown crown star" $getmode)
				"frag" "death" "pj" "ping"
			] $n)
		] ]

		// arg1: team, arg2: team name, arg3: [score], arg4: color
		.scoreboard.header = [
			arg4 = (= $arg1 2) // horizontal flip bool
			arg5 = (at [0 0x40D060 0x4060D0 0xD04040] (+ $arg1 1))
			result [
				uifill @[.sb_rowwidth] $uiPad:DM [
					uispace 0 $uiPad:S [
						uiFastImg <reorient:@@@arg4> "ui/" "scoreboard/" "scorebg"
						uiclamp-e
					] ; uiclamp-e
					uispace $uiPad:M $uiPad:M- [
						uiFancyText "wide" @@@arg2 1.4 @@@arg5
					] ; uialign- @@(? $arg4 1 -1) -1
					uispace $uiPad:O5 (*f $uiPad:O5 2.9) [
						uiFancyText "wide" (@@@arg3) 1 @@@arg5
					] ; uialign- @@(? $arg4 -1 1) -1
					if (numscoreboard @@arg1) [
						uihlist 0 [
							@@@@(? $arg4 [
								uialign -1 1
								@(looplistconcatrev x $.scoreboard.icons [ result [
									uifill @@[.sb_cellwidth] (*f $uiPad:O4 7) [
										uiShadowImg "" "ui/" "scoreboard/" @@x $uiPad:3XL
									]
								] ])
							] [
								uialign 1 1
								@(looplistconcat x $.scoreboard.icons [ result [
									uifill @@[.sb_cellwidth] (*f $uiPad:O4 7) [
										uiShadowImg "" "ui/" "scoreboard/" @@x $uiPad:3XL
									]
								] ])
							])
						]
					]
				]
			]
		]

		// arg1: team, arg2: children, arg3: custom cells, arg4: custom width
		.scoreboard.row = [
			arg4 = (= $arg1 2) // horizontal flip bool
			arg5 = (at [
				[@@@@(mad 0x303840) @@@@(mad 0x303840)] // spec
				[@@@@(mad 0x303030) @@@@(mad 0x282828)] // ffa
				[@@@@(mad 0x1C2E70) @@@@(mad 0x122466)] // aesir
				[@@@@(mad 0x701C1C) @@@@(mad 0x661212)] // vanir
			] (+ $arg1 1))
			result [
				uiFastImg (at [@@arg5] (& $n 1)) "ui/" "scoreboard/" "row" @(? $arg3 $arg3 $.sb_rowwidth) $uiPad:5XL [
					@@(? $arg2 [ uigroup [ uiclamp.e ; @@arg2 ] ])
					if (scoreboardhighlight $cn) [
						uispace $uiPad:O3 $uiPad:O3 [
							uiShadowImg <reorient:@@@@arg4> "ui/" "scoreboard/" "edge" $uiPad:M
						] ; uialign- @@@(? $arg4 1 -1) -1
					]
					@@(? $arg4 [
						uihlist 0 [
							@@(looplistconcatrev x $.scoreboard.cells [ result [ @x; ] ])
						] ; uiclamp-y ; uialign- -1
						uispace $uiPad:M 0 [ uihlist $uiPad:O3 [
							if (isai $cn) [ uiShadowImg "" "ui/" "scoreboard/" "bot" $uiPad:2XL ]
							uiFancyText "" (getclientname $cn) 0.6 (scoreboardstatus $cn)
						] ] ; uialign- 1
					] [
						uispace $uiPad:M 0 [ uihlist $uiPad:O3 [
							uiFancyText "" (getclientname $cn) 0.6 (scoreboardstatus $cn)
							if (isai $cn) [ uiShadowImg "" "ui/" "scoreboard/" "bot" $uiPad:2XL ]
						] ] ; uialign- -1
						@(? (> $arg1 -1) [
							uihlist 0 [
								@@(looplistconcat x $.scoreboard.cells [ result [ @x; ] ])
							] ; uiclamp-y ; uialign- 1
						])
					])
				] ; ++ n
			]
		]

		if (m_teammode $getmode) [
			.scoreboard.aesir = [
				local n
				uivlist 0 [
					@@(.scoreboard.header 1 (getteamname 1) [getteamscore 1])
					loopscoreboard cn 1 [ @@@(.scoreboard.row 1) ]
				] ; uialign- -2 -1
			]
			.scoreboard.vanir = [
				local n
				uivlist 0 [
					@@(.scoreboard.header 2 (getteamname 2) [getteamscore 2])
					loopscoreboard cn 2 [ @@@(.scoreboard.row 2) ]
				] ; uialign- -2 -1
			]
			.scoreboard.ctf = [
				uigroup [
					uioffset 0 $uiPad:O5 [
						uioffset $uiPad:2XL- $uiPad:2XL- [ uiFancyText "wide" "V" 1 ]
						uioffset $uiPad:2XL  $uiPad:2XL  [ uiFancyText "wide" "S" 1 ]
					] ; uialign- 0 -1
					uihlist $uiPad:DM [
						.scoreboard.aesir
						.scoreboard.vanir
						uialign* -2 -1
					]
				]
			]
		]

		.scoreboard.spec = [
			uigrid 3 $uiPad:L 0 [
				loopscoreboard cn -1 [
					@@@(.scoreboard.row -1 [] $.sb_namewidth)
				]
			]
		]

		.scoreboard.shelf.top3 = [
			uioffset $uiPad:5XL- 0 [
				uiFastImg "" "ui/" "scoreboard/" "shelf1" $uiPad:5XL
				push n (at [<mad:1/.8/0> <mad:.8/.8/.8> <mad:.8/.5/0>] $n) [
					uiFastImg $n "ui/" "scoreboard/" "glow"  $uiPad:5XL
					uiFastImg $n "ui/" "scoreboard/" "crown" $uiPad:3XL
				]
			] ; uialign- -1
		]
		.scoreboard.shelf.rest = [
			arg1 = (= $cn $getclientnum)
			uioffset $uiPad:5XL- 0 [
				uiFastImg "" "ui/" "scoreboard/shelf" (- 2 $arg1) $uiPad:5XL
				uiFastImg <mad:.18/.18/.18> "ui/" "scoreboard/" "glow" $uiPad:5XL
				push n (+ $n 1) [
					uifontcolortext "def.ol" $n (|A! (? $arg1 0xB0 0x60)) 0.55
				]
			] ; uialign- -1
		]

		.scoreboard.ffa = [
			local n x s
			s = (isspectator $getclientnum)
			uigroup [
				uivlist 0 [
					@@@(.scoreboard.header 0 "FFA" 0)
					loopscoreboard cn 0 [
						if (< $n 3) [
							if (= $cn $getclientnum) [ x = 1 ]
							@@@@@(.scoreboard.row 0 $.scoreboard.shelf.top3)
							if (= $n 3) [ uifill 0 $uiPad:M ]
						] [
							if (= $cn $getclientnum) [
								if (> $n (+ $.sb_offset 10)) [ uifill 0 $uiPad:M ]
								@@@@@@(.scoreboard.row 0 $.scoreboard.shelf.rest)
								if (< $n (+ $.sb_offset 0)) [ uifill 0 $uiPad:M ]
							] [
								if (&& [<= $n (+ $.sb_offset 10 $x $s)] [>= $n $.sb_offset]) [
									@@@@@@@(.scoreboard.row 0 $.scoreboard.shelf.rest)
								]
							]
						]
					]
				]
				uioffset $uiPad:2XL 0 [
					uialign 1
					// XXX what did I even mean to do here?
				]
			]
		]
	]
]

uiLiteMenu "scoreboard" [
	uiclamp.e
	uiallowinput 0
	refreshscoreboard
	uivlist $uiPad:5XL [
		uialign 0 -1
		if (m_timed $getmode) [ // var scoreboardtime otherwise
			uiFastImgStretched <rotate:3> "ui/" "scoreboard/" "shelf2" [
				uiFastImg <mad:0/0/0> "ui/" "scoreboard/" "glow" $uiPad:D2XL $uiPad:DM
				if $intermission [ uifontcolortext "mono" $timeremaining $c_orange 1.2 ] [
					uihlist $uiPad:O3 [
						uiFancyText "mono"       (div $timeremaining 60)    1.2
						uiFancyText "mono" ":"                              1.2
						uiFancyText "mono" (pad0 (mod $timeremaining 60) 2) 1.2
					]
				]
			] $uiPad:5XL $uiPad:5XL
		] [ uifill 0 $uiPad:DM ]
		if (m_teammode $getmode) [ .scoreboard.ctf ] [ .scoreboard.ffa ]
		.scoreboard.spec
	]
] [
	uivlist 0 [
		uiFancyText "wide" (getmodeprettyname $getmode) 1.2
		if $scoreboardmap [
			uiFancyText "wide" "- ON -" 0.8 $c_gray
			uiFancyText "wide" $scoreboardmap 1 $c_green
		] [
			uitext " " 0.8
			uitext " " 1
		]
	] ; uialign- 0 1
	uigroup [
		uialign 1 1
		uicolortext $scoreboardservinfo (|A! 0x48) 0.7
	]
] [
	uiSetBgBlur 1
	hidehud 1
	.sb_offset = 3
] [
	uiSetBgBlur -1
	hidehud 0
]
