
///////////////////////////////////////////////////////////////////////////////
//  Scoreboard                                                               //
///////////////////////////////////////////////////////////////////////////////

.scoreboard.aesir = [
	c_sbline = 0xB0284890
	uigroup [
		uialign -2 -1
		uifill 0 (*f $.UI_pad_5XL 3)
		uioffset 0 (+f (!f $.UI_pad_6XL) $.UI_pad_MS) [
			uialign -2 -1
			uifill $.sb_rowwidth (*f $.UI_pad_5XL 2) [
				UIfastimg "" "ui/" "scoreboard/" "scorebg"
				uiclamp*e
			]
			uioffset 0 (!f $.UI_pad_4XL) [
				uialign -1 -1
				uispace $.UI_pad_2XL 0 [
					uifontcolortext "wide_outline" (getteamname 1) 0x60FFFFFF 1.4
				]
			]
			uispace $.UI_pad_S -0.0025 [
				uialign 1 -1
				uifontcolortext "wide" (getteamscore 1) 0x4060D0 1
			]
		]
		uivlist 0 [
			uifill $.sb_rowwidth $.UI_pad_5XL [
				if (numscoreboard 1) [
					uispace $.UI_pad_M 0 [
						uialign 1
						uihlist $.UI_pad_3XL [
							looplist x $.scoreboardicons [
								uifill $.UI_pad_6XL 0 [ UIfastimg "" "ui/" "scoreboard/" $x $.UI_pad_3XL ]
							]
						]
					]
				]
			]
			loopscoreboard cn 1 [
				uicolor 0xB0203070 $.sb_rowwidth $.UI_pad_5XL [
					style_generic_gradient2
					UIfastimgtile "ui/" "ui_bg" 0
					uioutline $c_sbline
					uispace $.UI_pad_M 0 [
						uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.6
						uialign- -1
						uihlist 0 [
							uiclamp.y ; uialign 1
							looplist x $.scoreboardcells [ x ]
						]
					]
					uiclamp*e
				]
			]
			uifill 0 $.UI_pad_5XL
		]
	]
]

.scoreboard.vanir = [
	c_sbline = 0xB0842828
	uigroup [
		uialign -2 -1
		uifill 0 (*f $.UI_pad_5XL 3)
		uioffset 0 (+f (!f $.UI_pad_6XL) $.UI_pad_MS) [
			uialign -2 -1
			uifill $.sb_rowwidth (*f $.UI_pad_5XL 2) [
				UIfastimg <rotate:5> "ui/" "scoreboard/" "scorebg"
				uiclamp*e
			]
			uioffset 0 (!f $.UI_pad_4XL) [
				uialign 1 -1
				uispace $.UI_pad_2XL 0 [
					uifontcolortext "wide_outline" (getteamname 2) 0x60FFFFFF 1.4
				]
			]
			uispace $.UI_pad_S -0.0025 [
				uialign -1 -1
				uifontcolortext "wide" (getteamscore 2) 0xD04040 1
			]
		]
		uivlist 0 [
			uifill $.sb_rowwidth $.UI_pad_5XL [
				if (numscoreboard 2) [
					uispace $.UI_pad_M 0 [
						uialign -1
						uihlist $.UI_pad_3XL [
							looplist x $.scoreboardicons [
								uifill $.UI_pad_6XL 0 [ UIfastimg "" "ui/" "scoreboard/" $x $.UI_pad_3XL ]
							]
						]
					]
				]
			]
			loopscoreboard cn 2 [
				uicolor 0xB0702020 $.sb_rowwidth $.UI_pad_5XL [
					style_generic_gradient2
					UIfastimgtile "ui/" "ui_bg" 1
					uioutline $c_sbline
					uispace $.UI_pad_M 0 [
						uihlist 0 [
							uiclamp.y ; uialign -1
							//loop+* n 5 -1 6 [ do [cell@n] ]
							looplist x $.scoreboardcells [ x ]
						]
						uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.6
						uialign- 1
					]
					uiclamp*e
				]
			]
			uifill 0 $.UI_pad_5XL
		]
	]
]

.scoreboard.ffa = [
	c_sbline = 0xB0404040
	uigrid 2 $.UI_pad_DM 0 [
		loop n 2 [
			uifill $.sb_rowwidth $.UI_pad_5XL [
				if (> (numscoreboard 0) $n) [
					uispace $.UI_pad_M 0 [
						uialign 1
						uihlist $.UI_pad_3XL [
							looplist x $.scoreboardicons [
								uifill $.UI_pad_6XL 0 [ UIfastimg "" "ui/" "scoreboard/" $x $.UI_pad_3XL ]
							]
						]
					]
				]
			]
		]
		loopscoreboard cn 0 [
			uicolor 0xB0303030 $.sb_rowwidth $.UI_pad_5XL [
				style_generic_gradient2
				UIfastimgtile "ui/" "ui_bg" 0
				uioutline $c_sbline
				uispace $.UI_pad_M 0 [
					uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.6
					uialign- -1
					uihlist 0 [
						uiclamp.y ; uialign 1
						looplist x $.scoreboardcells [ x ]
					]
				]
				uiclamp*e
			]
		]
		loop n 2 [ uifill 0 $.UI_pad_5XL ]
	]
]

.scoreboard.spec = [
	if $scoreboardshowclientnum [ arg1 = $.UI_pad_DS ]
	arg1 = (+f $arg1 $.UI_pad_UXS)

	uigrid 3 $.UI_pad_L 0 [
		loopscoreboard cn -1 [
			uicolor 0xB0303840 $arg1 $.UI_pad_5XL [
				style_generic_gradient2
				UIfastimgtile "ui/" "ui_bg" 0
				uioutline 0xB0404860
				uispace $.UI_pad_M 0 [
					uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.6
					uialign- -1
					@@@(? $scoreboardshowclientnum [
						uifill $.UI_pad_6XL 0 [
							uialign 1
							uitext $cn 0.6
						]
					])
				]
				uiclamp*e
			]
		]
	]
]

UIquickdialog "scoreboard" [
	uiclamp.e
	uiallowinput 0
	refreshscoreboard
] [
	uivlist 0 [
		uialign 0 -1
		if (m_timed $getmode) [
			uihlist $.UI_pad_MS [
				local hue ; hue = (? (|| $intermission $paused) $c_orange $c_white)
				// var scoreboardtime otherwise
				uifontcolortext "mono"       (div $timeremaining 60)    $hue 1.2
				uifontcolortext "wide" ":"                              $hue 1.2
				uifontcolortext "mono" (pad0 (mod $timeremaining 60) 2) $hue 1.2
			]
		] [ uitext " " 1.2 ]
		uifill 0 $.UI_pad_DM
		uigroup [
			uifill 0 (*f $.UI_pad_5XL 3)
			if (m_teammode $getmode) [
				uioffset 0 (!f $.UI_pad_DSS) [
					uialign 0 -1
					uifont "wide_outline" [
						uioffset (!f $.UI_pad_2XL) (!f $.UI_pad_2XL) [ uitext "V" 1 ]
						uioffset     $.UI_pad_2XL      $.UI_pad_2XL  [ uitext "S" 1 ]
					]
				]
				uihlist $.UI_pad_DM [
					.scoreboard.aesir
					.scoreboard.vanir
				]
			] [ .scoreboard.ffa ]
		]
		.scoreboard.spec
	]
	uigroup [
		uialign 0 1
		uifont "wide_outline" [
			uivlist 0 [
				uitext (getmodeprettyname $getmode) 1.2
				uifill 0 $.UI_pad_MS
				if $scoreboardmap [
					uicolortext "- ON -" 0x60FFFFFF 0.7
					uitext $scoreboardmap 1
				] [
					uitext " " 0.7
					uitext " " 1
				]
			]
		]
	]
	uigroup [
		uialign 1 1
		uicolortext $scoreboardservinfo 0x48FFFFFF 0.7
	]
	//uitext $getfps 2 ; uialign- 1 1
] [
	UIsetbgblur 1
	hidehud 1
] [
	UIsetbgblur -1
	hidehud 0
]

.refreshcells = [
	local bit cc

	if $getmode       [ bit = (^ $bit 0x01) ; ++ cc ]
	if $showkills     [ bit = (^ $bit 0x02) ; ++ cc ]
	if $showdeaths    [ bit = (^ $bit 0x04) ; ++ cc ]
	if $scoreboardmultiplayer [
		if $showpj    [ bit = (^ $bit 0x08) ; ++ cc ]
		if $showping  [ bit = (^ $bit 0x10) ; ++ cc ]
	]
	if $showclientnum [ bit = (^ $bit 0x20) ; ++ cc ]
	//               name cell size   +   cell size * cc   +   cell spacing * cc
	.sb_rowwidth  = (+f $.UI_pad_UXS (*f $.UI_pad_6XL $cc) (*f $.UI_pad_3XL $cc))

	.scoreboardcells = (loopconcat n 6 [
		if (& $bit (<< 1 $n)) [ concat (
			? (&& $n [& $bit (- (<< 1 $n) 1)]) [[ UIbar 0 1 $.UI_pad_L 0 $c_sbline ]]
		) [[ uifill $.UI_pad_6XL 0 [@@@@(at [
			[ uitext (getclientscore $cn)  0.6 ]
			[ uitext (getclientfrags $cn)  0.6 ]
			[ uitext (getclientdeaths $cn) 0.6 ]
			[ if (scoreboardpj $cn)   [ uitext (scoreboardpj $cn)   0.6 ] [ uifonttext "wide" "^f4-" 0.6 ] ]
			[ if (scoreboardping $cn) [ uitext (scoreboardping $cn) 0.6 ] [ uifonttext "wide" "^f4-" 0.6 ] ]
			[ uitext $cn 0.6 ]
		] $n)] ]] ]
	])
	.scoreboardicons = (loopconcat n 6 [
		if (& $bit (<< 1 $n)) [ at [
			@@@(at "0 star star flag star crown crown star" $getmode)
			"frag" "death" "pj" "ping" "cn"
		] $n ]
	])
]
