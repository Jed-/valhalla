
///////////////////////////////////////////////////////////////////////////////
//  Scoreboard                                                               //
///////////////////////////////////////////////////////////////////////////////


.refreshscoreboard = [
	local bit cc .scoreboard.name .scoreboard.header .scoreboard.row .scoreboard.shelf.top3 .scoreboard.shelf.rest

	if $getmode       [ bit = (^ $bit 0x01) ; ++ cc ]
	if $showkills     [ bit = (^ $bit 0x02) ; ++ cc ]
	if $showdeaths    [ bit = (^ $bit 0x04) ; ++ cc ]
	if $scoreboardmultiplayer [
		if $showpj    [ bit = (^ $bit 0x08) ; ++ cc ]
		if $showping  [ bit = (^ $bit 0x10) ; ++ cc ]
	]
	if $showclientnum [ bit = (^ $bit 0x20) ; ++ cc ]

	if (!= (getalias .sb_bitflags) $bit) [
		.sb_bitflags = $bit ; .sb_init = 0
	]
	//               name cell size   +   cell size * cc   +   cell spacing * cc
	.sb_rowwidth  = (+f $.UI_pad_UXS (*f $.UI_pad_6XL $cc) (*f $.UI_pad_3XL $cc))

	.scoreboard.cells = (loopconcat n 6 [
		if (& $bit (<< 1 $n)) [ concat (
			? (&& $n [& $bit (- (<< 1 $n) 1)]) [[ UIbar 0 1 $.UI_pad_L 0 $c_sbline ]]
		) [[ uifill $.UI_pad_6XL 0 [@@@@(at [
			[ uitext (getclientscore  $cn) 0.6 ]
			[ uitext (getclientfrags  $cn) 0.6 ]
			[ uitext (getclientdeaths $cn) 0.6 ]
			[ if (scoreboardpj $cn)   [ uitext (scoreboardpj $cn)   0.6 ] [ uifonttext "wide" "^f4-" 0.6 ] ]
			[ if (scoreboardping $cn) [ uitext (scoreboardping $cn) 0.6 ] [ uifonttext "wide" "^f4-" 0.6 ] ]
			[ uitext $cn 0.6 ]
		] $n)] ]] ]
	])

	.scoreboard.icons = (loopconcat n 6 [
		if (& $bit (<< 1 $n)) [ at [
			@@@(at "0 star star flag star crown crown star" $getmode)
			"frag" "death" "pj" "ping" "cn"
		] $n ]
	])

	if (|| $arg1 [! $.sb_init]) [
		.sb_init = 1

		.scoreboard.name = [
			arg1 = (= $arg1 2)
			result [
				uifill 0 (*f $.UI_pad_5XL 3)
				uioffset 0 (+f (!f $.UI_pad_6XL) $.UI_pad_MS) [
					uialign -2 -1
					uifill $.sb_rowwidth (*f $.UI_pad_5XL 2) [
						UIfastimg <reorient:@@@arg1> "ui/" "scoreboard/" "scorebg"
						uiclamp*e
					]
					uioffset 0 (!f $.UI_pad_4XL) [
						uialign @@@(? $arg1 1 -1) -1
						uispace $.UI_pad_2XL 0 [
							uifontcolortext "wide_outline" @@@@arg2 0x60FFFFFF 1.4
						]
					]
					uispace $.UI_pad_S -0.0025 [
						uialign @@@(? $arg1 -1 1) -1
						uifontcolortext "wide" (@@@arg3) @@@arg4 1
					]
				]
			]
		]

		.scoreboard.header = [
			result [
				uifill $.sb_rowwidth $.UI_pad_5XL [
					if (numscoreboard @@arg1) [
						uispace $.UI_pad_M 0 [
							@@@@(? (= $arg1 2) [
								uialign -1
								uihlist $.UI_pad_3XL [
									@@(looplistconcatrev x $.scoreboard.icons [ result [
										uifill $.UI_pad_6XL 0 [ UIfastimg "" "ui/" "scoreboard/" @@x $.UI_pad_3XL ]
									] ])
								]
							] [
								uialign 1
								uihlist $.UI_pad_3XL [
									@@(looplistconcat x $.scoreboard.icons [ result [
										uifill $.UI_pad_6XL 0 [ UIfastimg "" "ui/" "scoreboard/" @@x $.UI_pad_3XL ]
									] ])
								]
							])
						]
					]
				]
			]
		]

		// arg1: team, arg2: children, arg3: custom cells, arg4: custom width
		.scoreboard.row = [
			arg1 = (= $arg1 2)
			if $arg2 [ arg2 = [ uigroup [@@arg2] ] ]
			if $arg4 [] [ arg4 = $.sb_rowwidth ]
			result [
				uicolor $c_sbbody @arg4 $.UI_pad_5XL [
					@@arg2
					style_generic_gradient2
					UIfastimgtile "ui/" "ui_bg" @@arg1
					uioutline (+ (? (scoreboardhighlight $cn) 0x202020) $c_sbline)
					uispace $.UI_pad_M 0 [
						@@@(? $arg1 [
							uihlist 0 [
								uiclamp.y ; uialign -1
								@@(looplistconcatrev x $.scoreboard.cells [ result [ @x; ] ])
							]
							uifontcolortext "default_outline" (getclientcolorname $cn) (scoreboardstatus $cn) 0.6
							uialign- 1
						] [
							uifontcolortext "default_outline" (getclientcolorname $cn) (scoreboardstatus $cn) 0.6
							uialign- -1
							@(? (!=s $arg3 "") $arg3 [
								uihlist 0 [
									uiclamp.y ; uialign 1
									@@(looplistconcat x $.scoreboard.cells [ result [ @x; ] ])
								]
							])
						])
					]
					uiclamp*e
				]
			]
		]

		.scoreboard.aesir = [
			c_sbbody = 0xB0203070
			c_sbline = 0xB0284890

			uigroup [
				uialign -2 -1
				@@(.scoreboard.name 1 (getteamname 1) [getteamscore 1] 0x4060D0)
				uivlist 0 [
					@@@(.scoreboard.header 1)
					loopscoreboard cn 1 [ @@@@(.scoreboard.row 1) ]
					uifill 0 $.UI_pad_5XL
				]
			]
		]

		.scoreboard.vanir = [
			c_sbbody = 0xB0702020
			c_sbline = 0xB0842828

			uigroup [
				uialign -2 -1
				@@(.scoreboard.name 2 (getteamname 2) [getteamscore 2] 0xD04040)
				uivlist 0 [
					@@@(.scoreboard.header 2)
					loopscoreboard cn 2 [ @@@@(.scoreboard.row 2) ]
					uifill 0 $.UI_pad_5XL
				]
			]
		]

		.scoreboard.shelf.top3 = [
			uioffset (!f $.UI_pad_5XL) 0 [
				uialign -1
				UIfastimg "" "ui/" "scoreboard/" "shelf1" $.UI_pad_5XL
				push n (at [<mad:1/.8/0> <mad:.8/.8/.8> <mad:.8/.5/0>] $n) [
					UIfastimg $n "ui/" "scoreboard/" "glow"  $.UI_pad_5XL
					UIfastimg $n "ui/" "scoreboard/" "crown" $.UI_pad_3XL
				]
			]
		]
		.scoreboard.shelf.rest = [
			arg1 = (= $cn $getclientnum)
			uioffset (!f $.UI_pad_5XL) 0 [
				uialign -1
				UIfastimg "" "ui/" "scoreboard/shelf" (- 2 $arg1) $.UI_pad_5XL
				UIfastimg <mad:.18/.18/.18> "ui/" "scoreboard/" "glow"  $.UI_pad_5XL
				uifontcolortext "default_outline" (+ $n 1) (? $arg1 0xB0FFFFFF 0x60FFFFFF) 0.5
			]
		]

		.scoreboard.ffa = [
			c_sbbody = 0xB0303030
			c_sbline = 0xB0404040

			local n x

			uigroup [
				uialign 0 -1
				@@(.scoreboard.name 0 "FFA" 0 0x40D060)
				uivlist 0 [
					@@@(.scoreboard.header 0)
					loopscoreboard cn 0 [
						if (< $n 3) [
							if (= $cn $getclientnum) [ x = 1 ]
							@@@@@(.scoreboard.row 0 $.scoreboard.shelf.top3)
							if (= $n 2) [ uifill 0 $.UI_pad_M ]
						] [
							if (= $cn $getclientnum) [
								if (> $n (+ $.sb_offset 11)) [ uifill 0 $.UI_pad_M ]
								@@@@@@(.scoreboard.row 0 $.scoreboard.shelf.rest)
								if (< $n (+ $.sb_offset -1)) [ uifill 0 $.UI_pad_M ]
							] [
								if (&& [<= $n (+ $.sb_offset 10 $x)] [>= $n $.sb_offset]) [
									@@@@@@@(.scoreboard.row 0 $.scoreboard.shelf.rest)
								]
							]
						]
						++ n
					]
					uifill 0 $.UI_pad_5XL
				]
				uioffset $.UI_pad_2XL 0 [

				]
			]
		]

		.scoreboard.spec = [
			c_sbbody = 0xB0303840
			c_sbline = 0xB0404860

			uigrid 3 $.UI_pad_L 0 [
				loopscoreboard cn -1 [
					@@@(.scoreboard.row -1 [] (
						? $scoreboardshowclientnum [
							uifill $.UI_pad_6XL 0 [
								uialign 1
								uitext $cn 0.6
							]
						]
					) (+f (? $scoreboardshowclientnum $.UI_pad_DS) $.UI_pad_UXS))
				]
			]
		]
	]
]

UIquickdialog "scoreboard" [
	uiclamp.e
	uiallowinput 0
	refreshscoreboard
] [
	uivlist 0 [
		uialign 0 -1
		if (m_timed $getmode) [
			uihlist $.UI_pad_MS [
				local hue ; hue = (? (|| $intermission $paused) $c_orange $c_white)
				// var scoreboardtime otherwise
				uifontcolortext "mono"       (div $timeremaining 60)    $hue 1.2
				uifontcolortext "wide" ":"                              $hue 1.2
				uifontcolortext "mono" (pad0 (mod $timeremaining 60) 2) $hue 1.2
			]
		] [ uitext " " 1.2 ]
		uifill 0 $.UI_pad_DM
		uigroup [
			uifill 0 (*f $.UI_pad_5XL 3)
			if (m_teammode $getmode) [
				uioffset 0 (!f $.UI_pad_DSS) [
					uialign 0 -1
					uifont "wide_outline" [
						uioffset (!f $.UI_pad_2XL) (!f $.UI_pad_2XL) [ uitext "V" 1 ]
						uioffset     $.UI_pad_2XL      $.UI_pad_2XL  [ uitext "S" 1 ]
					]
				]
				uihlist $.UI_pad_DM [
					.scoreboard.aesir
					.scoreboard.vanir
				]
			] [ .scoreboard.ffa ]
		]
		.scoreboard.spec
	]
	uigroup [
		uialign 0 1
		uifont "wide_outline" [
			uivlist 0 [
				uitext (getmodeprettyname $getmode) 1.2
				uifill 0 $.UI_pad_MS
				if $scoreboardmap [
					uicolortext "- ON -" 0x60FFFFFF 0.7
					uitext $scoreboardmap 1
				] [
					uitext " " 0.7
					uitext " " 1
				]
			]
		]
	]
	uigroup [
		uialign 1 1
		uicolortext $scoreboardservinfo 0x48FFFFFF 0.7
	]
	//uitext $getfps 2 ; uialign- 1 1
] [
	UIsetbgblur 1
	hidehud 1
	.sb_offset = 3
] [
	UIsetbgblur -1
	hidehud 0
]
