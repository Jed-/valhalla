
///////////////////////////////////////////////////////////////////////////////
//  Scoreboard                                                               //
///////////////////////////////////////////////////////////////////////////////

.scoreboard.update = [
	local bit cc

	if $getmode                 [ bit = (^ $bit 0x01) ; ++ cc ] // score
	if $showkills               [ bit = (^ $bit 0x02) ; ++ cc ] // frags
	if $showdeaths              [ bit = (^ $bit 0x04) ; ++ cc ] // deaths
	if $scoreboardmultiplayer [
		if $showpj              [ bit = (^ $bit 0x08) ; ++ cc ] // packet jump
		if $showping            [ bit = (^ $bit 0x10) ; ++ cc ] // ping
	]

	if (|| $arg1 [!= (getalias .sb_bitmask) $bit] [!= (getalias .sb_modemask) $bit]) [
		.sb_bitmask  = $bit
		.sb_modemask = $getmode

		local .sb_namewidth .sb_cellwidth .sb_rowwidth
		local .scoreboard.cells .scoreboard.icons
		local .scoreboard.header .scoreboard.row
		local .scoreboard.shelf.top3 .scoreboard.shelf.rest

		.sb_namewidth = $.UI_pad_UXS
		.sb_cellwidth = (+f $.UI_pad_DSS $.UI_pad_M)
		.sb_rowwidth  = (+f $.sb_namewidth (*f $.sb_cellwidth $cc))

		loop n 5 [ if (& $bit (<< 1 $n)) [
			append .scoreboard.cells [[uifill @@[.sb_cellwidth] 0 [@@@(at [
				[uitext (clamp (getclientscore  $cn)    0 9999) 0.58]
				[uitext (clamp (getclientfrags  $cn) -999 9999) 0.58]
				[uitext (clamp (getclientdeaths $cn)    0 9999) 0.58]
				[if (scoreboardpj $cn)   [uitext (clamp (scoreboardpj   $cn) 0 9999) 0.58] [uifonttext "wide" "^f4--" 0.58]]
				[if (scoreboardping $cn) [uitext (clamp (scoreboardping $cn) 0 9999) 0.58] [uifonttext "wide" "^f4--" 0.58]]
			] $n)]]]
			append .scoreboard.icons (at [
				@@@(at "0 star star flag star crown crown star" $getmode)
				"frag" "death" "pj" "ping"
			] $n)
		] ]

		// arg1: team, arg2: team name, arg3: [score], arg4: color
		.scoreboard.header = [
			arg4 = (= $arg1 2) // horizontal flip bool
			arg5 = (at [0 0x40D060 0x4060D0 0xD04040] (+ $arg1 1))
			result [
				uifill @[.sb_rowwidth] @[.UI_pad_DM] [
					uispace 0 @@[.UI_pad_S] [
						UIfastimg <reorient:@@@arg4> "ui/" "scoreboard/" "scorebg"
						uiclamp-e
					] ; uiclamp-e
					uispace @@[.UI_pad_M] @@(!f $.UI_pad_M) [
						uifontcolortext "wide_outline" @@@arg2 0x60FFFFFF 1.4
					] ; uialign- @@(? $arg4 1 -1) -1
					uispace @@[.UI_pad_ML] @@(*f $.UI_pad_ML 3) [
						uifontcolortext "wide" (@@@arg3) @@@arg5 1
					] ; uialign- @@(? $arg4 -1 1) -1
					if (numscoreboard @@arg1) [
						uihlist 0 [
							@@@@(? $arg4 [
								uialign -1 1
								@(looplistconcatrev x $.scoreboard.icons [ result [
									uifill @@[.sb_cellwidth] @@(*f $.UI_pad_MM 7) [
										UIfastimg "" "ui/" "scoreboard/" @@x @@@[.UI_pad_3XL]
									]
								] ])
							] [
								uialign 1 1
								@(looplistconcat x $.scoreboard.icons [ result [
									uifill @@[.sb_cellwidth] @@(*f $.UI_pad_MM 7) [
										UIfastimg "" "ui/" "scoreboard/" @@x @@@[.UI_pad_3XL]
									]
								] ])
							])
						]
					]
				]
			]
		]

		// arg1: team, arg2: children, arg3: custom cells, arg4: custom width
		.scoreboard.row = [
			arg4 = (= $arg1 2) // horizontal flip bool
			arg5 = (at [
				[<mad:.19/.22/.25> <mad:.19/.22/.25>] // spec
				[<mad:.19/.19/.19> <mad:.16/.16/.16>] // ffa
				[<mad:.11/.18/.44> <mad:.07/.14/.40>] // aesir
				[<mad:.44/.11/.11> <mad:.40/.07/.07>] // vanir
			] (+ $arg1 1))
			result [
				UIfastimg (at [@@arg5] (& $n 1)) "ui/" "scoreboard/" "row" @(? $arg3 $arg3 $.sb_rowwidth) @[.UI_pad_5XL] [
					@@(? $arg2 [ uigroup [ uiclamp.e ; @@arg2 ] ])
					if (scoreboardhighlight $cn) [
						uispace @@@[.UI_pad_MS] @@@[.UI_pad_MS] [
							UIfastimg <reorient:@@@@arg4> "ui/" "scoreboard/" "edge" @@@@[.UI_pad_M]
						] ; uialign- @@@(? $arg4 1 -1) -1
					]
					@@(? $arg4 [
						uihlist 0 [
							@@(looplistconcatrev x $.scoreboard.cells [ result [ @x; ] ])
						] ; uiclamp-y ; uialign- -1
						uispace @[.UI_pad_M] 0 [
							uifontcolortext "default_outline" (getclientcolorname $cn) (scoreboardstatus $cn) 0.6
						] ; uialign- 1
					] [
						uispace @[.UI_pad_M] 0 [
							uifontcolortext "default_outline" (getclientcolorname $cn) (scoreboardstatus $cn) 0.6
						] ; uialign- -1
						@(? (> $arg1 -1) [
							uihlist 0 [
								@@(looplistconcat x $.scoreboard.cells [ result [ @x; ] ])
							] ; uiclamp-y ; uialign- 1
						])
					])
				] ; ++ n
			]
		]

		if (m_ctf $getmode) [
			.scoreboard.aesir = [
				local n
				uivlist 0 [
					@@(.scoreboard.header 1 (getteamname 1) [getteamscore 1])
					loopscoreboard cn 1 [ @@@(.scoreboard.row 1) ]
				] ; uialign- -2 -1
			]
			.scoreboard.vanir = [
				local n
				uivlist 0 [
					@@(.scoreboard.header 2 (getteamname 2) [getteamscore 2])
					loopscoreboard cn 2 [ @@@(.scoreboard.row 2) ]
				] ; uialign- -2 -1
			]
			.scoreboard.ctf = [
				uigroup [
					uioffset 0 @@[.UI_pad_ML] [
						uifont "wide_outline" [
							uioffset @@@@(!f $.UI_pad_2XL) @@@@(!f $.UI_pad_2XL) [ uitext "V" 1 ]
							uioffset     @@@@[.UI_pad_2XL]     @@@@[.UI_pad_2XL] [ uitext "S" 1 ]
						]
					] ; uialign- 0 -1
					uihlist @@[.UI_pad_DM] [
						.scoreboard.aesir
						.scoreboard.vanir
						uialign* -2 -1
					]
				]
			]
		]

		.scoreboard.spec = [
			uigrid 3 @[.UI_pad_L] 0 [
				loopscoreboard cn -1 [
					@@@(.scoreboard.row -1 [] $.sb_namewidth)
				]
			]
		]

		.scoreboard.shelf.top3 = [
			uioffset @(+f 0.0015 (!f $.UI_pad_5XL)) 0 [
				UIfastimg "" "ui/" "scoreboard/" "shelf1" $.UI_pad_5XL
				push n (at [<mad:1/.8/0> <mad:.8/.8/.8> <mad:.8/.5/0>] $n) [
					UIfastimg $n "ui/" "scoreboard/" "glow"  $.UI_pad_5XL
					UIfastimg $n "ui/" "scoreboard/" "crown" $.UI_pad_3XL
				]
			] ; uialign- -1
		]
		.scoreboard.shelf.rest = [
			arg1 = (= $cn $getclientnum)
			uioffset @(+f 0.0015 (!f $.UI_pad_5XL)) 0 [
				UIfastimg "" "ui/" "scoreboard/shelf" (- 2 $arg1) $.UI_pad_5XL
				UIfastimg <mad:.18/.18/.18> "ui/" "scoreboard/" "glow"  $.UI_pad_5XL
				uifontcolortext "default_outline" (+ $n 1) (? $arg1 0xB0FFFFFF 0x60FFFFFF) 0.5
			] ; uialign- -1
		]

		.scoreboard.ffa = [
			local n x s
			s = (isspectator $getclientnum)
			uigroup [
				uivlist 0 [
					@@@(.scoreboard.header 0 "FFA" 0)
					loopscoreboard cn 0 [
						if (< $n 3) [
							if (= $cn $getclientnum) [ x = 1 ]
							@@@@@(.scoreboard.row 0 $.scoreboard.shelf.top3)
							if (= $n 3) [ uifill 0 $.UI_pad_M ]
						] [
							if (= $cn $getclientnum) [
								if (> $n (+ $.sb_offset 10)) [ uifill 0 $.UI_pad_M ]
								@@@@@@(.scoreboard.row 0 $.scoreboard.shelf.rest)
								if (< $n (+ $.sb_offset 0)) [ uifill 0 $.UI_pad_M ]
							] [
								if (&& [<= $n (+ $.sb_offset 10 $x $s)] [>= $n $.sb_offset]) [
									@@@@@@@(.scoreboard.row 0 $.scoreboard.shelf.rest)
								]
							]
						]
					]
				]
				uioffset $.UI_pad_2XL 0 [
					uialign 1
					// XXX
				]
			]
		]
	]
]

UIquickdialog "scoreboard" [
	uiclamp.e
	uiallowinput 0
	refreshscoreboard
	uivlist $.UI_pad_5XL [
		uialign 0 -1
		if (m_timed $getmode) [ // var scoreboardtime otherwise
			UIfastimgstretched <rotate:3> "ui/" "scoreboard/" "shelf2" [
				UIfastimg <mad:0/0/0> "ui/" "scoreboard/" "glow" $.UI_pad_D2XL $.UI_pad_DM
				if $intermission [ uifontcolortext "mono" $timeremaining $c_orange 1.2 ] [
					uihlist $.UI_pad_MS [
						uifonttext "mono"       (div $timeremaining 60)    1.2
						uifonttext "wide" ":"                              1.2
						uifonttext "mono" (pad0 (mod $timeremaining 60) 2) 1.2
					]
				]
			] $.UI_pad_5XL $.UI_pad_5XL
		] [ uifill 0 $.UI_pad_DM ]
		if (m_teammode $getmode) [ .scoreboard.ctf ] [ .scoreboard.ffa ]
		.scoreboard.spec
	]
] [
	uifont "wide_outline" [
		uialign 0 1
		uivlist 0 [
			uitext (getmodeprettyname $getmode) 1.2
			uifill 0 $.UI_pad_MS
			if $scoreboardmap [
				uicolortext "- ON -" 0x60FFFFFF 0.7
				uitext $scoreboardmap 1
			] [
				uitext " " 0.7
				uitext " " 1
			]
		]
	]
	uigroup [
		uialign 1 1
		uicolortext $scoreboardservinfo 0x48FFFFFF 0.7
	]
	//uitext $getfps 2 ; uialign- 1 1
] [
	UIsetbgblur 1
	hidehud 1
	.sb_offset = 3
] [
	UIsetbgblur -1
	hidehud 0
]
