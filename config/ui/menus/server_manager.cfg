
uiMenu "cci" [
	local numspec numplay numblue numred
	if $mainmenu [ hideui "cci" ] [
		cp_updateselected (listclients 1)
	]
//  looplist cn (listclients 1) [
//      if (isspectator $cn) [
//          numspec = (+ $numspec 1)
//      ] [
//          numplay = (+ $numplay 1)
//          case (getclientteam $cn) 1 [
//              numblue = (+ $numblue 1)
//          ] 2 [ numred = (+ $numred 1) ]
//      ]
//  ]

	uivlist $uiPad:L [
	    uiButton "" [
            uitext (? (isprivileged $getclientnum) "Relinquish Privileges" "Claim Privileges") 0.6
        ] $uiPad:3XL $uiPad:6XL [
            if (isprivileged $getclientnum) [
                setmaster 0
            ] [
                setmaster 1
            ]
        ] 0 [ uiSetTip "" "Claim master privilege to manage players and master modes" ]
        if (isprivileged $getclientnum) [
	        uihlist $uiPad:L [
                uiButton "hold2" [
                    uiRadio [= $getmastermode 0]
                    uitext "^f0Open" 0.6
                ] 0 0 [ mastermode 0 ] 0 [ uiSetTip "" "Everyone can join, vote and claim privileges" ]
                uiButton "hold2" [
                    uiRadio [= $getmastermode 1]
                    uitext "^f2Veto" 0.6
                ] 0 0 [ mastermode 1 ] 0 [ uiSetTip "" "Open joining and voting for all, with the option for privileged players to override" ]
                uiButton "hold2" [
                    uiRadio [= $getmastermode 2]
                    uitext "^f4Locked" 0.6
                ] 0 0 [ mastermode 2 ] 0 [ uiSetTip "" "New joining players spectate by default, privileged players can select who to unspectate" ]
                uiButton "hold2" [
                    uiRadio [= $getmastermode 3]
                    uitext "^f3Private" 0.6
                ] 0 0 [ mastermode 3 ] 0 [ uiSetTip "" "Private server: player join disabled" ]
            ]
        ]
		uihlist $uiPad:L [
			uiButton "hold2" [
				uiCheckbox $.cci_showspec
				uitext "Show Spectators" 0.6
			] $uiPad:D2XL 0 [
				.cci_showspec (! $.cci_showspec)
				listfilter= .cci_clients i [! (isspectator $i)]
			] -1
			uiButton "hold2" [
				uiCheckbox $.cci_showbots
				uitext "Show Bots" 0.6
			] $uiPad:D2XL 0 [
				.cci_showbots (! $.cci_showbots)
				listfilter= $.cci_clients i [! (isai $i)]
			] -1
			uiButton "hold2" [
				uiCheckbox $.cci_showself
				uitext "Show Self" 0.6
			] $uiPad:D2XL 0 [
				.cci_showself (! $.cci_showself)
				listdel= .cci_clients $getclientnum
			] -1
		]
		uiScrollBox cci $uiPad:UM (*f $uiPad:5XL 11) [
            uihlist $uiPad:L [
                uiclamp.y
                uialign -2 -1
                uivlist 0 [
                    if (m_teammode $getmode) [
                        cp_clientpanel 1 0
                        cp_clientpanel 1 1
                    ] [ cp_clientpanel 0 0 ]
                ]
                uivlist 0 [
                    if (m_teammode $getmode) [
                        cp_clientpanel 2 0
                        cp_clientpanel 2 1
                    ] [ cp_clientpanel 0 1 ]
                ]
                uialign* -2 -1
            ]
        ] 0xF (*f $uiPad:D5XL 2) // (bool: check if either column has <12 entries)
		uihlist $uiPad:L [
		    if (> (listlen $.cci_clients) 1) [
		        uiButton "" [
		            uicolortext "Whisper" $c_gray 0.6
			    ] $uiPad:3XL $uiPad:6XL [
			    ] 0 [ uiSetTip "" "You cannot send a whisper to more than one player simultaneously" ]
            ] [
                uiButton "" [
			        uicolortext "Whisper" $c_magenta 0.6
			    ] $uiPad:3XL $uiPad:6XL [
                    saycommand (+s "/whisper " (getclientname $.cci_clients) " ")
			    ] 0 [ uiSetTip "" "Send a whisper message to a single player (PM)" ]
            ]
			uiButton "" [
				uitext "Ignore" 0.6
			] $uiPad:3XL $uiPad:6XL [
			    listfilter= .cci_clients i [! (isai $i)]
                looplist cn $.cci_clients [ ignore $cn ]
			] 0 [ uiSetTip "" "Ignore player (block chat messages)" ]
			uiButton "" [
				uitext "Unignore" 0.6
			] $uiPad:3XL $uiPad:6XL [
			    listfilter= .cci_clients i [! (isai $i)]
                looplist cn $.cci_clients [ unignore $cn ]
			] 0 [ uiSetTip "" "Unblock player" ]
        ]
        if (isprivileged $getclientnum) [
            uihlist $uiPad:L [
                uiBar 1
			    uiButton "" [
				    uitext "Kick" 0.6
			    ] $uiPad:3XL $uiPad:6XL [
				    listfilter= .cci_clients i [&& [! (isai $i)] [!= $i $getclientnum]]
				    selected_kick = $.cci_clients
				    looplist cn $.cci_clients [ kick $cn ]
			    ] 0 [ uiSetTip ccikick "Kick player" ]
			    uiButton "" [
				    uitext "Mute" 0.6
			    ] $uiPad:3XL $uiPad:6XL [
				    listfilter= .cci_clients i [! (isai $i)]
				    looplist cn $.cci_clients [ mute $cn ]
			    ] 0 [ uiSetTip ccispec1 "Disable player from sending messages" ]
			    uiButton "" [
				    uitext "Unmute" 0.6
			    ] $uiPad:3XL $uiPad:6XL [
				    listfilter= .cci_clients i [! (isai $i)]
				    looplist cn $.cci_clients [ unmute $cn ]
			    ] 0 [ uiSetTip ccispec1 "Allow player to send messages" ]
			    uiButton "" [
				    uitext "Spectate" 0.6
			    ] $uiPad:3XL $uiPad:6XL [
				    listfilter= .cci_clients i [! (isai $i)]
				    looplist cn $.cci_clients [ spectator 1 $cn ]
			    ] 0 [ uiSetTip ccispec1 "Put player into spectator mode" ]
			    uiButton "" [
				    uitext "Unspectate" 0.6
			    ] $uiPad:3XL $uiPad:6XL [
				    listfilter= .cci_clients i [! (isai $i)]
				    looplist cn $.cci_clients [ spectator 0 $cn ]
			    ] 0 [ uiSetTip ccispec0 "Put player out of spectator mode" ]
			    uiButton "" [
				    uitext "Swap Team" 0.6
			    ] $uiPad:3XL $uiPad:6XL [
				    looplist cn $.cci_clients [ case (getclientteam $cn) 1 [ setteam $cn 2 ] 2 [ setteam $cn 1 ] ]
			    ] 0 [ uiSetTip cciteam "Swap teams for player" ]
			    uiButton "" [
				    uitext "Promote" 0.6
			    ] $uiPad:3XL $uiPad:6XL [
				    listfilter= .cci_clients i [! (isai $i)]
				    looplist cn $.cci_clients [ setmaster 1 $cn ]
			    ] 0 [ uiSetTip cciteam "Grant the player immediate server privileges" ]
            ]
		]
	]
] [] [] [] [ uitext "Manage Players" 0.8 ]

////////////////////////////////////////////////////////////////////////////////////////////////////


cp_updateselected = [ .cci_clients (listintersect $.cci_clients $arg1) ]

cp_client = [
	local selected ; selected = (>= (indexof $.cci_clients $arg1) 0)
	uiButtonArea (
			? $selected "hold1" "hold2"
		) (-f $uiPad:D6XL $uiPad:L) $uiPad:5XL [
		uirelease [
			if $selected [
				listdel= .cci_clients $arg1
			] [
			    if (iskeyheld "LCTRL") [
			        append= .cci_clients $arg1
                ] [
                    .cci_clients = $arg1
                ]
            ]
		]
		uispace $uiPad:S 0 [
			uiclamp.x
			uihlist 0 [
				uiclamp.x
				uicolortext (getclientcolorname $arg1) (scoreboardstatus $arg1) 0.55
				if $showclientnum [
					if (isai $arg1) [
						uitext (+s (? $selected "^f8" "^f4") "BOT") 0.55
					] [ uitext (+s (? $selected "^f8" "^f7") $arg1) 0.6 ]
					uialign- 1
				]
			]
		]
	]

]

cp_clientpanel = [
	uifill (-f $uiPad:D6XL $uiPad:L)
	looplist cn (
		listfilter i (listclients $.cci_showself) [
			&& [
				if $arg2 [isspectator $i] [! (isspectator $i)]
			] [
				case $arg1 1 [= (getclientteam $i) 1] 2 [= (getclientteam $i) 2] () 1
			] [
				if $.cci_showbots [result 1] [! (isai $i)]
			]
		]
	) [ cp_client $cn $arg1 $arg2 ]
]

////////////////////////////////////////////////////////////////////////////////////////////////////


uiMenu "bots" [
	uivlist $uiPad:L [
		uiOptionRow 0 [ uitext "Skill" 0.6 ] 0 [
		    uiOptionSld .botskill [
		        0   "RANDOM"          $c_gray
                1   "TARGET PRACTICE" $c_green
                25  "MORON"           $c_cyan_t
                50  "DECENT"          $c_blue
                75  "NORMAL"          $c_white
                86  "ADEPT"           $c_yellow
                92  "ULTRA"           $c_orange
                96  "TRAUMA"          $c_red
                101 "AIMBOT"          $c_magenta
            ]
        ] 0 [ uiSetTip "" "Adjust bot skill level" ]
        uihlist $uiPad:L [
            uiButton "" [
                uitext "Add bot" 0.6
            ] $uiPad:3XL $uiPad:6XL [
                botadd $.botskill
            ] 0 [ uiSetTip botadd "Add bot with desired skill" ]
            uiButton "" [
                uitext "Delete bot" 0.6
            ] $uiPad:3XL $uiPad:6XL [
                botdel
            ] 0 [ uiSetTip botdel "Remove the most recently added bot" ]
        ]
	]
] [] [] [] [ uitext "Add bots" 0.8 ]

////////////////////////////////////////////////////////////////////////////////////////////////////


uiMenu "serverdisconnect" [
	uivlist $uiPad:L [
		uitext " Your client was disconnected. " 0.7
		uitext (+s "Reason: ^f3" $lastdisconnectreason) 0.6
		uiButton "" [
            uitext "OK" 0.6
        ] $uiPad:3XL $uiPad:6XL [
            hideui "serverdisconnect"
        ]
	]
]
