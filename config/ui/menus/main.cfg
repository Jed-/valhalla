
UImenu "main" [
	uieschide (! $mainmenu)
	uihlist 0 [
		uivlist 0 [
			uifill $uiPad:D3XL
			UIbutton "hold2" [ uitext "Server Browser" 0.6 ] 0 $uiPad:6XL [ changeui "servers" ]
			UIbutton "hold2" [ uitext "Map Browser"    0.6 ] 0 $uiPad:6XL [ changeui "map_browser" ]
			UIbar 1 0 0 $uiPad:L
			UIbutton "hold2" [ uitext "Player Profile" 0.6 ] 0 $uiPad:6XL [ changeui "profile" ]
			UIbutton "hold2" [ uitext "Options"        0.6 ] 0 $uiPad:6XL [ changeui "options" ]
			UIbutton "hold2" [ uitext "Controls"       0.6 ] 0 $uiPad:6XL [ changeui "keybinds" ]
			UIbutton "hold2" [ uitext "Recorder"       0.6 ] 0 $uiPad:6XL [ changeui "recorder" ]
			if $mainmenu [
				UIbutton "hold2" [ uitext "Credits"    0.6 ] 0 $uiPad:6XL [ changeui "credits" ]
			]
			UIbar 1 0 0 $uiPad:L
			UIbutton "hold2" [ uitext "Quit"           0.6 ] 0 $uiPad:6XL [
				if $confirmquit [ changeui "dialog_quit" ] quit
			]
			uiclamp*x
		]
		if $mainmenu [
			// blank
			// UIbar 0 0 $uiPad:L
			// UIclock 0.36 0 3
		] [
			UIbar 0 1 $uiPad:L
			uivlist 0 [
				uifill $uiPad:D3XL
				if (isspectator $getclientnum) [
					local canplay
					canplay = (|| [isconnected 0 0] [< $getmastermode 2] [ismaster $getclientnum])
					UIbutton "hold2" [
						uitext (concatword (? $canplay "" "^f4") "Join Game") 0.6
					] 0 $uiPad:6XL [ if $canplay [ hideui $uiname ; spectator 0 ] ]
				] [ UIbutton "hold2" [ uitext "Spectate"     0.6 ] 0 $uiPad:6XL [ hideui $uiname ; spectator 1 ] ]
				if (getmode $m_default) [
					UIbutton "hold2" [ uitext "Change Loadout" 0.6 ] 0 $uiPad:6XL [ changeui "loadout" ]
				]
				case $getteam 0 [ uifill 0 $uiPad:6XL ] 1 [
					UIbutton "hold2" [ uitext "Join ^f3Vanir" 0.6 ] 0 $uiPad:6XL [ team "Vanir" ]
				] 2 [
					UIbutton "hold2" [ uitext "Join ^f1Aesir" 0.6 ] 0 $uiPad:6XL [ team "Aesir" ]
				]
				//uifill 0 $uiPad:6XL
				UIbar 1 0 0 $uiPad:L
				UIbutton "hold2" [
					uivlist $uiPad:L [
						uitext "Client Control" 0.6
						uitext "Interface" 0.6
					]
				] 0 (*f $uiPad:6XL 2) [ changeui "cci" ]
				UIbar 1 0 0 $uiPad:L
				UIbutton "hold2" [ uitext "Disconnect" 0.6 ] 0 $uiPad:6XL [ disconnect ]
				uiclamp*x
			]
		]
	]
] [
	if (!=s $name "player") [
		.firstrun 1
		normkey "F6"
		normkey "T"
		normkey "Y"
	]
	if $.firstrun [
		if $mainmenu [ normkey "ESCAPE" 0 ]
		normkey "TAB" [ changeui "servers" "main" ]
	] [ changeui "welcome" "main" ]
] [
	normkey "ESCAPE"
	normkey "TAB"
]




UImainsbutton = [
	arg9 = [
		uioffset (*f $uiPad:O1 $arg1) (*f $uiPad:O1- $arg1) [
			uispace $uiPad:DS $uiPad:2XL [
				@@@(? $uihover+? [
					uispace $uiPad:O5 $uiPad:O2 [
						uiclamp.x ; uialign 0 1
						uicolor $arg2 0 $uiPad:O1 [ uiclamp.x ; uioutline $arg2 ; uiclamp-e ]
					]
				]) ; doargs $arg3
			]
		]
	]
	uitarget 0 0 [
		UIhoversound
		uipress $arg2
		arg9 -1 0x0 [ uigroup [
			uifontcolortext def.ol $arg1 (|A 0x20) 0.9
			uicolortext $arg1 0 0.9
		] ]
		arg9 1 $c_green [
			uicolortext $arg1 @(? $uihover? $c_green $c_white) 0.9
		]
		uialign* -1
	]
]

UIquickdialog "main" [
	uieschide (! $mainmenu)
	uiclamp.e
] [
	uihlist 0 [
		uialign -1
		if (>f (-f $uiaspect 1.25) 0) [ uifill (divf (-f $uiaspect 1.25) 2) ]
		uivlist 0 [
			UImainsbutton "SERVER BROWSER"[ changeui "servers" ]
			UImainsbutton "PLAY ON MAP"   [ changeui "map_browser" ]
			UImainsbutton "PROFILE"       [ changeui "profile" ]
			UImainsbutton "OPTIONS"       [ changeui "options" ]
			if $mainmenu [
				UImainsbutton "CREDITS"   [ changeui "credits" ]
			]
			UImainsbutton "EXIT"          [ if $confirmquit [ changeui "dialog_quit" ] quit ]
			uiclamp*x
		]
	]
] [
	UIsetbgblur 1
	hidehud 1
	if (!=s $name "player") [
		.firstrun 1
		normkey "F6"
		normkey "T"
		normkey "Y"
	]
	if $.firstrun [
		if $mainmenu [ normkey "ESCAPE" 0 ]
		normkey "TAB" [ changeui "servers" "main" ]
	] [ changeui "welcome" "main" ]
] [
	UIsetbgblur -1
	hidehud 0
	normkey "ESCAPE"
	normkey "TAB"
]
