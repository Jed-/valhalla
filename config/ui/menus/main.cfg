
UImenu "main" [
	uieschide (! $mainmenu)
	uihlist 0 [
		uivlist 0 [
			uifill $uiPad:D3XL
			UIbutton "hold2" [ uitext "Server Browser" 0.6 ] 0 $uiPad:6XL [ changeui "servers" ]
			UIbutton "hold2" [ uitext "Map Browser"    0.6 ] 0 $uiPad:6XL [ changeui "map_browser" ]
			UIbar 1 0 0 $uiPad:L
			UIbutton "hold2" [ uitext "Player Profile" 0.6 ] 0 $uiPad:6XL [ changeui "profile" ]
			UIbutton "hold2" [ uitext "Options"        0.6 ] 0 $uiPad:6XL [ changeui "options" ]
			UIbutton "hold2" [ uitext "Controls"       0.6 ] 0 $uiPad:6XL [ changeui "keybinds" ]
			UIbutton "hold2" [ uitext "Recorder"       0.6 ] 0 $uiPad:6XL [ changeui "recorder" ]
			if $mainmenu [
				UIbutton "hold2" [ uitext "Credits"    0.6 ] 0 $uiPad:6XL [ changeui "credits" ]
			]
			UIbar 1 0 0 $uiPad:L
			UIbutton "hold2" [ uitext "Quit"           0.6 ] 0 $uiPad:6XL [
				if $confirmquit [ changeui "dialog_quit" ] quit
			]
			uiclamp*x
		]
		if $mainmenu [
			// blank
			// UIbar 0 0 $uiPad:L
			// UIclock 0.36 0 3
		] [
			UIbar 0 1 $uiPad:L
			uivlist 0 [
				uifill $uiPad:D3XL
				if (isspectator $getclientnum) [
					local canplay
					canplay = (|| [isconnected 0 0] [< $getmastermode 2] [ismaster $getclientnum])
					UIbutton "hold2" [
						uitext (concatword (? $canplay "" "^f4") "Join Game") 0.6
					] 0 $uiPad:6XL [ if $canplay [ hideui $uiname ; spectator 0 ] ]
				] [ UIbutton "hold2" [ uitext "Spectate"     0.6 ] 0 $uiPad:6XL [ hideui $uiname ; spectator 1 ] ]
				if (getmode $m_default) [
					UIbutton "hold2" [ uitext "Change Loadout" 0.6 ] 0 $uiPad:6XL [ changeui "loadout" ]
				]
				case $getteam 0 [ uifill 0 $uiPad:6XL ] 1 [
					UIbutton "hold2" [ uitext "Join ^f3Vanir" 0.6 ] 0 $uiPad:6XL [ team "Vanir" ]
				] 2 [
					UIbutton "hold2" [ uitext "Join ^f1Aesir" 0.6 ] 0 $uiPad:6XL [ team "Aesir" ]
				]
				//uifill 0 $uiPad:6XL
				UIbar 1 0 0 $uiPad:L
				UIbutton "hold2" [
					uivlist $uiPad:L [
						uitext "Client Control" 0.6
						uitext "Interface" 0.6
					]
				] 0 (*f $uiPad:6XL 2) [ changeui "cci" ]
				UIbar 1 0 0 $uiPad:L
				UIbutton "hold2" [ uitext "Disconnect" 0.6 ] 0 $uiPad:6XL [ disconnect ]
				uiclamp*x
			]
		]
	]
] [
	if (!=s $name "player") [
		.firstrun 1
		normkey "F6"
		normkey "T"
		normkey "Y"
	]
	if $.firstrun [
		if $mainmenu [ normkey "ESCAPE" 0 ]
		normkey "TAB" [ changeui "servers" "main" ]
	] [ changeui "welcome" "main" ]
] [
	normkey "ESCAPE"
	normkey "TAB"
]




UImainsbutton = [
	local alpha a255
	alpha = [ minf 1 (divf (-f $getmillis $[@@arg1.t]) $arg1) ]
	uitarget 0 0 [
		uihover [
			casef (getalias [@arg1.f]) 0 [
				[@arg1.t] = $getmillis
				[@arg1.f] = 0.0001
			] 1 [] () [ [@arg1.f] = (alpha 90) ]
		] [
			casef (getalias [@arg1.f]) 1 [
				[@arg1.t] = $getmillis
				[@arg1.f] = 0.9999
			] 0 [ [@arg1.f] = 0 ] () [ [@arg1.f] = (-f 1 (alpha 75)) ]
		]
		a255 = (max 1 (round (*f $[@arg1.f] 255)))
		UIdrawspritetiled $arg1 <mad:.165/.953/.482><agrad:0,.91,1,.86><agrad:.2,.94,0,.99>data/interface/debug/causticmap $uiPad:D6XL $uiPad:D6XL $uiPad:DS 128 0 50 $uihover?
		uiclamp-e
		UIhoversound
		uipress $arg3
		uispace (+f $uiPad:XL (*f $uiPad:5XL $[@arg1.f])) 0 [
			uitriangleoutline (|A! $a255) $uiPad:2XL $uiPad:L 270
		] ; uialign- -1
		uihlist 0 [
			uifill (*f $uiPad:6XL $[@arg1.f])
			uispace $uiPad:5XL 0 [
				UIfancytext "" $arg2 0.9
			]
		] ; uialign- -1
	]
]

UIquickdialog "main" [
	uieschide (! $mainmenu)
	uiclamp.e
] [
	uihlist 0 [
		uialign -1
		if (>f (-f $uiaspect $.UI_hudwide) 0) [ uifill (divf (-f $uiaspect $.UI_hudwide) 2) ]
		uivlist 0 [
			UImainsbutton m1 "ONLINE PLAY"    [ changeui "servers" ]
			UImainsbutton m2 "LOCAL PLAY"     [ changeui "map_browser" ]
			UImainsbutton m3 "PROFILE"        [ changeui "profile" ]
			UImainsbutton m4 "SETTINGS"       [ changeui "options" ]
			if $mainmenu [
				UImainsbutton m5 "CREDITS"    [ changeui "credits" ]
			]
			if $.UI_debug [
				UImainsbutton m6 "::DEBUG::"  [ changeui "debug" ]
			]
			if $isconnected [
				UImainsbutton m7 "DISCONNECT" [ disconnect ]
			] [
				UImainsbutton m8 "EXIT GAME"  [ if $confirmquit [ changeui "dialog_quit" ] quit ]
			]
			uiclamp*x
		]
	]
] [
	UIsetbgblur 1
	hidehud 1
	if (!=s $name "player") [
		.firstrun 1
		normkey "F6"
		normkey "T"
		normkey "Y"
	]
	if $.firstrun [
		if $mainmenu [ normkey "ESCAPE" 0 ]
		normkey "TAB" [ changeui "servers" "main" ]
	] [ changeui "welcome" "main" ]
] [
	UIsetbgblur -1
	hidehud 0
	normkey "ESCAPE"
	normkey "TAB"
]
