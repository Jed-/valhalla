
uiStdDialog "dialog_changes" [
	uivlist $uiPad:L [
		uitext " Apply changes now? " 0.7
		uiBar 1
		uispace $uiPad:XL 0 [
			uigrid 3 $uiPad:L 0 [
				loop i $pendingchanges [
					uiRadio 1 $uiPad:XL
					uifill
					uitext (pendingchanges $i) 0.6
				] ; uialign* -1
			] ; uialign- -1
		] ; uiclamp-x
	]
] [ applychanges ] [] [ if (! $pendingchanges) [ hideui $uiname ] ]




uiStdDialog "dialog_quit" [
	uivlist $uiPad:L [
		uitext " Quit to desktop? " 0.7
		uiBar 1
		uispace $uiPad:L 0 [
			uiButton "hold2" [
				uiCheckbox (! $uiConfirmQuit)
				uitext "Don't ask again" 0.6
			] 0 0 [ uiConfirmQuit (! $uiConfirmQuit) ] -1 [ uiclamp.x ]
		] ; uiclamp-x
	]
] [ quit ]




uiStdDialog "dialog_loadmap" [
	uivlist $uiPad:L [
		uitext " Gamemode " 0.7
		uiBar 1
		uispace $uiPad:L 0 [
            uivlist 0 [
                uigrid 2 0 0 [
				    loop i @@@@(listlen $modelist) [
					    uiButton "hold2" [
							uispace 0 0 [
								uiSetTip (+s "gamemode" $i) (getmodedesc $i)
								uihlist $uiPad:L [
									uiRadio [= $getnextmode $i]
									uitext (getmodeprettyname $i) 0.6
								]
							]
					    ] 0 0 [ mode $i ] -1
				    ] ; uiclamp*x
				]
                uivlist $uiPad:L [
                    uiBar 1
                    uitext " Mutators " 0.7
                    uiBar 1
                    uigrid 3 0 0 [
                        loop i @@@@(listlen $mutslist) [
							uiButton "hold2" [
								uispace 0 0 [
									uiSetTip (+s "mutator" $i) (getmutatordesc $i)
									uihlist $uiPad:L [
										uiCheckbox (& $getnextmutators (<< 1 $i))
										uitext (getmutatorprettyname $i) 0.5
									]
								]
							] 0 0 [ mutator $i ] -1
                        ] ; uiclamp*x
                    ]
                ]
			] ; uiclamp-x
		] ; uiclamp-x
	]
] [
	map $.mb_selected
	hideui "map_browser"
] [] [ mode $getmode ]




uiStdDialog "dialog_savemap" [
	uivlist $uiPad:L [
		uitext " Please enter a new map name: " 0.7
		uiBar 1
		uispace $uiPad:L 0 [
			uiAdvFieldCA .savemapname 18 0.7 "" [] [] 0 $uiPad:5XL [ uiclamp.x ] [
				uitext "^f4> INPUT MAP NAME <" 0.7
			]
		] ; uiclamp-x
	]
] [ savemap $.savemapname ] [] [ .savemapname "" ]




uiStdDialog "dialog_serverconnect" [
	uivlist $uiPad:L [
		uitext " Please enter the server password: " 0.7
		uiBar 1
		uispace $uiPad:L 0 [
			uiAdvFieldCA .serverpassw 18 0.7 "" [] [] 0 $uiPad:5XL [ uiclamp.x ] [
				uitext "^f4> INPUT SERVER PASSWORD <" 0.7
			]
		] ; uiclamp-x
	]
] [ connect (servinfoname $.sb_connectid) (servinfoport $.sb_connectid) $.serverpassw ] [] [ .serverpassw = "" ]




uiStdDialog "dialog_serverversion" [
	uivlist $uiPad:M [
		uitext " ^f6Server version mismatch " 0.7
		uiBar 1
		uitext "Looks like your client version differs from the server's." 0.6
		uitext "You can still connect, but gameplay inconsistencies may occur." 0.6
	]
] [ connect (servinfoname $.sb_connectid) ] [] [ uisound "ui/error" 150 ]




uiStdDialog "dialog_copycfg" [
	uivlist $uiPad:L [
		uitext " Copy map config? " 0.7
		uiBar 1
		uispace $uiPad:L 0 [
			uifill (+f $uiPad:D3XL $uiPad:DL)
			uivlist $uiPad:S [
				uitext (+s "^f0" $.cmc_from) 0.6
				uiTriangle 0 0 $uiPad:XL $uiPad:S 180
				uitext (+s "^f0" $.cmc_to) 0.6
			]
		] ; uiclamp-x
	]
] [
	textfocus "#copymapcfg"
	textload (+s "data/map/" $.cmc_from ".cfg")
	textsave (+s "data/map/" $.cmc_to   ".cfg")
] [] [
	if (|| [=s $.cmc_from ""] [=s $.cmc_to ""]) [ hideui $uiname ]
] [ .cmc_from "" ; .cmc_to "" ]
