
uiMenu "recorder" [
	uihlist $uiPad:L [
		uivlist 0 [
			uifill $uiPad:D6XL
			uiHorSld moviefps 10 (min 200 $maxfps) 2 [] [ concat $$arg1 "fps" ]
			uiHorSld .mr_resmul 10 100 5 [
				moview = (*f (? $fullscreen $desktopw $screenw) (divf $.mr_resmul 100))
				movieh = (*f (? $fullscreen $desktoph $screenh) (divf $.mr_resmul 100))
			] [ concatword $$arg1 "^%" ]
			uispace 0 $uiPad:L [
				uiclamp.x
				uitext (format "^f2%1 ^f4x ^f2%2" $moview $movieh) 0.6
				uiButtonArea "hold2 $c_diag2" $uiPad:5XL $uiPad:5XL [
					uialign 1
					uiCheckbox $.mr_options 0 0 0 [
						uiTriangle $c_line1 0 $uiPad:O5 0 270
					]
					uirelease [ .mr_options (! $.mr_options) ]
				]
			]
			uiAdvFieldLA moviedir 16 0.6 "" [] [] 0 $uiPad:5XL [] [
				uicolortext " location:" $c_gray 0.5
				uialign- -1
				uicolortext "no subfolder set yet" $c_gray 0.5
			]
			uiAdvFieldLA .mr_title 16 0.6 "" [] [] 0 $uiPad:5XL [] [
				uicolortext " name:" $c_gray 0.5
				uialign- -1
				uicolortext (concatword "movie_" (
					if $mainmenu [ result "menu" ] [
						concatword (getmodename $getmode) "_" (
							? (=s $mapname "") "newmap" $mapname
						)
					]
				) "_" $getmillis) $c_gray 0.5
			]
			uiBar 0 0 0 $uiPad:M
			uiButtonArea "hold2" 0 $uiPad:5XL [
				uispace $uiPad:M 0 [
					uiclamp.x
					uihlist $uiPad:L [
						if (&& $movierecording [< (abs (mod $getmillis 800)) 400])  [
							uiCircle $c_red $c_darkred $uiPad:XL
						] [ uiCircle $c_line1 $c_line2 $uiPad:XL ]
						uitext (? $movierecording "Stop Recording" "Start Recording") 0.6
					] ; uialign- -1
					uihlist $uiPad:O3 [
						uiKeybox $uiPad:SXL $uiPad:4XL [ uitext "Ctrl" 0.5 ]
						uiKeybox $uiPad:4XL $uiPad:4XL [ uitext "R"    0.5 ]
					] ; uialign- 1
				]
			] 0 $uiPad:5XL [ togglerecording ]
			uiclamp*x
		]
		if $.mr_options [
			uiBar 0 1
			uivlist 0 [
				uifill $uiPad:D3XL 0
				uiButton "hold2" [
					uiCheckbox $moviesound
					uitext "Record Sound" 0.6
				] 0 $uiPad:5XL [ moviesound (! $moviesound) ] -1
				uiButton "hold2" [
					uiCheckbox (! $moviesync)
					uitext "Frameskipping" 0.6
				] 0 $uiPad:5XL [ moviesync (! $moviesync) ] -1
				uiBar 1 0 $uiPad:L $uiPad:L
				uiButton "hold2" [
					uiCheckbox $movieaccel
					uitext "HW Accel" 0.6
				] 0 $uiPad:5XL [ movieaccel (! $movieaccel) ] -1
				uiButton "hold2" [
					uiCheckbox $movieaccelyuv
					uitext "YUV Encoding" 0.6
				] 0 $uiPad:5XL [ movieaccelyuv (! $movieaccelyuv) ] -1
				uiButton "hold2" [
					uiCheckbox $movieaccelblit
					uitext "Blitting" 0.6
				] 0 $uiPad:5XL [ movieaccelblit (! $movieaccelblit) ] -1
				uiButton "hold2" [
					uiCheckbox $dbgmovie
					uitext "Debugging" 0.6
				] 0 $uiPad:5XL [ dbgmovie (! $dbgmovie) ] -1
				uiclamp*x
			]
		]
	]
] [
	moviefps = (min $moviefps 200 $maxfps)
	if (|| $movierecording [= (gcd $screenw $screenh) (gcd $moview $movieh)]) [] [
		moview $screenw
		movieh $screenh
		.mr_resmul 100
	]
] [] [] [ uitext "Movie Recorder" 0.8 ]

togglerecording = [	
	if $movierecording [ movie ] [
		if (=s $.mr_title "") [
			movie (concatword "movie_" (
				if $mainmenu [ result "menu" ] [
					concatword (getmodename $getmode) "_" (
						? (=s $mapname "") "newmap" $mapname
					)
				]
			) "_" $getmillis)
		] [ movie $.mr_title ]
	]
]

