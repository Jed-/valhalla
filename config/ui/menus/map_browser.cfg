///////////////////////////////////////////////////////////////////////////////
//  Map browser and list generation.                                         //
///////////////////////////////////////////////////////////////////////////////


defmaptable = [
    [ dm_azag         0x7B  "Temple of Azag ^f4by Morti^ff"                ]
    [ dm_azag2        0x7B  "Temple of Azag II ^f4by Morti^ff"             ]
    [ ctf_blackforest 0x7F  "Black Forest ^f4by Ardelico & Morti^ff"       ]
    [ ctf_desertwind  0x7F  "Sadstone Station ^f4by Ardelico & Morti^ff"   ]
    [ dm_vortex       0x33  "Vortex Anomaly ^f4by Ardelico & Morti^ff"     ]
    [ dm_lightspeed   0x7B  "Lightspeed Express ^f4by Ardelico^ff"         ]
    [ dm_nebula       0x33  "Nebula ^f4by Ardelico^ff"                     ]
    [ dm_pinelake     0x7B  "Pinelake Station ^f4by Ardelico & Morti^ff"   ]
    [ dm_sera         0x33  "Serenity Station ^f4by Ardelico^ff"           ]
    [ dm_syndicate    0x7F  "Syndicate ^f4by Pritchard, garsipal^ff"       ]
    [ dm_tropica      0x33  "Tropical Outpost ^f4by Ardelico^ff"           ]
    [ dm_wintergreen  0x33  "Wintergreen Station ^f4by Ardelico^ff"        ]
]


// genmaplist VARNAME MODE_BITMASK MAPLIST

genmaplist = [
    $arg1 = (loopconcat* m 1 (listlen $defmaptable) [
        if (|| [= $arg2 0] [& $arg2 (at $defmaptable (+ $m 1) 1)]) [ at $defmaptable $m 0 ]
    ])
]


// Generation of map lists based on mode masks

genmaplist all_maps         0x0  // No mask
genmaplist dm_maps          0x1  // Deathmatch
genmaplist tdm_maps         0x2  // Team Deathmatch
genmaplist ctf_maps         0x4  // Capture The Flag
genmaplist elimination_maps 0x8  // Elimination
genmaplist lms_maps         0x10 // Last Man Standing
genmaplist berserker_maps   0x20 // Berserker
genmaplist infection_maps   0x40 // Infection


// Generation of a custom maps list based on maps not included in the "official" map table,
// which are automatically categorized as "user maps".

genusermaps = [
    user_maps = [] // where custom maps by the user will be stored
    loopfiles m "data/map" "ogz" [
        if (< (indexof $all_maps $m) 0) [
            append user_maps $m
        ]
    ]
]

genusermaps

uiLiteMenu "map_browser" [
    uiallowinput 1
    uialign 1
    if (> $.mb_totalpg 1) [
        uiscrolldown [ UIscrollsound .mb_curpage (? (iskeyheld "LCTRL")  999  1) (- $.mb_totalpg 1) ]
        uiscrollup   [ UIscrollsound .mb_curpage (? (iskeyheld "LCTRL") -999 -1) 0 ]
    ]
] [
    uiHudCell 0 0 [
        uivlist $uiPad:L [
            uihlist 0 [
                uiButton "hold2 $c_diag2" [
                    uiCheckbox [= $.mb_maplist 1] 0 0 0 [
                        if $arg1 [ uiTriangle $c_line1 0 $uiPad:O5 0 180 ]
                    ]
                    uitext "Community" 0.6
                ] $.z 0 [
                    .mb_maplist = 1
                    .mb_curmaps  = $.mb_cat1maps
                    .mb_totalpg  = (listlen $.mb_curmaps)
                    .mb_curpage  = 0
                ] -1
                uigroup [
                    uifill $.z $uiPad:6XL
                    uihlist 0 [
                        uiclamp.y
                        uifill $uiPad:DXS 0 [
                            uifonttext "mono" (+ $.mb_curpage 1) 0.7
                            uialign- 1 -1
                        ]
                        uioffset 0 $uiPad:L- [
                            uiclip 0 $uiPad:6XL [ uicolortext "/" $c_gray 1.2 ]
                        ] // text takes up a lot of vertical space, have to "cut" it
                        uifill $uiPad:DXS 0 [
                            uifonttext "mono" (max $.mb_totalpg 1) 0.7
                            uialign- -1 1
                        ]
                        uiclamp*y
                    ]
                ]
                uiButton "hold2 $c_diag2" [
                    uitext "Official" 0.6
                    uiCheckbox [= $.mb_maplist 0] 0 0 0 [
                        if $arg1 [ uiTriangle $c_line1 0 $uiPad:O5 0 180 ]
                    ]
                ] $.z 0 [
                    .mb_maplist = 0
                    .mb_curmaps = $.mb_cat0maps
                    .mb_totalpg = (listlen $.mb_curmaps)
                    .mb_curpage = 0
                ] 1
                uiclamp*y
            ]
            uigroup [
                uifill $.x $.y
                uihover [] [ if $uiallowinput [ uihold [] [ .mb_selidx = -1 ] ] ]
                uigrid 3 0 0 [
                    uialign -1 -1
                    local idx img ; idx = -1
                    looplist m (at $.mb_curmaps $.mb_curpage) [
                        ++ idx ; img = (+s data/map/ $m .png)
                        uitarget $.z $.z [
                            uiHoverOnce [ uiHoverSound 3 ]
                            if (= $idx $.mb_selidx) [
                                uimodcolor $c_grdmod
                                if (findfile $img) [ uiimage $img ] [ uiline $c_line2 ]
                                uiclamp*e
                                uispace 0 $uiPad:S [
                                    uimodcolor $c_grdmod 0 0 [ uiclamp.e ]
                                    uiclip (-f $.z $uiPad:M) 0 [ uiFancyText "" $m 0.5 ]
                                ] ; uiclamp-x ; uialign- 0 1
                                uirelease [ 
                                    .mb_selected = $m
                                    if (= $.mb_maplist 0) [
                                        .mb_selidx = $.mb_selidx
                                        .mb_description = (at $defmaptable $.mb_selidx 2)
                                    ] [
                                        .mb_selidx = 0
                                        .mb_description = ""
                                    ]
                                    showui "newgame"
                                    hideui "map_browser"
                                ]
                            ] [
                                uimodcolor $c_boxmod
                                if (findfile $img) [ uiimage $img ] [ uiline $c_line2 ]
                                uiclamp*e
                                uispace 0 $uiPad:S [
                                    uimodcolor $c_grdmod 0 0 [ uiclamp.e ]
                                    uiclip (-f $.z $uiPad:M) 0 [ uiFancyText "" $m 0.5 $c_gray_l ]
                                ] ; uiclamp-x ; uialign- 0 1
                                uihover [ .mb_selidx = $idx ; uiSetMillis ]
                                uioutline $c_line1 $.z $.z
                            ]
                        ]
                    ]
                ]
                if (> $.mb_selidx -1) [ // drawing on top to avoid overlap from other cells
                    uioffset (*f $.z (mod $.mb_selidx 3)) (*f $.z (/ $.mb_selidx 3)) [
                        uioutline (uiCosWave:INT $c_green $c_line1 500) $.z $.z
                    ] ; uialign- -1 -1
                ]
            ]
            uihlist 0 [
                uifont "modernpics" [
                    uifill $.z
                    if (> $.mb_curpage 0) [
                        uiButtonArea "hold2 $c_diag2" 0 0 [
                            uiclamp.e
                            uitext "<" 0.9
                            uirelease [ -- .mb_curpage ]
                        ]
                    ] [ uicolortext "<" $c_gray 0.9 ]
                ]
                uifill $.z 0 [ uiFastImg "" "ui/" "io/" "mouseM" $uiPad:6XL ]
                uifont "modernpics" [
                    uifill $.z
                    if (< (+ $.mb_curpage 1) $.mb_totalpg) [
                        uiButtonArea "hold2 $c_diag2" 0 0 [
                            uiclamp.e
                            uitext ">" 0.9
                            uirelease [ ++ .mb_curpage ]
                        ]
                    ] [ uicolortext ">" $c_gray 0.9 ]
                ]
                uiclamp*y
            ]
        ]
    ]
] [
    .y = (-f 1 $uiPad:DM $uiPad:DS)
    .z = (/f $.y 5)
    .x = (*f $.z 3)
    .mb_selidx   = -1
    .mb_cat1maps = (subdivlist $user_maps 15)
    .mb_cat0maps = (subdivlist $all_maps 15)
    .mb_curmaps  = $[.mb_cat@[.mb_maplist]maps]
    .mb_totalpg  = (listlen $.mb_curmaps)
    .mb_curpage  = (min $.mb_curpage (- $.mb_totalpg 1))
    hideui "texture_browser"
    hideui "mapmodel_browser"
    hideui "geo_prefab"
    hideui "editvars"
] [ if $editing [ showui "editvars" ] ]


MAX_MAPS = 12

uiMenu "newgame" [
    uivlist $uiPad:L [
       uimodcolor $c_boxmod 0.82 0.4 [
           case $.UI_curtab 0 [
               uihlist $uiPad:L [
                    uivlist $uiPad:L [
                        uitext " Map " 0.7
                        uiBar 1
                        uivlist $uiPad:L [
                            uimodcolor $c_boxmod 0.25 0.25 [
                                uiclamp 1 1 1 1
                                uiButtonArea "hold2" 0.025 0.025 [
                                    uiimage "data/interface/valknut.png" 0.275 0.275 [ 
                                        uiimage (concatword "data/map/" $.mb_selected ".png") 0.275 0.275 [
                                            uiclamp 1 1 1 1
                                        ]
                                        uihover [
                                            if (= $.mb_maplist 0) [
                                                .mb_description = (at $defmaptable $.mb_selidx 2)
                                            ]
                                        ]
                                    ]
                                    uirelease [
                                        showui "map_browser"
                                    ]
                                ]
                            ]
                            uihlist $uiPad:S [
                                uiButton "hold2" [
                                    uiFastImg "" "ui/" "arrow_l1" "" 0.025
                                ] 0 $uiPad:6XL [
                                    if (> $.mb_selidx 0) [
                                        .mb_selidx = (- $.mb_selidx 1)
                                        .mb_selected = (at $defmaptable $.mb_selidx 0)
                                        .mb_maplist = 0
                                        .mb_description = (at $defmaptable $.mb_selidx 2)
                                    ]
                                ] 0 [
                                    uiSetTip "" "previous map"
                                ]
                                uialign- -1
                                uiButton "hold2" [
                                    uiFastImg "" "ui/" "refresh" "" 0.025
                                ] 0 $uiPad:6XL [
                                    .mb_selidx = (rnd $MAX_MAPS)
                                    .mb_selected = (at $defmaptable $.mb_selidx 0)
                                    .mb_maplist = 0
                                    .mb_description = (at $defmaptable $.mb_selidx 2)	
                                ] 0 [
                                    uiSetTip "" "random map"
                                ]
                                uialign- 0
                                uiButton "hold2" [
                                    uiFastImg "" "ui/" "arrow_r1" "" 0.025
                                ] 0 $uiPad:6XL [
                                    if (< $.mb_selidx (- $MAX_MAPS 1)) [
                                        .mb_selidx = (+ $.mb_selidx 1)
                                        .mb_selected = (at $defmaptable $.mb_selidx 0)
                                        .mb_maplist = 0
                                        .mb_description = (at $defmaptable $.mb_selidx 2)
                                    ]
                                ] 0 [
                                    uiSetTip "" "next map"
                                ]
                                uialign- 1
                            ]
                        ]
                    ]
                    uiBar 0 1
                    uivlist $uiPad:L [
                        uitext " Mode " 0.7
                        uiBar 1
                        uispace $uiPad:L 0 [
                            uivlist $uiPad:L [
                                uigrid 2 0 0 [
                                    loop i @@@@(listlen $modelist) [
                                        uiButton "hold2" [
                                            uispace 0 0 [
                                                uiSetTip (+s "gamemode" $i) (getmodedesc $i)
                                                uihlist $uiPad:L [
                                                    uiRadio [= $getnextmode $i]
                                                    uitext (getmodeprettyname $i) 0.6
                                                ]
                                                uihover [
                                                    .mb_description = (getmodedesc $i)
                                                ]
                                            ]
                                        ] 0 0 [ mode $i ] -1
                                    ] ; uiclamp*x
                                ]
                            ]
                        ]
                        uiBar 1
                        uitext " Mutators " 0.7
                        uiBar 1
                        uispace $uiPad:L 0 [
                            uigrid 3 0 0 [
                                loop i @@@@(listlen $mutslist) [
                                    uiButton "hold2" [
                                        uispace 0 0 [
                                            uiSetTip (+s "mutator" $i) (getmutatordesc $i)
                                            uihlist $uiPad:L [
                                                uiCheckbox (& $getnextmutators (<< 1 $i))
                                                uitext (getmutatorprettyname $i) 0.5
                                            ]
                                            uihover [
                                                .mb_description = (getmutatordesc $i)
                                            ]
                                        ]
                                    ] 0 0 [ mutator $i ] -1
                                ] ; uiclamp*x
                            ]
                        ] ; uiclamp-x
                    ] ; uiclamp-x
                ]
            ] 1 [
                uivlist $uiPad:L [
                    uiOptionRow 0 [ uitext "Time Limit" 0.6 ] 0 [
                        uiHorSld timelimit 0 60 1
                        uihover [
                            .mb_description = "Maximum time for the match"
                        ]
                    ]
                    uiOptionRow 0 [ uitext "Score Limit" 0.6 ] 0 [
                        uiHorSld scorelimit -1 1000 1
                        uihover [
                            .mb_description = "Maximum score limit for the match. Use -1 to adjust based on the mode"
                        ]
                    ]
                    uiOptionRow 0 [ uitext "Round Time Limit" 0.6 ] 0 [
                        uiHorSld roundtimelimit 1 10 1
                        uihover [
                            .mb_description = "Maximum time per round in round-based modes"
                        ]
                    ]
                    uiOptionRow 0 [ uitext "Round Limit" 0.6 ] 0 [
                        uiHorSld roundlimit 0 30 1
                        uihover [
                            .mb_description = "Maximum number of rounds in round-based modes"
                        ]
                    ]
                    uiOptionRow 0 [ uitext "Damage" 0.6 ] 2 [
                        uiButton "hold2" [
                            uiCheckbox $selfdamage
                            uitext "Self Damage" 0.6
                        ] $uiPad:D3XL 0 [ selfdamage (! $selfdamage) ] -1
                        uiButton "hold2" [
                            uiCheckbox $teamdamage
                            uitext "Friendly Fire" 0.6
                        ] $uiPad:D3XL 0 [ teamdamage (! $teamdamage) ] -1
                        uihover [
                            .mb_description = "Controls whether self or allies can be damaged"
                        ]
                    ]
                ]
            ] 2 [
                uivlist $uiPad:L [
                    uiOptionRow 0 [ uitext "Bot Skill Level" 0.6 ] 0 [
                        uiOptionSld .botskill [
                            0   "RANDOM"          $c_gray
                            1   "TARGET PRACTICE" $c_green
                            25  "MORON"           $c_cyan_t
                            50  "DECENT"          $c_blue
                            75  "NORMAL"          $c_white
                            86  "ADEPT"           $c_yellow
                            92  "IMPRESSIVE"      $c_orange
                            96  "TRAUMA"          $c_red
                            101 "AIMBOT"          $c_magenta
                        ]
                        uihover [
                            .mb_description = (+s "Bot skill level: " $.botskill)
                        ]
                    ]
                    uiOptionRow 0 [ uitext "Bot Amount" 0.6 ] 0 [
                        uiHorSld .botamount 0 32 1
                        uihover [
                            .mb_description = (+s "Number of bots to add to the game: " $.botamount)
                        ]
                    ]
                    uiOptionRow 1 [
                        uiButton "hold2" [
                            uiCheckbox $botidle
                            uitext "Bot Idle" 0.6
                         ] 0 0 [ botidle (! $botidle) ] -1
                         uihover [
                            .mb_description = "Puts the bot on pause for testing"
                        ]
                    ]
                ]
            ]
        ]
        uihlist $uiPad:S [
            uifontcolorblendtext "modernpics" "=" $c_white 0.85 $uiPad:US
            uicolorblendtext (? $.mb_description $.mb_description " ") $c_white 0.85 0.45
        ]
        uialign- -1
        uiBar 1
        uifill $uiPad:UM
        uiOptionSld .UI_curtab [
            0 "Map and Mode"  $c_white
            1 "Game settings" $c_white
            2 "Bots"          $c_white
        ] [ ] 1 0.8
        uiButtonArea "hold2" 0.25 0.05 [
            uicolortext "Play" (? $.mb_selected $c_white $c_gray) 0.7
            uirelease [
                if $.mb_selected [
                    hideui "map_browser"
                    map $.mb_selected
                    loop i $.botamount [
                        botadd $.botskill
                    ]
                ]
            ]
        ]
    ]
] [
    .UI_curtab = 0
] [] [] [
    local title
    case $.UI_curtab 0 [
        title = "Map and Mode"
    ] 1 [
        title = "Game settings""
    ] 2 [
        title = "Bots"
    ]
    uitext $title 0.7
]