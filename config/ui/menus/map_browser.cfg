///////////////////////////////////////////////////////////////////////////////
//  Map browser and list generation.                                         //
///////////////////////////////////////////////////////////////////////////////


defmaptable = [
    azag            0x7B
    azag2           0x7B
    ctf_blackforest 0x7F
    ctf_desertwind  0x7F
    flux3           0x33
    lightspeed      0x7B
    nebula          0x33
    pinelake        0x7B
    sera            0x33
    tropica         0x33
    wintergreen     0x33
]


// genmaplist VARNAME MODE_BITMASK MAPLIST

genmaplist = [
    if $arg3 [] [ arg3 = $defmaptable ]
    $arg1 = (loopconcat* m 2 (/ (listlen $arg3) 2) [
        if (|| [= $arg2 0] [& $arg2 (at $arg3 (+ $m 1))]) [ at $arg3 $m ]
    ])
]


// Generation of map lists based on mode masks

genmaplist all_maps             0x0  // No mask
genmaplist deathmatch_maps      0x1  // Deathmatch
genmaplist teamdeathmatch_maps  0x2  // Team Deathmatch
genmaplist capturetheflag_maps  0x4  // Capture The Flag
genmaplist elimination_maps     0x8  // Elimination
genmaplist lastmanstanding_maps 0x10 // Last Man Standing
genmaplist berserker_maps       0x20 // Berserker
genmaplist infection_maps       0x40 // Infection


// Generation of a custom maps list based on maps not included in the "official" map table,
// which are automatically categorized as "user maps".

genusermaps = [
    user_maps = [] // where custom maps by the user will be stored
    loopfiles m "data/map" "ogz" [
        if (< (indexof $all_maps $m) 0) [
            append user_maps $m
        ]
    ]
]

genusermaps

uiLiteMenu "map_browser" [
    uiallowinput (! (uivisible "dialog_loadmap"))
    uialign 1
    if (> $.mb_totalpg 1) [
        uiscrolldown [ UIscrollsound .mb_curpage (? (iskeyheld "LCTRL")  999  1) (- $.mb_totalpg 1) ]
        uiscrollup   [ UIscrollsound .mb_curpage (? (iskeyheld "LCTRL") -999 -1) 0 ]
    ]
] [
    uiHudCell 0 0 [
        uivlist $uiPad:L [
            uihlist 0 [
                uiButton "hold2 $c_diag2" [
                    uiCheckbox [= $.mb_maplist 1] 0 0 0 [
                        if $arg1 [ uiTriangle $c_line1 0 $uiPad:O5 0 180 ]
                    ]
                    uitext "Community" 0.6
                ] $.z 0 [
                    .mb_maplist = 1
                    .mb_curmaps  = $.mb_cat1maps
                    .mb_totalpg  = (listlen $.mb_curmaps)
                    .mb_curpage  = 0
                ] -1
                uigroup [
                    uifill $.z $uiPad:6XL
                    uihlist 0 [
                        uiclamp.y
                        uifill $uiPad:DXS 0 [
                            uifonttext "mono" (+ $.mb_curpage 1) 0.7
                            uialign- 1 -1
                        ]
                        uioffset 0 $uiPad:L- [
                            uiclip 0 $uiPad:6XL [ uicolortext "/" $c_gray 1.2 ]
                        ] // text takes up a lot of vertical space, have to "cut" it
                        uifill $uiPad:DXS 0 [
                            uifonttext "mono" (max $.mb_totalpg 1) 0.7
                            uialign- -1 1
                        ]
                        uiclamp*y
                    ]
                ]
                uiButton "hold2 $c_diag2" [
                    uitext "Official" 0.6
                    uiCheckbox [= $.mb_maplist 0] 0 0 0 [
                        if $arg1 [ uiTriangle $c_line1 0 $uiPad:O5 0 180 ]
                    ]
                ] $.z 0 [
                    .mb_maplist = 0
                    .mb_curmaps = $.mb_cat0maps
                    .mb_totalpg = (listlen $.mb_curmaps)
                    .mb_curpage = 0
                ] 1
                uiclamp*y
            ]
            uigroup [
                uifill $.x $.y
                uihover [] [ if $uiallowinput [ uihold [] [ .mb_selidx = -1 ] ] ]
                uigrid 3 0 0 [
                    uialign -1 -1
                    local idx img ; idx = -1
                    looplist m (at $.mb_curmaps $.mb_curpage) [
                        ++ idx ; img = (+s data/map/ $m .png)
                        uitarget $.z $.z [
                            uiHoverOnce [ uiHoverSound 3 ]
                            if (= $idx $.mb_selidx) [
                                uimodcolor $c_grdmod
                                if (findfile $img) [ uiimage $img ] [ uiline $c_line2 ]
                                uiclamp*e
                                uispace 0 $uiPad:S [
                                    uimodcolor $c_grdmod 0 0 [ uiclamp.e ]
                                    uiclip (-f $.z $uiPad:M) 0 [ uiFancyText "" $m 0.5 ]
                                ] ; uiclamp-x ; uialign- 0 1
                                uirelease [ .mb_selected = $m ; showui "dialog_loadmap" ]
                            ] [
                                uimodcolor $c_boxmod
                                if (findfile $img) [ uiimage $img ] [ uiline $c_line2 ]
                                uiclamp*e
                                uispace 0 $uiPad:S [
                                    uimodcolor $c_grdmod 0 0 [ uiclamp.e ]
                                    uiclip (-f $.z $uiPad:M) 0 [ uiFancyText "" $m 0.5 $c_gray_l ]
                                ] ; uiclamp-x ; uialign- 0 1
                                uihover [ .mb_selidx = $idx ; uiSetMillis ]
                                uioutline $c_line1 $.z $.z
                            ]
                        ]
                    ]
                ]
                if (> $.mb_selidx -1) [ // drawing on top to avoid overlap from other cells
                    uioffset (*f $.z (mod $.mb_selidx 3)) (*f $.z (/ $.mb_selidx 3)) [
                        uioutline (uiCosWave:INT $c_green $c_line1 500) $.z $.z
                    ] ; uialign- -1 -1
                ]
            ]
            uihlist 0 [
                uifont "modernpics" [
                    uifill $.z
                    if (> $.mb_curpage 0) [
                        uiButtonArea "hold2 $c_diag2" 0 0 [
                            uiclamp.e
                            uitext "<" 0.9
                            uirelease [ -- .mb_curpage ]
                        ]
                    ] [ uicolortext "<" $c_gray 0.9 ]
                ]
                uifill $.z 0 [ uiFastImg "" "ui/" "io/" "mouseM" $uiPad:6XL ]
                uifont "modernpics" [
                    uifill $.z
                    if (< (+ $.mb_curpage 1) $.mb_totalpg) [
                        uiButtonArea "hold2 $c_diag2" 0 0 [
                            uiclamp.e
                            uitext ">" 0.9
                            uirelease [ ++ .mb_curpage ]
                        ]
                    ] [ uicolortext ">" $c_gray 0.9 ]
                ]
                uiclamp*y
            ]
        ]
    ]
] [
    .y = (-f 1 $uiPad:DM $uiPad:DS)
    .z = (/f $.y 5)
    .x = (*f $.z 3)
    .mb_selidx   = -1
    .mb_cat1maps = (subdivlist $user_maps 15)
    .mb_cat0maps = (subdivlist $all_maps 15)
    .mb_curmaps  = $[.mb_cat@[.mb_maplist]maps]
    .mb_totalpg  = (listlen $.mb_curmaps)
    .mb_curpage  = (min $.mb_curpage (- $.mb_totalpg 1))
    hideui "texture_browser"
    hideui "mapmodel_browser"
    hideui "geo_prefab"
    hideui "editvars"
] [ if $editing [ showui "editvars" ] ]
