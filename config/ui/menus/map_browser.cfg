
uiLiteMenu "map_browser" [
	uiallowinput (! (uivisible "dialog_loadmap"))
	uialign 1
	if (> $.mb_totalpg 1) [
		uiscrolldown [ UIscrollsound .mb_curpage (? (iskeyheld "LCTRL")  999  1) (- $.mb_totalpg 1) ]
		uiscrollup   [ UIscrollsound .mb_curpage (? (iskeyheld "LCTRL") -999 -1) 0 ]
	]
] [
	uiHudCell 0 0 [
		uivlist $uiPad:L [
			uihlist 0 [
				uiButton "hold2 $c_diag2" [
					uiCheckbox [= $.mb_maplist 1] 0 0 0 [
						if $arg1 [ uiTriangle $c_line1 0 $uiPad:O5 0 180 ]
					]
					uitext "Community" 0.6
				] $.z 0 [
					.mb_maplist = 1
					.mb_curmaps  = $.mb_cat1maps
					.mb_totalpg  = (listlen $.mb_curmaps)
					.mb_curpage  = 0
				] -1
				uigroup [
					uifill $.z $uiPad:6XL
					uihlist 0 [
						uiclamp.y
						uifill $uiPad:DXS 0 [
							uifonttext "mono" (+ $.mb_curpage 1) 0.7
							uialign- 1 -1
						]
						uioffset 0 $uiPad:L- [
							uiclip 0 $uiPad:6XL [ uicolortext "/" $c_gray 1.2 ]
						] // text takes up a lot of vertical space, have to "cut" it
						uifill $uiPad:DXS 0 [
							uifonttext "mono" (max $.mb_totalpg 1) 0.7
							uialign- -1 1
						]
						uiclamp*y
					]
				]
				uiButton "hold2 $c_diag2" [
					uitext "Integrated" 0.6
					uiCheckbox [= $.mb_maplist 0] 0 0 0 [
						if $arg1 [ uiTriangle $c_line1 0 $uiPad:O5 0 180 ]
					]
				] $.z 0 [
					.mb_maplist = 0
					.mb_curmaps = $.mb_cat0maps
					.mb_totalpg = (listlen $.mb_curmaps)
					.mb_curpage = 0
				] 1
				uiclamp*y
			]
			uigroup [
				uifill $.x $.y
				uihover [] [ if $uiallowinput [ uihold [] [ .mb_selidx = -1 ] ] ]
				uigrid 3 0 0 [
					uialign -1 -1
					local idx img ; idx = -1
					looplist m (at $.mb_curmaps $.mb_curpage) [
						++ idx ; img = (+s data/map/ $m .png)
						uitarget $.z $.z [
							if (= $idx $.mb_selidx) [
								uimodcolor $c_grdmod
								if (findfile $img) [ uiimage $img ] [ uiline $c_line2 ]
								uiclamp*e
								uispace 0 $uiPad:S [
									uimodcolor $c_grdmod 0 0 [ uiclamp.e ]
									uiclip (-f $.z $uiPad:M) 0 [ uiFancyText "" $m 0.5 ]
								] ; uiclamp-x ; uialign- 0 1
								uirelease [ .mb_selected = $m ; showui "dialog_loadmap" ]
							] [
								uimodcolor $c_boxmod
								if (findfile $img) [ uiimage $img ] [ uiline $c_line2 ]
								uiclamp*e
								uispace 0 $uiPad:S [
									uimodcolor $c_grdmod 0 0 [ uiclamp.e ]
									uiclip (-f $.z $uiPad:M) 0 [ uiFancyText "" $m 0.5 $c_gray_l ]
								] ; uiclamp-x ; uialign- 0 1
								uihover [ .mb_selidx = $idx ; uiSetMillis ]
								uioutline $c_line1 $.z $.z
							]
							uiHoverOnce [ uiHoverSound ]
						]
					]
				]
				if (> $.mb_selidx -1) [ // drawing on top to avoid overlap from other cells
					uioffset (*f $.z (mod $.mb_selidx 3)) (*f $.z (/ $.mb_selidx 3)) [
						uioutline (uiCosWave:INT $c_green $c_line1 500) $.z $.z
					] ; uialign- -1 -1
				]
			]
			uihlist 0 [
				uifont "webdings" [
					uifill $.z
					if (> $.mb_curpage 0) [
						uiButtonArea "hold2 $c_diag2" 0 0 [
							uiclamp.e
							uitext "7" 0.9
							uirelease [ -- .mb_curpage ]
						]
					] [ uicolortext "7" $c_gray 0.9 ]
				]
				uifill $.z 0 [ uiFastImg "" "ui/" "io/" "mouseM" $uiPad:6XL ]
				uifont "webdings" [
					uifill $.z
					if (< (+ $.mb_curpage 1) $.mb_totalpg) [
						uiButtonArea "hold2 $c_diag2" 0 0 [
							uiclamp.e
							uitext "8" 0.9
							uirelease [ ++ .mb_curpage ]
						]
					] [ uicolortext "8" $c_gray 0.9 ]
				]
				uiclamp*y
			]
		]
	]
] [
	.y = (-f 1 $uiPad:DM $uiPad:DS)
	.z = (/f $.y 5)
	.x = (*f $.z 3)
	.mb_selidx   = -1
	.mb_cat1maps = (subdivlist $usermaps 15)
	.mb_cat0maps = (subdivlist $allmaps 15)
	.mb_curmaps  = $[.mb_cat@[.mb_maplist]maps]
	.mb_totalpg  = (listlen $.mb_curmaps)
	.mb_curpage  = 0
	hideui "texture_browser"
	hideui "mapmodel_browser"
	hideui "geo_prefab"
	hideui "editvars" 
] [ if $editing [ showui "editvars" ] ]
