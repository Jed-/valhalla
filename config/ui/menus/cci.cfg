
uiMenu "cci" [
	local numspec numplay numblue numred
	if $mainmenu [ hideui "cci" ] [
		cp_updateselected (listclients 1)
	]
	//looplist cn (listclients 1) [
	//    if (isspectator $cn) [
	//        numspec = (+ $numspec 1)
	//    ] [
	//        numplay = (+ $numplay 1)
	//        case (getclientteam $cn) 1 [
	//            numblue = (+ $numblue 1)
	//        ] 2 [ numred = (+ $numred 1) ]
	//    ]
	//]

	uivlist $uiPad:L [
		uihlist $uiPad:L [
			uiButton "hold2" [
				uiCheckbox $.cci_showspec
				uitext "Show Spec" 0.6
			] $uiPad:D2XL 0 [
				.cci_showspec (! $.cci_showspec)
				listfilter= .cci_clients i [! (isspectator $i)]
			] -1
			uiButton "hold2" [
				uiCheckbox $.cci_showbots
				uitext "Show Bots" 0.6
			] $uiPad:D2XL 0 [
				.cci_showbots (! $.cci_showbots)
				listfilter= $.cci_clients i [! (isai $i)]
			] -1
			uiButton "hold2" [
				uiCheckbox $.cci_showself
				uitext "Show Self" 0.6
			] $uiPad:D2XL 0 [
				.cci_showself (! $.cci_showself)
				listdel= .cci_clients $getclientnum
			] -1
		]
		uiToolTipBox $uiPad:UM (*f $uiPad:5XL 11) [
			uihlist $uiPad:L [
				uiclamp.y
				uialign -2 -1
				uivlist 0 [
					if (m_teammode $getmode) [
						cp_clientpanel 1 0
						cp_clientpanel 1 1
					] [ cp_clientpanel 0 0 ]
				]
				uivlist 0 [
					if (m_teammode $getmode) [
						cp_clientpanel 2 0
						cp_clientpanel 2 1
					] [ cp_clientpanel 0 1 ]
				]
				uialign* -2 -1
			]
		] (*f $uiPad:D5XL 2) [
			uitext $.cci_clients 0.5
		]
		uihlist $uiPad:L [
			uialign -1 -2
			// XXX incomplete buttons, placeholders
			uiButton "" [
				uitext "Kick" 0.6
			] $uiPad:3XL $uiPad:6XL [
				listfilter= .cci_clients i [&& [! (isai $i)] [!= $i $getclientnum]]
				selected_kick = $.cci_clients
				looplist cn $.cci_clients [ kick $cn ]
			] 0 [ uiSetTip "Kick players (!bot & !self)" ]
			uiButton "" [
				uitext "Spectate" 0.6
			] $uiPad:3XL $uiPad:6XL [
				listfilter= .cci_clients i [! (isai $i)]
				looplist cn $.cci_clients [ spectator 1 $cn ]
			] 0 [ uiSetTip "Put players into spectator mode (!bot)" ]
			uiButton "" [
				uitext "Unspectate" 0.6
			] $uiPad:3XL $uiPad:6XL [
				listfilter= .cci_clients i [! (isai $i)]
				looplist cn $.cci_clients [ spectator 0 $cn ]
			] 0 [ uiSetTip "Put players out of spectator mode (!bot)" ]
			uiButton "" [
				uitext "Swap Team" 0.6
			] $uiPad:3XL $uiPad:6XL [
				looplist cn $.cci_clients [ case (getclientteam $cn) 1 [ setteam $cn 2 ] 2 [ setteam $cn 1 ] ]
			] 0 [ uiSetTip "Swap teams for .cci_clients players" ]
		]
	]
] [] [] [] [ uitext "Client Control Interface" 0.8 ]

////////////////////////////////////////////////////////////////////////////////////////////////////


cp_updateselected = [ .cci_clients (listintersect $.cci_clients $arg1) ]

cp_client = [
	local selected ; selected = (>= (indexof $.cci_clients $arg1) 0)
	uiButtonArea (
			? $selected "hold1" "hold2"
		) (-f $uiPad:D6XL $uiPad:L) $uiPad:5XL [
		uirelease [
			if $selected [
				listdel= .cci_clients $arg1
			] [ append .cci_clients $arg1 ]
		]
		uispace $uiPad:S 0 [
			uiclamp.x
			uihlist 0 [
				uiclamp.x
				uicolortext (getclientcolorname $arg1) (scoreboardstatus $arg1) 0.55
				if $showclientnum [
					if (isai $arg1) [
						uitext (concatword (? $selected "^f8" "^f4") "BOT") 0.55
					] [ uitext (concatword (? $selected "^f8" "^f7") $arg1) 0.6 ]
					uialign- 1
				]
			]
		]
	]

]

cp_clientpanel = [
	uifill (-f $uiPad:D6XL $uiPad:L)
	looplist cn (
		listfilter i (listclients $.cci_showself) [
			&& [
				if $arg2 [isspectator $i] [! (isspectator $i)]
			] [
				case $arg1 1 [= (getclientteam $i) 1] 2 [= (getclientteam $i) 2] () 1
			] [
				if $.cci_showbots [result 1] [! (isai $i)]
			]
		]
	) [ cp_client $cn $arg1 $arg2 ]
]
