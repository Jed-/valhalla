
uiMenu "profile" [
	uihlist $uiPad:L [
		uigroup [
			uiplayerpreview $playermodel $[playercolor@(at ["" "blue" "red"] $.ps_team)] $.ps_team -1 $uiPad:D6XL $uiPad:USS [
				style_generic_box d d d d n
				uiclamp*e
			]
			uiAdvFieldCA .name 15 0.6 "" [] [ name $.name ] 0 0 [
				uialign 0 -1 ; uiclamp.x
			]
			uivlist 0 [
				uiButton "hold2" [
					uitext "Male" 0.6
					uiRadio [= $playermodel 0]
				] $uiPad:D4XL $uiPad:6XL [ playermodel = 0 ] 1
				uiButton "hold2" [
					uitext "Female" 0.6
					uiRadio [= $playermodel 1]
				] $uiPad:D4XL $uiPad:6XL [ playermodel = 1 ] 1
			] ; uialign- 1 1
		]
		uivlist 0 [
			uiclamp.y
			looplist2 idx var [
				0 "playercolor"
				1 "playercolorblue"
				2 "playercolorred"
			] [
				uivlist $uiPad:L [
					uitext (at "Solo ^f8Aesir ^f3Vanir" $idx) 0.7
					uigrid (? $idx 2 3) $uiPad:S $uiPad:S [
						uihover [ .ps_team = $idx ]
						loop n (+ (getvarmax $var) 1) [
							uicolor (getplayercolor $n $idx) $uiPad:SXL $uiPad:5XL [
								uioutline (if (= $$var $n) [ result $c_baige ] [ getplayercolor $n $idx ])
								uiclamp-e
								uipress [ $var $n ]
							]
						]
					]
				]
			]
			uivlist $uiPad:L [
				uitext "Custom flag" 0.7
				uifill 0 $uiPad:SXL [
					uiButton "hold2" [
						uihlist $uiPad:M [
							cases $customflag "" [] "geo" [
								uicroppedimage "data/interface/particle/editor_icons.png" $uiPad:SXL $uiPad:SXL 256p 0 128p 128p
							] () [
								uiFastImg "" "countryflags/" $customflag "" $uiPad:SXL $uiPad:5XL
							]
							uitext (? (=s "" $customflag) "choose" "change") 0.6
						]
					] $uiPad:SXL $uiPad:SXL [
						showui "customflags"
					]
				]
			]
		]
	]
] [
	.name = $name
	.ps_team = $getteam
] [] [] [ uitext "Player Profile" 0.8 ]

.customflags = [
]

.customflag_entry = [ // code name body
	uitarget 1 (*f 2 $uiPad:5XL) [
		style_button_hold1 "n" "n" "n" (? (=s [@@arg1] $customflag) "d" "n")
		uiclamp*e
		uirelease [ customflag [@@@arg1] ]
		uihlist 0 [
			uialign -1 0
			@@arg3
			uitext [ @@@arg2 ] 0.6
		]
	]
]

uiMenu "customflags" [
	uivlist 0 [
		uitext "Choose a flag to display next to your name" 0.5
		uiScrollBox "customflags" $uiPad:USS $uiPad:UXS [ uivlist 0 [
			uialign -1 -1
			.customflag_entry "" "None" [
				uifill $uiPad:SXL $uiPad:SXL []
			]
			.customflag_entry "geo" "Your country" [
				uicroppedimage "data/interface/particle/editor_icons.png" $uiPad:SXL $uiPad:SXL 256p 0 128p 128p
			]
			looplist e $.customflags [
				.customflag_entry (at $e 0) (at $e 1) [
					uiFastImg "" "countryflags/" (at $e 0) "" $uiPad:SXL $uiPad:5XL
				]
			]
		] ]
	]
]
