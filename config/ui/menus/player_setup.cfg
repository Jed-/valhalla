
uiMenu "profile" [
	uihlist $uiPad:L [
		uigroup [
			uiplayerpreview 0 $[playercolor@(at ["" "blue" "red"] $.ps_team)] $.ps_team $.ps_weap $uiPad:D6XL $uiPad:USS [
				style_generic_box d d d d n
				uiclamp*e
			]
			uiAdvFieldCA .name 15 0.6 "" [] [ name $.name ] 0 0 [
				uialign 0 -1 ; uiclamp.x
			]
			uivlist 0 [
				uiButton "hold2" [
					uitext "Shotgun" 0.6
					uiRadio [= $.ps_weap 0]
				] $uiPad:D4XL $uiPad:6XL [ .ps_weap = 0 ] 1
				uiButton "hold2" [
					uitext "Submachine Gun" 0.6
					uiRadio [= $.ps_weap 1]
				] $uiPad:D4XL $uiPad:6XL [ .ps_weap = 1 ] 1
				uiButton "hold2" [
					uitext "Pulse Rifle" 0.6
					uiRadio [= $.ps_weap 2]
				] $uiPad:D4XL $uiPad:6XL [ .ps_weap = 2 ] 1
				uiButton "hold2" [
					uitext "Rocket Launcher" 0.6
					uiRadio [= $.ps_weap 3]
				] $uiPad:D4XL $uiPad:6XL [ .ps_weap = 3 ] 1
				uiButton "hold2" [
					uitext "Railgun" 0.6
					uiRadio [= $.ps_weap 4]
				] $uiPad:D4XL $uiPad:6XL [ .ps_weap = 4 ] 1
			] ; uialign- 1 1
		]
		uivlist 0 [
			uiclamp.y
			looplist2 idx var [
				0 "playercolor"
				1 "playercolorblue"
				2 "playercolorred"
			] [
				uivlist $uiPad:L [
					uitext (at "Solo ^f8Aesir ^f3Vanir" $idx) 0.7
					uigrid (? $idx 2 3) $uiPad:S $uiPad:S [
						uihover [ .ps_team = $idx ]
						loop n (+ (getvarmax $var) 1) [
							uicolor (getplayercolor $n $idx) $uiPad:SXL $uiPad:5XL [
								uioutline (if (= $$var $n) [ result $c_baige ] [ getplayercolor $n $idx ])
								uiclamp-e
								uipress [ $var $n ]
							]
						]
					]
				]
			]
		]
	]
] [
	.name = $name
	.ps_team = $getteam
	.ps_weap = $getweapon
] [] [] [ uitext "Player Profile" 0.8 ]
