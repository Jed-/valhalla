///////////////////////////////////////////////////////////////////////////////
//  STORY MODE UI                                                            //
///////////////////////////////////////////////////////////////////////////////

.uiStoryTip = ""
.uiStoryTipMode = 0
.uiStoryTipType = 0
.uiStoryTipMillis = 0

uiLiteMenu .storyTip [
    uiallowinput 0
    uioffset 0 -0.84 [
        uispace $uiPad:S $uiPad:S [
            uiFastImgStretched [<fade:0.5><mad:0.5,0.5,0.5><agrad:.5,,.5,.95,.1>] "hud/" "shelf" "" [
                uispace $uiPad:O5 $uiPad:O5 [
                    uihlist $uiPad:S [
                        if $.uiStoryTipType [
                            (*f 1.0 (uiCosWave 250))
                            local character color alpha
                            alpha = 1.0
                            case $.uiStoryTipType 1 [
                                character = "="
                                color = $c_blue
                            ] 2 [
                                character = "!"
                                color = $c_yellow
                                alpha = (*f $alpha (uiCosWave 250))
                            ] 3 [
                                character = "%"
                                color = $c_green
                            ]
                            uifontcolorblendtext "modernpics.ol" $character $color $alpha 0.7
                        ]
                        local millis elapsed delay step initSize targetSize size alphaMultiplier alpha
                        millis = $.uiStoryTipMillis
                        elapsed = (- $getmillis $millis)
                        delay = 380
                        step = (getstepmillis $millis $delay)
                        initSize = 0.0
                        targetSize = 0.55
                        size = (lerpf $initSize $targetSize $step)
                        alphaMultiplier = (? (= $.uiStoryTipMode 1) (uiCosWave 500) 1.0)
                        if (>= $elapsed $delay) [
                            millis = (+ $millis $delay)
                            step = (getstepmillis $millis 250)
                            alpha = (lerpf 0.0 1.0 $step)
                            alpha = (*f $alpha $alphaMultiplier)
                        ] [
                            alpha = 0.0
                        ]
                        uifontcolorblendtext "wide.ol" $.uiStoryTip $c_white $alpha $size
                    ]
                ]
            ]
        ]
    ]
] []

.displayStoryTip = [
    .uiStoryTip = $arg1
    .uiStoryTipMode = $arg2
    .uiStoryTipType = $arg3
    .uiStoryTipMillis = $getmillis
    showui ".storyTip"
]

.uiStoryInteraction = ""

uiLiteMenu .storyInteraction [
    uiallowinput 0
    uioffset 0 0.12 [
       uifontcolorblendtext "wide.ol" $.uiStoryInteraction 0xFFFFFF 1.0 0.55
    ]
] []

.displayStoryInteraction = [
    .uiStoryInteraction = $arg1
    showui ".storyInteraction"
]

.clearStoryUi = [
    .uiStoryTip = ""
    .uiStoryTipMode = 0
    .uiStoryTipType = 0
    .uiStoryTipMillis = 0
    hideui ".storyTip"
    .uiStoryInteraction = ""
    hideui ".storyInteraction"
]