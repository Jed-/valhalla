
///////////////////////////////////////////////////////////////////////////////
//  GAMEHUD UI -- MAKE CHANGES AT THY OWN PERIL                              //
///////////////////////////////////////////////////////////////////////////////

newui "hud" [
	uiclamp.e
	if (|| $mainmenu $uiHideHUD) [ uiallowinput 0 ] [
		if $editing [
			uieschide 0
			//uiabovehud
			uispace $uiPad:M $uiPad:M [
				uihlist $uiPad:M [
					if (iskeyheld "LALT") [
						uiallowinput 1
						uiHudCell $uiPad:DM $uiPad:DXL [
							uiFastImg "" "ui/" "config" "" $uiPad:SXL
						]
					] [
						uiallowinput 0
						uiHudCell $uiPad:DM $uiPad:DXL [
							uivlist 0 [
								uiFastImg "" "ui/" "speed" "" $uiPad:6XL
								uitext $floatspeed 0.6
								uifill 0 $uiPad:S
								uigroup [
									uiFastImg "" "ui/" "grid" "" $uiPad:6XL
									uitext $gridpower 0.5
									uialign- 1 1
								]
							]
						]
					]
				]
			] ; uialign- -1 1

		] [
			uiallowinput 0
			if (m_timed $getmode) [ // var scoreboardtime otherwise
				uiFastImgStretched (fade 0.5) "hud/" "shelf" "" [
					uialign 0 -1
					uiFastImg <mad:0/0/0> "hud/" "glow" "" $uiPad:D2XL $uiPad:DM
					if $intermission [ uifontcolortext "mono" $timeremaining $c_orange 1.2 ] [
						uihlist $uiPad:O3 [
							uiFancyText "mono"       (div $timeremaining 60)    1.2
							uiFancyText "mono" ":"                              1.2
							uiFancyText "mono" (pad0 (mod $timeremaining 60) 2) 1.2
						]
					]
				] $uiPad:5XL
			]

			if (iskeyheld "TAB") [
				if (uivisible "scoreboard") [] [ showui "scoreboard" ]
				uivlist $uiPad:5XL [
					uialign 0 -1
					uifill 0 $uiPad:DM
					if (m_teammode $getmode) [
						.scoreboard.ctf
					] [ .scoreboard.ffa ]
					.scoreboard.spec
				]

				uispace $uiPad:L $uiPad:L [
					uivlist 0 [
						uiFancyText "wide" (getmodeprettyname $getmode) 1.2
						uifill 0 $uiPad:DM [ uivlist 0 [
							if $scoreboardmap [
								uiFancyText "wide" "- ON -" 0.8 $c_gray
								uiFancyText "wide" $scoreboardmap 1 $c_green
							]
						] ]
					]
					if (isconnected 1 0) [
						uigroup [
							uicolortext $scoreboardservinfo (|A! 0x48) 0.7
						] ; uialign- -1 1
					]
				] ; uiclamp-x ; uialign- 0 1
			] [
				if (uivisible "scoreboard") [ hideui "scoreboard" ]
				local x cn
				if $spectating [
					cn = $getfollow
					uivlist 0 [ // spectating display group
						uispace $uiPad:3XL 0 [
							uiFancyText "wide" "SPECTATING" 1.1 $c_yellow
						]
						uispace $uiPad:3XL 0 [
							uiFancyText "" (concat (
								getclientcolorname $cn
							) "") 0.95
						]
						uialign* 1
						uifill 0 $uiPad:DXL
					] ; uialign- 1 1
				] [ cn = $getclientnum ]

				x = (maxf 0 (-f $uiaspect $uiWideHUD))
				if (>f $x 0) [ x = (*f $x 0.5) ]

				uispace $uiPad:L $uiPad:L [ // killfeed display group
					uivlist 0 [
						local i timestamp
						looplistrev n $fragQueue [
							case $i 0 [ timestamp = $n ] 1 [
								do [ .killfeed.row @n ]
							] ; i = (mod (+ $i 1) 3)
						]
					]
				] ; uialign- 1 -1

				uispace (+f $uiPad:3XL $x) $uiPad:3XL [ // game HUD group
					//uioutline (rnd 0xFFFFFF) ; uiclamp-e // debug boundary outline
					
					if (|| [< $cn 0] [isdead $cn] [&& $thirdperson [> $getfollow -1]]) [] [
						uihlist 0 [
							// resource (shield/health) display group
							uivlist 0 [
								if (& $getmutators 1) [
									.resource.bar $uiPad:D6XL (getclientshield $cn) 200 shield 0xFF8C00 0xBD6300 0xFFDC2F 1.3
									uifill 0 $uiPad:S
								]
								.resource.bar $uiPad:DSXL (getclienthealth $cn) 100 health 0x006D46 0x00AB77 0x0EEB00 1.5
							]
							// powerup and duration display group
							pushif cp (getclientpowerup $cn) [
								uifill $uiPad:2XL
								uiFastImgStretched (fade 0.5) "hud/" "shelf" "" [
									uialign -2 1
									uispace $uiPad:M $uiPad:S [
										uivlist $uiPad:L [
											uiFastImg "" "hud/" (at [
												doubledmg haste armor infammo
												agility invulnerability
											] (- $cp 1)) "" (-f $uiPad:DXL $uiPad:M)
											.resource.bar $uiPad:DXL (getclientpowerupmillis $cn) (? (= $cp 6) 15000 30000) "" 0x00B5E8 0x098BE2 0x54E0FF
										]
									]
								] $uiPad:5XL
							]
						] ; uialign- -1 1
						if 0 [ // flag hold group // XXX incomplete due to lack of detect func
							uiFastImgStretched (fade 0.5) "hud/" "shelf" "" [
								uialign 1
								uispace $uiPad:M $uiPad:M [
									pushif n (getclientteam $cn) [
										n = (? (- $n 1) "blue" "red")
										uiFastImg "" "hud/" "flag_" $n $uiPad:DXL
									]
								]
							] $uiPad:5XL
						]
						if 1 [
							uihlist $uiPad:L [ // temp ammo group
								uiFastImg "" "hud/" (at [
									shotgun smg pulse rocket railgun pistol
								] (getclientweapon $cn)) "" $uiPad:DM
								uiclip 0 $uiPad:DM [
									uifonttext "wide" "    " 2.08
									uiFancyText "wide" (getclientammo $cn) 2
									uialign- -1
								]
							] ; uialign- 1 1
						] [
							uispace $uiPad:L 0 [ // to-be-eventually ammo group
								push n (at [shotgun smg pulse rocket railgun pistol] (getclientweapon $num)) [
									uiShadowedImg "" "hud/" $n "_raw" (*f (gettexaspectwh (
										concatword "data/interface/hud/" $n "_raw.png"
									)) $uiPad:DS) $uiPad:DS 2.5
								]
								uiFancyText "wide" (getclientammo $cn) 2
								uialign- 1 1
							] ; uialign- 1 1
						]
					]
				] ; uiclamp-x ; uialign- -1 1
			]
		]
	]
] [
	uiBitsHUD = 0
	showui "hud_"
]

// temporary measure to set Y pos of chat input
newui "hud_" [
	uiallowinput 0
	uiabovehud
	uialign -1 1
	uifill 0 0.14
]

// use: 1:WIDTH  2:VALUE  3:MAXVAL 4:ICON  5:COL1  6:COL2  7:COL3
.resource.bar = [
	local h x y
	h = (divf $arg1 10)
	x = (+f $uiPad:O4- $arg1)
	y = (+f $uiPad:O4- $h)

	if $arg4 [
		uifill (+f $arg1 $h) 0 [
			uivlist 0 [
				uispace $uiPad:O3 $uiPad:S- [
					uiFancyText "wide" $arg2 (*f $arg1 3.125) $arg7
				] ; uialign- 1
				uicolor 0 $arg1 $h [
					uicolor $arg6 $x $y [ if $arg2 [
						uihlist 0 [
							uiclip (minf $x (%f (max 0 $arg2) $arg3 $x)) $y [
								uihgradient $arg5 $arg7 $x $y
							]
							if (< $arg2 $arg3) [
								uioffset $uiPad:O1- 0 [
									uicolor 0 $uiPad:O2 $y
									uioutline 0 $uiPad:O2 $y
								]
							]
						] ; uialign- -1
					] ]
					uioutline 0 $arg1 $h
					uioutline 0 $x $y
				]
				uifill 0 (*f $uiPad:O1 0.5) // pixel alignment fix
			] ; uialign- 1 1
			uiFastImg "" "hud/" $arg4 "" (*f $h 2)
			uialign- -1 1
		] ; uialign- 1
	] [
		uicolor 0 $arg1 $h [
			uicolor $arg6 $x $y [ if $arg2 [
				uihlist 0 [
					uiclip (minf $x (%f (max 0 $arg2) $arg3 $x)) $y [
						uihgradient $arg5 $arg7 $x $y
					]
					if (< $arg2 $arg3) [
						uioffset $uiPad:O1- 0 [
							uicolor 0 $uiPad:O2 $y
							uioutline 0 $uiPad:O2 $y
						]
					]
				] ; uialign- -1
			] ]
			uioutline 0 $arg1 $h
			uioutline 0 $x $y
		]
	]
]

.killfeed.row = [
	local ms fl fr mul
	ms = (- $getmillis $timestamp)
	if (< $ms $uiKillDecay) [
		ms = (%f $ms         $uiKillDecay 1)
		fl = (%f $uiKillFade $uiKillDecay 1)
		fr = (-f 1 $fl)
		cond [<f $ms $fl] [
			mul = (%f $ms $fl 1)
		] [>f $ms $fr] [
			mul = (-f 1 (%f (-f $ms $fr) $fl 1))
		] [ mul = 1 ]
		if (<f $ms $fr) [
			uifill 0 (*f (-f 1 $mul) $uiPad:5XL)
			uiFastImgStretched <mad:.18/.18/.18> "hud/" "row" "" [
				uialign 1 -2
				uispace $uiPad:M 0 [
					uifill 0 $uiPad:5XL
					uihlist 0 [
						if (> $arg5 127) [
							uiShadowedImg "" "hud/" "bot" "" $uiPad:2XL
							uifill $uiPad:O3
						]
						uiFancyText "" $arg1 0.65 (INT:TRANS $arg7 * 2)
						if (|| [= $arg3 -2] [= $arg5 $arg6]) [
							uiFancyText "" " committed sudoku!" 0.65
							uifill $uiPad:XL
							uiShadowedImg "" "hud/" "death" "" $uiPad:3XL
						] [
							if (= $arg3 -1) [ icon = "melee" ] [
								//icon = (at "shotgun smg pulse rocket railgun pistol" $arg3)
								icon = (at "pistol pistol pistol rocket pistol pistol" $arg3)
							]
							uifill $uiPad:XL
							uiShadowedImg "" "hud/" $icon "_raw" (*f (gettexaspectwh (
								concatword "data/interface/hud/" $icon "_raw.png"
							)) $uiPad:3XL) $uiPad:3XL
							if $arg4 [
								uifill $uiPad:S
								uiShadowedImg "" "hud/" "crit" "" $uiPad:3XL
							]
							uifill $uiPad:XL
							if (> $arg6 127) [
								uiShadowedImg "" "hud/" "bot" "" $uiPad:2XL
								uifill $uiPad:O3
							]
							uiFancyText "" $arg2 0.65 (INT:TRANS $arg8 * 2)
						]
					]
				]
			] $uiPad:5XL
			uifill 0 $uiPad:L
		] [ uifill 0 (*f $mul (+f $uiPad:5XL $uiPad:L)) ]
	]
]

///////////////////////////////////////////////////////////////////////////////
//  Scoreboard                                                               //
///////////////////////////////////////////////////////////////////////////////

newui "scoreboard" [
	uiallowinput 0
	refreshscoreboard
] [
	uiSetBgBlur 1
	.sb_offset = 3
] [ uiSetBgBlur -1 ]

.scoreboard.update = [
	local bit cc

	if $getmode                 [ bit = (^ $bit 0x01) ; ++ cc ] // score
	if $showkills               [ bit = (^ $bit 0x02) ; ++ cc ] // frags
	if $showdeaths              [ bit = (^ $bit 0x04) ; ++ cc ] // deaths
	if $scoreboardmultiplayer [
		if $showpj              [ bit = (^ $bit 0x08) ; ++ cc ] // packet jump
		if $showping            [ bit = (^ $bit 0x10) ; ++ cc ] // ping
	]

	if (|| $arg1 [!= (getalias .sb_bitmask) $bit] [!= (getalias .sb_modemask) $bit]) [
		.sb_bitmask  = $bit
		.sb_modemask = $getmode

		local .sb_namewidth .sb_cellwidth .sb_rowwidth
		local .scoreboard.cells .scoreboard.icons
		local .scoreboard.header .scoreboard.row
		local .scoreboard.shelf.top3 .scoreboard.shelf.rest

		.sb_namewidth = $uiPad:UXS
		.sb_cellwidth = (+f $uiPad:DSS $uiPad:M)
		.sb_rowwidth  = (+f $.sb_namewidth (*f $.sb_cellwidth $cc))

		loop n 5 [ if (& $bit (<< 1 $n)) [
			append .scoreboard.cells [[uifill @@[.sb_cellwidth] 0 [@@@(at [
				[uiFancyText "" (min   (getclientscore  $cn)      9999) 0.58]
				[uiFancyText "" (clamp (getclientfrags  $cn) -999 9999) 0.58]
				[uiFancyText "" (min   (getclientdeaths $cn)      9999) 0.58]
				[if (scoreboardpj $cn)   [uiFancyText "" (min (scoreboardpj   $cn) 9999) 0.58] [uiFancyText "" "--" 0.58 $c_gray]]
				[if (scoreboardping $cn) [uiFancyText "" (min (scoreboardping $cn) 9999) 0.58] [uiFancyText "" "--" 0.58 $c_gray]]
			] $n)]]]
			append .scoreboard.icons (at [
				@@@(at "0 star star flag star crown crown star" $getmode)
				"frag" "death" "pj" "ping"
			] $n)
		] ]

		// 1:TEAM
		.scoreboard.header = [
			local bflip
			bflip = (= $arg1 2) // right-to-left flip bit
			result [
				uifill @[.sb_rowwidth] $uiPad:DM [
					uispace 0 $uiPad:S [
						uiFastImg <reorient:@@@bflip> "hud/" "scorebg"
						uiclamp-e
					] ; uiclamp-e
					uispace $uiPad:M $uiPad:M- [
						uiFancyText "wide" @@@(
							at "FFA Aesir Vanir" $arg1
						) 1.4 @@@col
					] ; uialign- @@(? $bflip 1 -1) -1
					uispace $uiPad:O5 (*f $uiPad:O5 2.9) [
						uiFancyText "wide" (getteamscore @@@arg1) 1 @@@(
							at [0x40D060 0x4060D0 0xD04040] $arg1
						)
					] ; uialign- @@(? $bflip -1 1) -1
					if (numscoreboard @@arg1) [
						uihlist 0 [
							@@@@(? $bflip [
								uialign -1 1
								@(looplistconcatrev x $.scoreboard.icons [ result [
									uifill @@[.sb_cellwidth] @@(*f $uiPad:O4 7) [
										uiShadowedImg "" "hud/" @@x "" $uiPad:3XL
									]
								] ])
							] [
								uialign 1 1
								@(looplistconcat x $.scoreboard.icons [ result [
									uifill @@[.sb_cellwidth] @@(*f $uiPad:O4 7) [
										uiShadowedImg "" "hud/" @@x "" $uiPad:3XL
									]
								] ])
							])
						]
					]
				]
			]
		]

		// 1:TEAM  2:[CHILDREN]  3:SIDE-EXTRAS
		.scoreboard.row = [
			local bflip col
			bflip = (= $arg1 2) // right-to-left flip bit
			col = [
				[@@@@(mad 0x303840)]                    // spec
				[@@@@(mad 0x303030) @@@@(mad 0x282828)] // ffa
				[@@@@(mad 0x1C2E70) @@@@(mad 0x122466)] // aesir
				[@@@@(mad 0x701C1C) @@@@(mad 0x661212)] // vanir
			]
			result [
				uiFastImgStretched @(? (> $arg1 -1) [(at [@@col] @(+ $arg1 1) (& $n 1))] [@(at $col 0)]) "hud/" "row" "" [
					uifill @@(? $arg3 $arg3 $.sb_rowwidth)
					@@(? $arg2 [ uigroup [ uiclamp.e ; @@arg2 ] ])
					if (scoreboardhighlight $cn) [
						uispace $uiPad:O3 $uiPad:O3 [
							uiShadowedImg <reorient:@@@@bflip> "hud/" "edge" "" $uiPad:M
						] ; uialign- @@@(? $bflip 1 -1) -1
					]
					@@(? $bflip [
						uihlist 0 [
							@@(looplistconcatrev x $.scoreboard.cells [ result [ @x; ] ])
						] ; uiclamp-y ; uialign- -1
						uispace $uiPad:M 0 [ uihlist $uiPad:O3 [
							if (isai $cn) [ uiShadowedImg "" "hud/" "bot" "" $uiPad:2XL ]
							uiFancyText "" (getclientname $cn) 0.6 (scoreboardstatus $cn)
						] ] ; uialign- 1
					] [
						uispace $uiPad:M 0 [ uihlist $uiPad:O3 [
							uiFancyText "" (getclientname $cn) 0.6 (scoreboardstatus $cn)
							if (isai $cn) [ uiShadowedImg "" "hud/" "bot" "" $uiPad:2XL ]
						] ] ; uialign- -1
						@(? (> $arg1 -1) [
							uihlist 0 [
								@@(looplistconcat x $.scoreboard.cells [ result [ @x; ] ])
							] ; uiclamp-y ; uialign- 1
						])
					])
				] $uiPad:5XL
				++ n
			]
		]

		if (m_teammode $getmode) [
			.scoreboard.aesir = [
				local n
				uivlist 0 [
					@@(.scoreboard.header 1)
					loopscoreboard cn 1 [ @@@(.scoreboard.row 1) ]
				] ; uialign- -2 -1
			]
			.scoreboard.vanir = [
				local n
				uivlist 0 [
					@@(.scoreboard.header 2)
					loopscoreboard cn 2 [ @@@(.scoreboard.row 2) ]
				] ; uialign- -2 -1
			]
			.scoreboard.ctf = [
				uigroup [
					uioffset 0 $uiPad:O5 [
						uioffset $uiPad:2XL- $uiPad:2XL- [ uiFancyText "wide" "V" 1 ]
						uioffset $uiPad:2XL  $uiPad:2XL  [ uiFancyText "wide" "S" 1 ]
					] ; uialign- 0 -1
					uihlist $uiPad:DM [
						.scoreboard.aesir
						.scoreboard.vanir
						uialign* -2 -1
					]
				]
			]
		]

		.scoreboard.spec = [
			uigrid 3 $uiPad:L 0 [
				loopscoreboard cn -1 [
					@@@(.scoreboard.row -1 [] $.sb_namewidth)
				]
			]
		]

		.scoreboard.shelf.top3 = [
			uioffset $uiPad:5XL- 0 [
				uiFastImg (fade 0.5) "hud/" "shelf" "" $uiPad:5XL
				push n (at [<mad:1/.8/0> <mad:.8/.8/.8> <mad:.8/.5/0>] $n) [
					uiFastImg $n "hud/" "glow"  "" $uiPad:5XL
					uiFastImg $n "hud/" "crown" "" $uiPad:3XL
				]
			] ; uialign- -1
		]
		.scoreboard.shelf.rest = [
			arg1 = (= $cn $getclientnum)
			uioffset $uiPad:5XL- 0 [
				uiFastImg (fade (? $arg1 0.5 0.25)) "hud/" "shelf" "" $uiPad:5XL
				uiFastImg <mad:.18/.18/.18> "hud/" "glow" "" $uiPad:5XL
				push n (+ $n 1) [
					uifontcolortext "def.ol" $n (|A! (? $arg1 0xB0 0x60)) 0.55
				]
			] ; uialign- -1
		]

		.scoreboard.ffa = [
			local n x
			uigroup [
				uivlist 0 [
					@@@(.scoreboard.header 0)
					loopscoreboard cn 0 [
						if (< $n 3) [
							if (= $cn $getclientnum) [ x = 1 ]
							@@@@@(.scoreboard.row 0 $.scoreboard.shelf.top3)
							if (= $n 3) [ uifill 0 $uiPad:M ]
						] [
							if (= $cn $getclientnum) [
								if (> $n (+ $.sb_offset 10)) [ uifill 0 $uiPad:M ]
								@@@@@@(.scoreboard.row 0 $.scoreboard.shelf.rest)
								if (< $n (+ $.sb_offset 0)) [ uifill 0 $uiPad:M ]
							] [
								if (&& [<= $n (+ $.sb_offset 10 $x $spectating)] [>= $n $.sb_offset]) [
									@@@@@@@(.scoreboard.row 0 $.scoreboard.shelf.rest)
								] [ ++ n ]
							]
						]
					]
				]
				uioffset $uiPad:2XL 0 [
					uialign 1
					// XXX what did I even mean to do here?
				]
			]
		]
	]
]
