
///////////////////////////////////////////////////////////////////////////////
//  GAMEHUD UI -- MAKE CHANGES AT THY OWN PERIL                              //
///////////////////////////////////////////////////////////////////////////////

newui "ghud" [
	uiallowinput 0
	uiclamp.e
	if (|| $editing $mainmenu $hidehud) [] [
		local i x ts ms fl fr mul col
		x = (-f $uiaspect $uiWideHUD)
		if (>f $x 0) [ x = (*f $x 0.5) ]
		uispace (+f $uiPad:L $x) $uiPad:L [
			uivlist 0 [
				looplistrev n $fragQueue [
					case (mod $i 3) 1 [
						do [ .killfeed.row @n ]
					] 0 [ ts = $n ] ; ++ i
				]
			] ; uialign- 1 -1
			uigroup [
				//uiHorColorSld
			] ; uialign- -1 1
			uigroup [

			] ; uialign- 1 1
		] ; uiclamp-e
	]
]

.killfeed.row = [
	ms = (- $getmillis $ts)
	if (< $ms $uiKillDecay) [
		ms = (%f $ms         $uiKillDecay 1)
		fl = (%f $uiKillFade $uiKillDecay 1)
		fr = (-f 1 $fl)
		cond [<f $ms $fl] [
			mul = (%f $ms $fl 1)
		] [>f $ms $fr] [
			mul = (-f 1 (%f (-f $ms $fr) $fl 1))
		] [ mul = 1 ]
		if (<f $ms $fr) [
			uifill 0 (*f (-f 1 $mul) $uiPad:5XL)
			col = (? (= $arg5 $getclientnum) [
				0x484848 0x26409E 0x9E2626
			] [ 0x383838 0x1C2E70 0x701C1C ])
			col = (mad (at $col (getclientteam $arg5)))
			uiFastImgStretched $col "ui/" "killfeed/" "row" [
				uialign 1 -2
				uispace $uiPad:M 0 [
					uifill 0 $uiPad:5XL
					uihlist 0 [
						if (> $arg5 127) [
							uiShadowImg "" "ui/" "scoreboard/" "bot" $uiPad:2XL
							uifill $uiPad:O3
						]
						uiFancyText "" $arg1 0.65
						if (|| [= $arg3 -2] [= $arg5 $arg6]) [
							uiFancyText "" " committed sudoku!" 0.65
							uifill $uiPad:XL
							uiShadowImg "" "ui/" "scoreboard/" "death" $uiPad:3XL
						] [
							if (= $arg3 -1) [ icon = "melee" ] [
								//icon = (at "shotgun smg pulse rocket railgun pistol" $arg3)
								icon = (at "pistol pistol pistol rocket pistol pistol" $arg3)
							]
							uifill $uiPad:XL
							uiShadowImg (
								if (> $arg3 -1) [ mad (at [
									0xFF3030 0xFFC040 0xC040C0
									0xFF8000 0x40FF80 0x50CFE5
								] $arg3) ] [ result "" ]
							) "ui/" "killfeed/" $icon (*f (gettexaspectwh (
								concatword "data/interface/ui/killfeed/" $icon ".png"
							)) $uiPad:3XL) $uiPad:3XL
							if $arg4 [
								uifill $uiPad:S
								uiShadowImg "" "ui/" "killfeed/" "crit" $uiPad:3XL
							]
							uifill $uiPad:XL
							if (> $arg6 127) [
								uiShadowImg "" "ui/" "scoreboard/" "bot" $uiPad:2XL
								uifill $uiPad:O3
							]
							uiFancyText "" $arg2 0.65
						]
					]
				]
			] $uiPad:5XL
			uifill 0 $uiPad:L
		] [ uifill 0 (*f $mul (+f $uiPad:5XL $uiPad:L)) ]
	]
]
