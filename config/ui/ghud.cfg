
///////////////////////////////////////////////////////////////////////////////
//  GAMEHUD UI -- MAKE CHANGES AT THY OWN PERIL                              //
///////////////////////////////////////////////////////////////////////////////

newui "ghud" [
	uieschide 0
	uiallowinput 0
	uialign 1 -1
	if (|| $editing $mainmenu $hidehud) [] [
		local  timeout  ms
		timeout = 3500
		uispace $uiPad:L $uiPad:L [
			uivlist 0 [
				looplistrev n $fragQueue [
					ms = (- $getmillis (at $n 0))
					if (< $ms $timeout) [
						local act tgt weap crit actn tgtn cur col
						act  = (at $n 1) ; crit = (at $n 4)
						tgt  = (at $n 2) ; actn = (at $n 5)
						weap = (at $n 3) ; tgtn = (at $n 6)
						cur  = (= $actn $getclientnum)
						col = (case (getclientteam $actn) 0 [
							? $cur <mad:.28/.28/.28> <mad:.20/.20/.20> ] 1 [
							? $cur <mad:.15/.25/.62> <mad:.11/.18/.44> ] 2 [
							? $cur <mad:.62/.15/.15> <mad:.44/.11/.11> ])

						UIfastimgstretched (concatword $col <agrad:1,0,1>) "ui/" "killfeed/" "row" [
							//UIfastimgstretched <agrad:0,1><mad:.1/.1/.2> "ui/" "killfeed/" "row" [ uiclamp.e ] $uiPad:5XL
							uialign 1 -2
							uispace $uiPad:M 0 [
								uifill 0 $uiPad:5XL
								uihlist 0 [
									if (isai $actn) [
										UIshadowimg "" "ui/" "scoreboard/" "bot" $uiPad:2XL
										uifill $uiPad:O3
									]
									UIfancytext "" $act 0.65
									if (|| [= $weap -2] [= $actn $tgtn]) [
										UIfancytext "" " committed sudoku!" 0.65
										uifill $uiPad:XL
										UIshadowimg "" "ui/" "scoreboard/" "death" $uiPad:3XL
									] [
										if (= $weap -1) [ icon = "melee" ] [
											//icon = (at "shotgun smg pulse rocket railgun pistol" $weap)
											icon = (at "pistol pistol pistol rocket pistol pistol" $weap)
										]
										uifill $uiPad:XL
										UIshadowimg (if (> $weap -1) [at [
											<mad:.99/.19/.19>
											<mad:.99/.75/.25>
											<mad:.75/.25/.75>
											<mad:.99/.50/.00>
											<mad:.25/.99/.50>
											<mad:.31/.81/.90>
										] $weap] [ result "" ]) "ui/" "killfeed/" $icon (*f (gettexaspectwh (
											concatword "data/interface/ui/killfeed/" $icon ".png"
										)) $uiPad:3XL) $uiPad:3XL
										if $crit [
											uifill $uiPad:S
											UIshadowimg "" "ui/" "killfeed/" "crit" $uiPad:3XL
										]
										uifill $uiPad:XL
										if (isai $tgtn) [
											UIshadowimg "" "ui/" "scoreboard/" "bot" $uiPad:2XL
											uifill $uiPad:O3
										]
										UIfancytext "" $tgt 0.65
									]
								]
							]
						] $uiPad:5XL
						uifill 0 $uiPad:L
					] [
						local decay
						decay = (max 0 (- 250 (- $ms $timeout)))
						decay = (*f (divf $decay 250) (+f $uiPad:5XL $uiPad:L))
						if $decay [ uifill 0 $decay ] [ listdel= fragQueue (escape $n) ]
					]
				]
			]
		]
	]
]
