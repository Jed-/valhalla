
///////////////////////////////////////////////////////////////////////////////
//  GAMEHUD UI -- MAKE CHANGES AT THY OWN PERIL                              //
///////////////////////////////////////////////////////////////////////////////

newui "ghud" [
	uieschide 0
	uiallowinput 0
	uialign 1 -1
	if (|| $editing $mainmenu $hidehud) [] [
		local  timeout  ms
		timeout = 2800
		uispace $uiPad:L $uiPad:L [
			uivlist 0 [
				looplistrev n $fragQueue [
					ms = (- $getmillis (at $n 0))
					if (< $ms $timeout) [
						local  actor  weap  crit  target
						actor  = (at $n 1)
						weap   = (at $n 3)
						crit   = (at $n 4)
						target = (at $n 2)
						UIfastimgstretched (
							if $crit [
								format "<mad:%1/%2/%3>" .2 .2 .2 // XXX
							] [ result <mad:.2/.2/.2> ]
						) "ui/" "killfeed/" "row" [
							uialign 1 -2
							uispace $uiPad:M 0 [
								if (= $weap -2) [
									uihlist $uiPad:L [
										UIshadowobj 1 <mad:0/0/0> <> [ UIfastimg %1 "ui/" "scoreboard/" "death" $uiPad:3XL ]
										UIfancytext "" (concat $actor "committed sudoku!") 0.65
									]
								] [
									uihlist 0 [
										UIfancytext "" $actor 0.65
										uifill $uiPad:L
										if (!= $weap -1) [
											UIshadowobj 1 <mad:0/0/0> (case $weap 0 [
												result <mad:.99/.19/.19> ] 1 [
												result <mad:.99/.75/.25> ] 2 [
												result <mad:.75/.25/.75> ] 3 [
												result <mad:.99/.50/.00> ] 4 [
												result <mad:.25/.99/.50> ] 5 [
												result <mad:.31/.81/.90> ]
											) [ UIfastimg %1  "ui/" "killfeed/" "pistol" $uiPad:5XL ]
										] [ UIshadowobj 1 <mad:0/0/0> <> [ UIfastimg %1 "ui/" "killfeed/" "melee" $uiPad:5XL ] ]
										UIshadowobj $crit <mad:0/0/0> <> [ UIfastimg %1 "ui/" "killfeed/" "crit" $uiPad:5XL ]
										uifill $uiPad:L
										UIfancytext "" $target 0.65
									]
								]
							]
						] $uiPad:5XL
						uifill 0 $uiPad:L
					] [
						local decay
						decay = (max 0 (- 250 (- $ms $timeout)))
						decay = (*f (divf $decay 250) (+f $uiPad:5XL $uiPad:L))
						if $decay [ uifill 0 $decay ] [ fragQueue = (listdel $fragQueue (escape $n)) ]
					]
				]
			]
		]
	]
]
