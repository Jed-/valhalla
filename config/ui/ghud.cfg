
///////////////////////////////////////////////////////////////////////////////
//  GAMEHUD UI -- MAKE CHANGES AT THY OWN PERIL                              //
///////////////////////////////////////////////////////////////////////////////

newui "ghud" [
	uiallowinput 0
	uiclamp.e
	if (|| $editing $mainmenu $hidehud) [] [
		local i x cn ts ms fl fr mul

		x = (-f $uiaspect $uiWideHUD)
		if (>f $x 0) [ x = (*f $x 0.5) ]
		if (isspectator $getclientnum) [
			num = $getfollow
		] [ num = $getclientnum ]

		uispace (+f $uiPad:L $x) $uiPad:L [
			//uioutline (rnd 0xFFFFFF) ; uiclamp-e
			uivlist 0 [
				looplistrev n $fragQueue [
					case (mod $i 3) 1 [
						do [ .killfeed.row @n ]
					] 0 [ ts = $n ] ; ++ i
				]
			] ; uialign- 1 -1
			if (|| [< $num 0] [isdead $num]) [] [
				uivlist 0 [
					uigroup [
						uihlist 0 [
							uifill (+f $uiPad:SXL $uiPad:L)
							uivlist 0 [
								// resource (shield/health) display group
								if (& $getmutators 1) [
									local sc sp
									sc = (max 0 (getclientshield $num))
									sp = (%f $sc 200 1)
									.resource.bar $uiPad:D6XL $sc 200 $sp shield 0xFF8C00 0xBD6300 0xFFDC2F 1.3
									uifill 0 $uiPad:S
								]
								local hc sc
								hc = (max 0 (getclienthealth $num))
								hp = (%f $hc 100 1)
								.resource.bar $uiPad:DSXL $hc 100 $hp health 0x006D46 0x00AB77 0x0EEB00 1.5
							]
							uifill $uiPad:3XL
							// powerup and duration display group
							local cp pt pd pp
							cp = (getclientpowerup $num)
							pt = (getclientpowerupmillis $num)
							pd = (? (= $cp 6) 15000 30000)
							pp = (%f $pt $pd 1)
							if $cp [ // powerup group
								uiFastImgStretched (fade 0.5) "hud/" "shelf" "" [
									uialign -1 1
									uispace $uiPad:M $uiPad:S [
										uivlist $uiPad:L [
											uiFastImg "" "hud/" (at [
												doubledmg haste armor infammo
												agility invulnerability
											] (- $cp 1)) "" (-f $uiPad:DXL $uiPad:M)
											.resource.bar $uiPad:DXL $pt $pd $pp "" 0x00B5E8 0x098BE2 0x54E0FF
										]
									]
								] $uiPad:5XL
							]
						] ; uialign- -1 1
						if 0 [ // flag hold group // XXX incomplete due to lack of detect func
							uiFastImgStretched (fade 0.5) "hud/" "shelf" "" [
								uialign 1
								uispace $uiPad:M $uiPad:M [
									push n (case (getclientteam $num) 1 [ result "red" ] [ result "blue" ]) [
										uiFastImg "" "hud/" "flag_" $n $uiPad:DXL
									]
								]
							] $uiPad:5XL
						]
						if 1 [
							uihlist $uiPad:L [ // temp ammo group
								uiFastImg "" "hud/" (at [
									shotgun smg pulse rocket railgun pistol
								] (getclientweapon $num)) "" $uiPad:DM
								uiclip 0 $uiPad:DM [
									uifonttext "wide" "    " 2.08
									uiFancyText "wide" (getclientammo $num) 2
									uialign- -1
								]
								uifill
							] ; uialign- 1 1
						] [
							uispace $uiPad:L 0 [ // to-be-eventually ammo group
								push n (at [shotgun smg pulse rocket railgun pistol] (getclientweapon $num)) [
									uiShadowImg "" "hud/" $n "_raw" (*f (gettexaspectwh (
										concatword "data/interface/hud/" $n "_raw.png"
									)) $uiPad:DS) $uiPad:DS 2.5
								]
								uiFancyText "wide" (getclientammo $num) 2
								uialign- 1 1
							] ; uialign- 1 1
						]
					] ; uiclamp-x
					uifill 0 $uiPad:L
				] ; uiclamp-x ; uialign- -1 1	
			]
		] ; uiclamp-e
	]
]

// use: X  VAL  MAX  %PC  ICON  COL1  COL2  COL3  TEXT-SIZE  CHILDREN
.resource.bar = [
	local h x y
	h = (divf $arg1 10)
	x = (+f $uiPad:O4- $arg1)
	y = (+f $uiPad:O4- $h)
	uifill $arg1 (? $arg5 (*f $h 2) $h) [
		uivlist 0 [
			if $arg9 [
				uispace $uiPad:O3 $uiPad:S- [
					uiFancyText "wide" $arg2 $arg9 $arg8
				] ; uialign- 1
			]
			uicolor 0 $arg1 $h [
				uicolor $arg7 $x $y [ if $arg2 [
					uihlist 0 [
						uiclip (*f $x (minf 1 $arg4)) $y [
							uihgradient $arg6 $arg8 $x $y
						]
						if (< $arg2 $arg3) [
							uioffset (*f $uiPad:O1- 0.5) 0 [
								uicolor 0 $uiPad:O1 $y [
									uioutline 0 $uiPad:O1 $y
								]
							]
						]
					] ; uialign- -1
				] ]
				arg10
				uioutline 0 $arg1 $h
				uioutline 0 $x $y
			]
			if $arg5 [ uifill 0 0.0005 ] // pixel alignment fix
		] ; uialign- 1 1
		if $arg5 [
			uioffset (!f $h) 0 [
				uiFastImg "" "hud/" $arg5 "" (*f $h 2)
				uiclamp
			] ; uialign- -1 1
		]
	] ; uialign- 1
]

.killfeed.row = [
	ms = (- $getmillis $ts)
	if (< $ms $uiKillDecay) [
		ms = (%f $ms         $uiKillDecay 1)
		fl = (%f $uiKillFade $uiKillDecay 1)
		fr = (-f 1 $fl)
		cond [<f $ms $fl] [
			mul = (%f $ms $fl 1)
		] [>f $ms $fr] [
			mul = (-f 1 (%f (-f $ms $fr) $fl 1))
		] [ mul = 1 ]
		if (<f $ms $fr) [
			uifill 0 (*f (-f 1 $mul) $uiPad:5XL)
			uiFastImgStretched @@@(mad 0x303030) "hud/" "row" "" [
				uialign 1 -2
				uispace $uiPad:M 0 [
					uifill 0 $uiPad:5XL
					uihlist 0 [
						if (> $arg5 127) [
							uiShadowImg "" "hud/" "bot" "" $uiPad:2XL
							uifill $uiPad:O3
						]
						uiFancyText "" $arg1 0.65 (INT:TRANS $arg7 * 2)
						if (|| [= $arg3 -2] [= $arg5 $arg6]) [
							uiFancyText "" " committed sudoku!" 0.65
							uifill $uiPad:XL
							uiShadowImg "" "hud/" "death" "" $uiPad:3XL
						] [
							if (= $arg3 -1) [ icon = "melee" ] [
								//icon = (at "shotgun smg pulse rocket railgun pistol" $arg3)
								icon = (at "pistol pistol pistol rocket pistol pistol" $arg3)
							]
							uifill $uiPad:XL
							uiShadowImg "" "hud/" $icon "_raw" (*f (gettexaspectwh (
								concatword "data/interface/hud/" $icon "_raw.png"
							)) $uiPad:3XL) $uiPad:3XL
							if $arg4 [
								uifill $uiPad:S
								uiShadowImg "" "hud/" "crit" "" $uiPad:3XL
							]
							uifill $uiPad:XL
							if (> $arg6 127) [
								uiShadowImg "" "hud/" "bot" "" $uiPad:2XL
								uifill $uiPad:O3
							]
							uiFancyText "" $arg2 0.65 (INT:TRANS $arg8 * 2)
						]
					]
				]
			] $uiPad:5XL
			uifill 0 $uiPad:L
		] [ uifill 0 (*f $mul (+f $uiPad:5XL $uiPad:L)) ]
	]
]
