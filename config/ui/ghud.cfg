
///////////////////////////////////////////////////////////////////////////////
//  GAMEHUD UI -- MAKE CHANGES AT THY OWN PERIL                              //
///////////////////////////////////////////////////////////////////////////////

newui "ghud" [
	uiallowinput 0
	uiclamp.e
	if (|| $editing $mainmenu $hidehud) [] [
		local i x cn ts ms fl fr mul

		x = (-f $uiaspect $uiWideHUD)
		if (>f $x 0) [ x = (*f $x 0.5) ]
		if (isspectator $getclientnum) [
			num = $getfollow
		] [ num = $getclientnum ]

		uispace (+f $uiPad:L $x) $uiPad:L [

			uivlist 0 [
				looplistrev n $fragQueue [
					case (mod $i 3) 1 [
						do [ .killfeed.row @n ]
					] 0 [ ts = $n ] ; ++ i
				]
			] ; uialign- 1 -1

			if (> $num -1) [
				uivlist 0 [
					uihlist 0 [
						uifill $uiPad:5XL
						uivlist 0 [
							local hc sc hp sp
							hc = (max 0 (getclienthealth $num))
							sc = (max 0 (getclientshield $num))
							hp = (%f $hc 100 1)
							sp = (%f $sc 200 1)
							uigroup [ // shield group
								uicolor 0 (+f $uiPad:D5XL $uiPad:O3) (+f $uiPad:5XL $uiPad:O3) [
									uihgradient 0xFF8C00 0xBD6300 $uiPad:D5XL $uiPad:5XL [
										if $sc [ uihlist 0 [
											uiclip (*f $uiPad:D5XL $sp) $uiPad:5XL [
												uihgradient 0xFF8C00 0xFFDC2F $uiPad:D5XL $uiPad:5XL
											]
											if (< $sc 200) [ uiline 0xBD6300 ; uiclamp-y ]
										] ; uialign- -1 ]
										uioutline 0xBD6300 ; uiclamp-e
									]
									uioutline 0 ; uiclamp-e
								] ; uialign- 1 1
								uioffset 0 $uiPad:L- [ uispace $uiPad:O3 0 [
									uiFancyText "wide" $sc 1.1 0xFFDC2F
								] ] ; uialign- 1 -1
								uioffset $uiPad:5XL- 0 [
									uiFastImg "" "hud/" "shield" "" (*f $uiPad:5XL 2)
								] ; uialign- -1 1
							] ; uialign- 1
							uifill 0 $uiPad:5XL
							uigroup [ // health group
								uicolor 0 (+f $uiPad:D6XL $uiPad:O3) (+f $uiPad:6XL $uiPad:O3) [
									uihgradient 0x00AB77 0x006D46 $uiPad:D6XL $uiPad:6XL [
										if $hc [ uihlist 0 [
											uiclip (*f $uiPad:D6XL (minf 1 $hp)) $uiPad:6XL [
												uihgradient 0x00AB77 0x0EEB00 $uiPad:D6XL $uiPad:6XL
											]
											if (< $hc 100) [ uiline 0x006D46 ; uiclamp-y ]
										] ; uialign- -1 ]
										uioutline 0x006D46 ; uiclamp-e
									]
									uioutline 0 ; uiclamp-e
								] ; uialign- 1 1
								uioffset 0 $uiPad:L- [ uispace $uiPad:O3 0 [
									uiFancyText "wide" $hc 1.3 0x0EEB00
								] ] ; uialign- 1 -1
								uioffset $uiPad:6XL- 0 [
									uiFastImg "" "hud/" "health" "" $uiPad:DM
								] ; uialign- -1 1
							]
						] ; uialign- -1 1
						uifill $uiPad:L
						uigroup [ // powerup group
							local pu
							pu = (getclientpowerup $num)
							if 1 [ //$pu [
								uiFastImg "" "hud/" (at [
									doubledmg haste armor infammo
									agility invulnerability
								] (- $pu 1)) "" $uiPad:DL 0 [
									uioutline 0 ; uiclamp-e
									uiFancyText "" (precf (divf (getclientpowerupmillis $num) 1000) 1) 0.8
									uialign- 1 -1
								]
							]
						] ; uialign- -1 1
					] ; uialign- -1
					uifill 0 $uiPad:DM
				] ; uiclamp-x ; uialign- 0 1
				uigroup [
					// ammo group
				] ; uialign- 1 1
			]
		] ; uiclamp-e
	]
]

.killfeed.row = [
	ms = (- $getmillis $ts)
	if (< $ms $uiKillDecay) [
		ms = (%f $ms         $uiKillDecay 1)
		fl = (%f $uiKillFade $uiKillDecay 1)
		fr = (-f 1 $fl)
		cond [<f $ms $fl] [
			mul = (%f $ms $fl 1)
		] [>f $ms $fr] [
			mul = (-f 1 (%f (-f $ms $fr) $fl 1))
		] [ mul = 1 ]
		if (<f $ms $fr) [
			uifill 0 (*f (-f 1 $mul) $uiPad:5XL)
			uiFastImgStretched @@@(mad 0x303030) "hud/" "row" "" [
				uialign 1 -2
				uispace $uiPad:M 0 [
					uifill 0 $uiPad:5XL
					uihlist 0 [
						if (> $arg5 127) [
							uiShadowImg "" "hud/" "bot" "" $uiPad:2XL
							uifill $uiPad:O3
						]
						uiFancyText "" $arg1 0.65 (INT:TRANS $arg7 * 2)
						if (|| [= $arg3 -2] [= $arg5 $arg6]) [
							uiFancyText "" " committed sudoku!" 0.65
							uifill $uiPad:XL
							uiShadowImg "" "hud/" "death" "" $uiPad:3XL
						] [
							if (= $arg3 -1) [ icon = "melee" ] [
								//icon = (at "shotgun smg pulse rocket railgun pistol" $arg3)
								icon = (at "pistol pistol pistol rocket pistol pistol" $arg3)
							]
							uifill $uiPad:XL
							uiShadowImg "" "hud/" $icon "_raw" (*f (gettexaspectwh (
								concatword "data/interface/hud/" $icon "_raw.png"
							)) $uiPad:3XL) $uiPad:3XL
							if $arg4 [
								uifill $uiPad:S
								uiShadowImg "" "hud/" "crit" "" $uiPad:3XL
							]
							uifill $uiPad:XL
							if (> $arg6 127) [
								uiShadowImg "" "hud/" "bot" "" $uiPad:2XL
								uifill $uiPad:O3
							]
							uiFancyText "" $arg2 0.65 (INT:TRANS $arg8 * 2)
						]
					]
				]
			] $uiPad:5XL
			uifill 0 $uiPad:L
		] [ uifill 0 (*f $mul (+f $uiPad:5XL $uiPad:L)) ]
	]
]
