
///////////////////////////////////////////////////////////////////////////////
//  GAMEHUD UI -- MAKE CHANGES AT THY OWN PERIL                              //
///////////////////////////////////////////////////////////////////////////////

newui "ghud" [
	uiallowinput 0
	uiclamp.e
	if (|| $editing $mainmenu $hidehud) [] [
		local i x ; x = (-f $uiaspect $uiWideHUD)
		if (>f $x 0) [ x = (*f $x 0.5) ]
		uispace (+f $uiPad:L $x) $uiPad:L [
			uivlist 0 [
				looplistrev n $fragQueue [	
					if (& $i 1) [] [ .killfeed.row @n ]
					++ i
				]
			] ; uialign- 1 -1
			uigroup [
				//uiHorColorSld
			] ; uialign- -1 1
			uigroup [

			] ; uialign- 1 1
		] ; uiclamp-e
	]
]

.killfeed.row = [
	arg1 = (- $getmillis $arg1)
	if (< $arg1 $uiKillDecay) [
		local t z1 z2 mul
		t =  (%f $arg1        $uiKillDecay 1)
		z1 = (%f $uiKillFade  $uiKillDecay 1)
		z2 = (-f 1 $z1)
		cond [<f $t $z1] [
			mul = (%f $t $z1 1)
		] [>f $t $z2] [
			mul = (-f 1 (%f (-f $t $z2) $z1 1))
		] [ mul = 1 ]
		if (<f $t $z2) [
			uifill 0 (*f (-f 1 $mul) $uiPad:5XL)
			arg8 = (? (= $arg6 $getclientnum) [
				0x484848 0x26409E 0x9E2626
			] [ 0x383838 0x1C2E70 0x701C1C ])
			arg8 = (mad (at $arg8 (getclientteam $arg6)))
			uiFastImgStretched $arg8 "ui/" "killfeed/" "row" [
				uialign 1 -2
				uispace $uiPad:M 0 [
					uifill 0 $uiPad:5XL
					uihlist 0 [
						if (> $arg6 127) [
							uiShadowImg "" "ui/" "scoreboard/" "bot" $uiPad:2XL
							uifill $uiPad:O3
						]
						uiFancyText "" $arg2 0.65
						if (|| [= $arg4 -2] [= $arg6 $arg7]) [
							uiFancyText "" " committed sudoku!" 0.65
							uifill $uiPad:XL
							uiShadowImg "" "ui/" "scoreboard/" "death" $uiPad:3XL
						] [
							if (= $arg4 -1) [ icon = "melee" ] [
								//icon = (at "shotgun smg pulse rocket railgun pistol" $arg4)
								icon = (at "pistol pistol pistol rocket pistol pistol" $arg4)
							]
							uifill $uiPad:XL
							uiShadowImg (
								if (> $arg4 -1) [ mad (at [
									0xFF3030 0xFFC040 0xC040C0
									0xFF8000 0x40FF80 0x50CFE5
								] $arg4) ] [ result "" ]
							) "ui/" "killfeed/" $icon (*f (gettexaspectwh (
								concatword "data/interface/ui/killfeed/" $icon ".png"
							)) $uiPad:3XL) $uiPad:3XL
							if $arg5 [
								uifill $uiPad:S
								uiShadowImg "" "ui/" "killfeed/" "crit" $uiPad:3XL
							]
							uifill $uiPad:XL
							if (> $arg7 127) [
								uiShadowImg "" "ui/" "scoreboard/" "bot" $uiPad:2XL
								uifill $uiPad:O3
							]
							uiFancyText "" $arg3 0.65
						]
					]
				]
			] $uiPad:5XL
			uifill 0 $uiPad:L
		] [ uifill 0 (*f $mul (+f $uiPad:5XL $uiPad:L)) ]
	]
]
