
///////////////////////////////////////////////////////////////////////////////
//  GAMEHUD UI -- MAKE CHANGES AT THY OWN PERIL                              //
///////////////////////////////////////////////////////////////////////////////

newui "ghud" [
	uiallowinput 0
	uiclamp.e
	if (|| $editing $mainmenu $hidehud) [] [
		local i x ts ms fl fr mul
		x = (-f $uiaspect $uiWideHUD)
		if (>f $x 0) [ x = (*f $x 0.5) ]
		uispace (+f $uiPad:L $x) $uiPad:L [
			uivlist 0 [
				looplistrev n $fragQueue [
					case (mod $i 3) 1 [
						do [ .killfeed.row @n ]
					] 0 [ ts = $n ] ; ++ i
				]
			] ; uialign- 1 -1
			if (isspectator $getclientnum) [] [
				uivlist 0 [
					local hm hc sc hp sp 
					hm = (getclientmaxhealth $getclientnum)
					hc = (clamp (getclienthealth $getclientnum) 0 $hm)
					sc = (clamp (getclientshield $getclientnum) 0 200)
					hp = (clampf (%f $hc $hm 1) 0 1)
					sp = (clampf (%f $sc 200 1) 0 1)
					uigroup [
						uioffset $uiPad:O3- $uiPad:O5 [
							uicolor (|A 0x80) $uiPad:D5XL $uiPad:5XL
						] ; uialign- -1 1
						uihgradient 0xFF8D00 0xBD6300 $uiPad:D5XL $uiPad:5XL [
							uihgradient 0xFF8D00 0xFFDB2D (*f $uiPad:D5XL $sp) 0 [
								uiline 0xBD6300 ; uiclamp-y ; uialign- 1	
							] ; uiclamp-y ; uialign- -1
							uioutline 0xBD6300
							uiclamp-e
						]
						uioffset 0 $uiPad:SXL- [ uispace $uiPad:O3 0 [
							uiFancyText "wide" $sc 1.3 0xFFDB2D
						] ] ; uialign- 1 -1
					] ; uialign- 1
					uifill 0 $uiPad:SXL
					uigroup [
						uioffset $uiPad:O3- $uiPad:O5 [
							uicolor (|A 0x80) $uiPad:D6XL $uiPad:6XL
						] ; uialign- -1 1
						uihgradient 0x00AD76 0x006D46 $uiPad:D6XL $uiPad:6XL [
							uihgradient 0x00AD76 0x06EA00 (*f $uiPad:D6XL $hp) 0 [
								uiline 0x006D46 ; uiclamp-y ; uialign- 1	
							] ; uiclamp-y ; uialign- -1
							uioutline 0x006D46
							uiclamp-e
						]
						uioffset 0 $uiPad:SXL- [ uispace $uiPad:O3 0 [
							uiFancyText "wide" $hc 1.3 0x06EA00
						] ] ; uialign- 1 -1
					]
					uifill 0 $uiPad:DL
				] ; uialign- -1 1
				uigroup [

				] ; uialign- 1 1
			]
		] ; uiclamp-e
	]
]

.killfeed.row = [
	ms = (- $getmillis $ts)
	if (< $ms $uiKillDecay) [
		ms = (%f $ms         $uiKillDecay 1)
		fl = (%f $uiKillFade $uiKillDecay 1)
		fr = (-f 1 $fl)
		cond [<f $ms $fl] [
			mul = (%f $ms $fl 1)
		] [>f $ms $fr] [
			mul = (-f 1 (%f (-f $ms $fr) $fl 1))
		] [ mul = 1 ]
		if (<f $ms $fr) [
			uifill 0 (*f (-f 1 $mul) $uiPad:5XL)
			uiFastImgStretched @@@(mad 0x303030) "hud/" "row" "" [
				uialign 1 -2
				uispace $uiPad:M 0 [
					uifill 0 $uiPad:5XL
					uihlist 0 [
						if (> $arg5 127) [
							uiShadowImg "" "hud/" "bot" "" $uiPad:2XL
							uifill $uiPad:O3
						]
						uiFancyText "" $arg1 0.65 (INT:TRANS $arg7 * 2)
						if (|| [= $arg3 -2] [= $arg5 $arg6]) [
							uiFancyText "" " committed sudoku!" 0.65
							uifill $uiPad:XL
							uiShadowImg "" "hud/" "death" "" $uiPad:3XL
						] [
							if (= $arg3 -1) [ icon = "melee" ] [
								//icon = (at "shotgun smg pulse rocket railgun pistol" $arg3)
								icon = (at "pistol pistol pistol rocket pistol pistol" $arg3)
							]
							uifill $uiPad:XL
							uiShadowImg "" "hud/" $icon "_raw" (*f (gettexaspectwh (
								concatword "data/interface/hud/" $icon "_raw.png"
							)) $uiPad:3XL) $uiPad:3XL
							if $arg4 [
								uifill $uiPad:S
								uiShadowImg "" "hud/" "crit" "" $uiPad:3XL
							]
							uifill $uiPad:XL
							if (> $arg6 127) [
								uiShadowImg "" "hud/" "bot" "" $uiPad:2XL
								uifill $uiPad:O3
							]
							uiFancyText "" $arg2 0.65 (INT:TRANS $arg8 * 2)
						]
					]
				]
			] $uiPad:5XL
			uifill 0 $uiPad:L
		] [ uifill 0 (*f $mul (+f $uiPad:5XL $uiPad:L)) ]
	]
]
