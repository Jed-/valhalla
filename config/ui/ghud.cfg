
///////////////////////////////////////////////////////////////////////////////
//  GAMEHUD UI -- MAKE CHANGES AT THY OWN PERIL                              //
///////////////////////////////////////////////////////////////////////////////

newui "ghud" [
	uieschide 0
	uiallowinput 0
	uialign 1 -1
	if (|| $editing $mainmenu $hidehud) [] [
		uispace $uiPad:L $uiPad:L [
			uivlist 0 [
				local x
				looplistrev n $fragQueue [
					do [ .killfeed.row @n ]
					++ x
				]
			]
		]
	]
]

.killfeed.row = [
	arg1 = (- $getmillis $arg1)
	if (< $arg1 $uiKillDecay) [
		arg8 = (? (= $arg6 $getclientnum) [
			0x484848 0x26409E 0x9E2626
		] [ 0x383838 0x1C2E70 0x701C1C ])
		arg8 = (at $arg8 (getclientteam $arg6))
		UIfastimgstretched (mad $arg8) "ui/" "killfeed/" "row" [
			uialign 1 -2
			uispace $uiPad:M 0 [
				uifill 0 $uiPad:5XL
				uihlist 0 [
					if (> $arg6 127) [
						UIshadowimg "" "ui/" "scoreboard/" "bot" $uiPad:2XL
						uifill $uiPad:O3
					]
					UIfancytext "" $arg2 0.65
					if (|| [= $arg4 -2] [= $arg6 $arg7]) [
						UIfancytext "" " committed sudoku!" 0.65
						uifill $uiPad:XL
						UIshadowimg "" "ui/" "scoreboard/" "death" $uiPad:3XL
					] [
						if (= $arg4 -1) [ icon = "melee" ] [
							//icon = (at "shotgun smg pulse rocket railgun pistol" $arg4)
							icon = (at "pistol pistol pistol rocket pistol pistol" $arg4)
						]
						uifill $uiPad:XL
						UIshadowimg (
							if (> $arg4 -1) [ mad (at [
								0xFF3030 0xFFC040 0xC040C0
								0xFF8000 0x40FF80 0x50CFE5
							] $arg4) ] [ result "" ]
						) "ui/" "killfeed/" $icon (*f (gettexaspectwh (
							concatword "data/interface/ui/killfeed/" $icon ".png"
						)) $uiPad:3XL) $uiPad:3XL
						if $arg5 [
							uifill $uiPad:S
							UIshadowimg "" "ui/" "killfeed/" "crit" $uiPad:3XL
						]
						uifill $uiPad:XL
						if (> $arg7 127) [
							UIshadowimg "" "ui/" "scoreboard/" "bot" $uiPad:2XL
							uifill $uiPad:O3
						]
						UIfancytext "" [@@arg3] 0.65
					]
				]
			]
		] $uiPad:5XL
		uifill 0 $uiPad:L
	] [
		local decay
		decay = (max 0 (- 250 (- $arg1 $uiKillDecay)))
		decay = (*f (divf $decay 250) (+f $uiPad:5XL $uiPad:L))
		if $decay [ uifill 0 $decay ] [ listdel= fragQueue (escape $n) ]
	]
]
