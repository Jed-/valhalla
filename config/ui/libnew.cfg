
////////////////////////////////////////////////////////////////////////////////////////
//                                                                                    //
//                    UI  LIBRARY  LEGEND                                             //
//                                                                                    //                                                                         //
////////////////////////////////////////////////////////////////////////////////////////

fullconsolecommand 0




// usage: /toggleUI "name" MILLIS FORCE-STATE ACTION
toggleUI = [
	local idx.n idx.s state
	idx.n = (indexof $uiWindowsAnim $arg1)

	if (> $idx.n -1) [
		idx.s = (+ $idx.n 1)
		state = (at $uiWindowsAnim $idx.s)
		if (&& [> $numargs 2] [= $arg4 $state]) [] [
			if $arg2 [] [ arg2 = $uiWindowSpeed ]
			[.@arg1.time] = (getmillis 1)
			[.@arg1.anim] = $arg2
			[.@arg1.crop] = (% (*f $arg2 $[.@arg1.fade]) -100)
			if $state [ [.@arg1.crop] = (+ $arg2 $[.@arg1.crop]) ]
			set uiWindowsAnim (listsplice $uiWindowsAnim (! $state) $idx.s 1)
		]
	] [
		if $arg2 [] [ arg2 = $uiWindowSpeed ]
		[.@arg1.time] = (getmillis 1)
		[.@arg1.anim] = $arg2
		if (> $numargs 2) [
			if (^ $arg4 (uivisible $arg1)) [
				set uiWindowsAnim (concat $uiWindowsAnim $arg1 $arg4)
			]
		] [
			arg4 = (! (uivisible $arg1))
			set uiWindowsAnim (concat $uiWindowsAnim $arg1 $arg4)
		]
		if $arg4 [ showui $arg1 ]
	]
]

// usage: /enableUI "name" MILLIS <force-state>
enableUI = [
	toggleUI $arg1 $arg2 1
]

// usage: /disableUI "name" MILLIS <force-state>
disableUI = [
	toggleUI $arg1 $arg2 0
]

|f = [
	// separate color components
	arg2 = (&     $arg1 0xFFFFFF) // rgb
	arg3 = (& (>> $arg1 24) 0xFF) // alpha
	if $arg3 [] [ arg3 = 0xFF ]
	arg3 = (*f $[.@uiname.fade] $arg3)
	| (<< (max 1 (round $arg3)) 24) $arg2
]


////////////////////////////////////////////////////////////////////////////////////////

uiFancyMenu = [
	newui $arg1 [
		@arg5
		if $uiInputOOB [
			uieschide 0
			uipress    [ uiCleanKB ]
			uiescpress [ uiCleanKB ]
		]
		uivlist 0 [
			@@(? $arg6 [
				uicolor (|f 0xEE181818) 0 0 [
					//uiFastImgStretched (fade (*f $.@@arg1.fade 0.5)) "shadow2" "" "" [] $uiPad:6XL
					uiclamp*e ; @@arg6
				]
			])
			uicolor (|f 0xEE202020) 0.01 0.01 [
				uiFastImgTiled "<intmul:0>" "ui/" "ui_bg0"
				//uiFastImgStretched (fade (*f $.@@arg1.fade 0.5)) "shadow3" "" "" [] $uiPad:M
				uispace $uiPad:L $uiPad:L [ @@@@arg2 ] ; uiclamp*e
			] ; uiclamp*x
		]
		uioutline (|f 0xEE181818)
		uiclamp*e
	] [
		uiSetBgBlur 1
		uiSetMillis @arg1
		uiSetLanguage @arg1
		@arg3
	] [
		uiSetMillis @arg1
		uiSetBgBlur -1
		if (=s $.UI_KBthisui @arg1) [ uiCleanKB ]
		@arg4
	]
]

uiText      = [ uicolortext $arg1 (|f -1)    $arg2 ]
uiColorText = [ uicolortext $arg1 (|f $arg3) $arg2 ]

uiFontText      = [ uifont $arg1 [ uiText      $arg2 $arg3       ] ]
uiFontColorText = [ uifont $arg1 [ uiColorText $arg2 $arg3 $arg4 ] ]

uiFancyBar = [
	if (> $numargs 4) [] [ arg5 = $c_line1 ]
	arg5 = (|f $arg5)
	if (|| $arg3 $arg4) [
		uispace $arg3 $arg4 [
			uiclamp $arg1 $arg1 $arg2 $arg2
			uiline $arg5
			uiclamp- $arg1 $arg1 $arg2 $arg2
		]
	] [
		uiline $arg5
		uiclamp- $arg1 $arg1 $arg2 $arg2
	]
]

uiFancyMenu "test1" [
	uicolor (|f 0x404040) 0.2 0.2 [
		uiFontColorText "wide" "1" 5 0x202020
		uiFontText "wide" "TESTING" 0.8 -1
		uioutline (|f 0xFF0000) ; uiclamp-e
	]
]

////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////

// uiHorSld VAR MIN MAX STEP [ on-change ] [ label formatting ] X Y [ pulse trigger cond ]
uiHorSld2 = [
	if $arg7 [] [ arg7 = $uiPad:5XL ]
	if $arg8 [] [ arg8 = $uiPad:5XL ]
	if $arg6 [ arg6 = (do $arg6) ] [ arg6 = $$arg1 ]
	if $arg9 [ arg9 = (do $arg9) ] [ arg9 = (|| uihover+? uihold+?) ]
	uitarget $arg7 $arg8 [
		uiHoverOnce [ uiHoverSound ]
		uiSetScrollLock 1
		uihslider $arg1 $arg2 $arg3 $arg4 [ doargs $arg5 ] [
			uihlist 0 [
				loop n 31 [ uiBar 0 1 0 (*f $arg8 (? (mod $n 5) 0.475 0.35)) $c_cb0 ]
			] ; uiclamp-e
			uiBar 1 0 0 0 $c_cb0
			uisliderbutton [
				if (<=f $arg2 $$arg1 $arg3) [
					uicolor $c_main1 0 0 [
						uioutline $c_main1
						uiFastImgTiled <mad:0/0/0> "ui/" "ui_bg2" "" [ uiclamp.e ]
						uiFastImgStretched "" "shadow2" "" "" [ uiclamp.x ]
						uiclamp*e
						uifontcolortext "def.ol" $arg6 (|A! (? $arg9 0xA0 0x60)) 0.65
					] ; uiclamp-y
				]
			] ; uiclamp-y
		] ; uiclamp-e
	]
]
