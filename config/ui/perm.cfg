
///////////////////////////////////////////////////////////////////////////////
//  PERMANENT UI -- DO *NOT* MAKE CHANGES HERE                               //
///////////////////////////////////////////////////////////////////////////////

newui "perm" [
	uieschide 0
	uiclamp.e

	// Disables active input field if clicking Out-Of-Bounds
	if $uiInputOOB [
		uiallowinput 1
		uipress    [ .UI_KBclean ]
		uiescpress [ .UI_KBclean ]
		uitarget
	] [ uiallowinput 0 ]

	// test OOB condition
	// uicolor (? $uiInputOOB 0x20FF0000 0x2000FF00)

	//uigroup [
	//	uivlist 0 [
	//		uiFancyText "mono" (concat mouse X: (getpxposx)) 0.7
	//		uiFancyText "mono" (concat mouse Y: (getpxposy)) 0.7
	//		uialign* -1
	//	] ; uialign- 1 -1
	//]

	if $mainmenu [
		uiallowinput (uivisible "main")
		uigroup [
			uitarget 0 0 [
				uipress [ changeui "credits" "main" ]
				uispace $uiPad:4XL $uiPad:4XL [
					uivlist $uiPad:3XL- [
						uiFastImg "" "badge_tesseract" "" "" $uiPad:D2XL
						uivlist $uiPad:M- [
							uifontcolortext "wide.ol" "TESSERACT" $c_cyan_t 0.6
							uifontcolortext "wide.ol" "ENGINE" $c_cyan_t 0.7
						]
					]
				]
			] ; uialign- 1 1
		]
		h0.f = 0 
	] [
		if (|| [isdead $getclientnum] [&& $uiBlurBg $uiBlurring]) [
			+= h0.f 0.2
		] [ -= h0.f 0.2 ]
		clampf= h0.f 0 1
	]

	if $h0.f [
		uiFastImgTiled (fade $h0.f) "ui/" "ui_bg2"
		if (isdead $getclientnum) [
			uiFastImg (fade $h0.f) "hud/" "damage"
		]
		if $uiVignette [] [
			uiFastImg (fade $h0.f) "shadow"
		]
	]
	if $uiVignette [ uiFastImg "" "shadow" ]
	uiclamp*e
]

newui "tips" [
	uiallowinput 0
	uiclamp.e
	pushif n $uiTipName [
		if $uiTipLive [
			local xdiff ydiff x y
			xdiff = (abs (- $uiTipPosX (getpxposx)))
			ydiff = (abs (- $uiTipPosY (getpxposy)))
			if (|| [> $xdiff 16] [> $ydiff 16] [
				> $getmillis (+ $uiTipTime 6000)
			]) [ set uiTipName "" ; set uiTipLive 0 ] [
				x = (*f (divf $uiTipPosX $screenw) $uiaspect)
				y =     (divf $uiTipPosY $screenh)
				+= x (*f 0.00060 $crosshairsize)
				+= y (*f 0.00126 $crosshairsize)
				uioffset $x $y [
					uiFastImgStretched <mad:.05/.05/.05> "hud/" "row" "" [
						uispace $uiPad:S 0 [
							uihlist $uiPad:S [
								uiFancyText "wide" "?" 0.6 $c_green
								uitext $n 0.5
							]
						]
					] $uiPad:4XL
				] ; uialign- -1 -1
			]
		] [
			if (|| [!= $uiTipPosX (getpxposx)] [!= $uiTipPosY (getpxposy)]) [
				set uiTipTime $getmillis
				set uiTipPosX (getpxposx)
				set uiTipPosY (getpxposy)
			] [ set uiTipLive (> $getmillis (+ $uiTipTime 1500)) ]
		]
	]
]
