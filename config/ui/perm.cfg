
///////////////////////////////////////////////////////////////////////////////
//  PERMANENT UI -- DO *NOT* MAKE CHANGES HERE                               //
///////////////////////////////////////////////////////////////////////////////

newui "perm" [
	uieschide 0
	uiclamp.e

	// Disables active input field if clicking Out-Of-Bounds
	if $uiInputOOB [
		uiallowinput 1
		uipress    [ uiCleanKB ]
		uiescpress [ uiCleanKB ]
		uitarget
	] [ uiallowinput 0 ]

	nodebug [
		local animProgress windowsCache
		windowsCache = $uiAnimWindows
		looplist2 ui curState $windowsCache [
			if $curState [
				animProgress = (- $getmillis $[.@ui.timestamp] $[.@ui.progress] -1)
			] [ animProgress = (- (+ $[.@ui.timestamp] $[.@ui.duration]) $getmillis $[.@ui.progress]) ]
			if (<= 0 $animProgress $[.@ui.duration]) [
				[.@ui.opacity] = (%f $animProgress $[.@ui.duration])
			] [
				listsplice= uiAnimWindows "" (indexof $uiAnimWindows $ui) 2
				[.@ui.opacity]  = $curState
				[.@ui.progress] = 0
				if $curState [] [ hideui $ui ]
			]
		]
	]

	if $mainmenu [
		uiallowinput (uivisible "main")
		uigroup [
			uitarget 0 0 [
				uipress [ changeui "credits" "main" ]
				uispace $uiPad:4XL $uiPad:4XL [
					uivlist $uiPad:3XL- [
						uiFastImg "" "badge_tesseract" "" "" $uiPad:D2XL
						uivlist $uiPad:M- [
							uifontcolortext "wide.ol" "TESSERACT" $c_cyan_t 0.6
							uifontcolortext "wide.ol" "ENGINE" $c_cyan_t 0.7
						]
					]
				]
			] ; uialign- 1 1
		]
	] [ nodebug [
		local bits

		if (&& $uiBlurBg $uiBlurring) [ ^= bits 1 ]
		if (|| (isdead $getclientnum) [ && $isspectator [
			< (+ (getclienthealth $getfollow) 1) 0
		] ])                          [ ^= bits 2 ]

		uiAnimate effects 120 $bits

		if $bits [ uiEffects = $bits ] [
			if (=f $.effects.opacity 0) [ uiEffects = 0 ]
		]

		if (& $uiEffects 3) [
			uiFastImgTiled (fade $.effects.opacity) "ui/" "ui_bg2"
		]
		if (& $uiEffects 2) [
			uiFastImg (fade $.effects.opacity) "hud/" "damage"
		]
		if (|| $uiVignette [& $uiEffects 1]) [
			uiFastImg (fade $.effects.opacity) "shadow"
		]
	] ] ; uiclamp*e
] [
	showui "tips"
	uiEffects = 0 // stores bits for fullscreen effects
]

// arg1: var
// arg2: duration
// arg3: condition
uiAnimate = [
	arg1 = [.@arg1.opacity] ; if $arg3 [
		if (<f $$arg1 1) [
			$arg1 = (+f $$arg1 (/f $getframemillis $arg2))
		] [ $arg1 = (min 1 $$arg1) ]
	] [
		if (>f $$arg1 0.01) [
			$arg1 = (-f $$arg1 (/f $getframemillis $arg2))
		] [ $arg1 = (max 0 $$arg1) ]
	]
]
