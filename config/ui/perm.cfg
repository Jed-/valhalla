
///////////////////////////////////////////////////////////////////////////////
//  PERMANENT UI -- DO *NOT* MAKE CHANGES HERE                               //
///////////////////////////////////////////////////////////////////////////////

newui "perm" [
	uieschide 0
	uiclamp.e

	// Disables active input field if clicking Out-Of-Bounds
	if $uiInputOOB [
		uiallowinput 1
		uipress    [ uiCleanKB ]
		uiescpress [ uiCleanKB ]
		uitarget
	] [ uiallowinput 0 ]

	nodebug [
		local progress windows
		windows = $uiWindowsAnim
		looplist2 ui state $windows [
			if $state [
				progress = (- $getmillis $[.@ui.time] $[.@ui.crop])
			] [ progress = (- (+ $[.@ui.time] $[.@ui.anim]) $getmillis $[.@ui.crop]) ]
			if (<= 0 $progress $[.@ui.anim]) [
				[.@ui.fade] = (%f $progress $[.@ui.anim] 1) 
			] [
				listsplice= uiWindowsAnim "" (indexof $uiWindowsAnim $ui) 2
				[.@ui.fade] = $state
				[.@ui.crop] = 0
				if $state [] [ hideui $ui ]
			]
		]
	]

	if $mainmenu [
		uiallowinput (uivisible "main")
		uigroup [
			uitarget 0 0 [
				uipress [ changeui "credits" "main" ]
				uispace $uiPad:4XL $uiPad:4XL [
					uivlist $uiPad:3XL- [
						uiFastImg "" "badge_tesseract" "" "" $uiPad:D2XL
						uivlist $uiPad:M- [
							uifontcolortext "wide.ol" "TESSERACT" $c_cyan_t 0.6
							uifontcolortext "wide.ol" "ENGINE" $c_cyan_t 0.7
						]
					]
				]
			] ; uialign- 1 1
		]
		h0.f = 0 
	] [
		if (|| [isdead $getclientnum] [&& $uiBlurBg $uiBlurring]) [
			+= h0.f 0.2
		] [ -= h0.f 0.2 ]
		clampf= h0.f 0 1
	]

	if $h0.f [
		uiFastImgTiled (fade $h0.f) "ui/" "ui_bg2"
		if (isdead $getclientnum) [
			uiFastImg (fade $h0.f) "hud/" "damage"
		]
		if $uiVignette [] [
			uiFastImg (fade $h0.f) "shadow"
		]
	]
	if (&& $uiVignette [! $mainmenu]) [ uiFastImg "" "shadow" ]
	uiclamp*e
] [ showui "tips" ]
