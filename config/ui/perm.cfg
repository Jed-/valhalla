
///////////////////////////////////////////////////////////////////////////////
//  PERMANENT UI -- DO *NOT* MAKE CHANGES HERE                               //
///////////////////////////////////////////////////////////////////////////////

newui "perm" [
	uieschide 0
	uiclamp.e

	// Disables active input field if clicking Out-Of-Bounds
	if $uiInputOOB [
		uiallowinput 1
		uipress    [ uiCleanKB ]
		uiescpress [ uiCleanKB ]
		uitarget
	] [ uiallowinput 0 ]

	nodebug [
		local animProgress windowsCache
		windowsCache = $uiAnimWindows
		looplist2 ui curState $windowsCache [
			if $curState [
				animProgress = (- $getmillis $[.@ui.timestamp] $[.@ui.progress] -1)
			] [ animProgress = (- (+ $[.@ui.timestamp] $[.@ui.duration]) $getmillis $[.@ui.progress]) ]
			if (<= 0 $animProgress $[.@ui.duration]) [
				[.@ui.opacity] = (%f $animProgress $[.@ui.duration])
			] [
				listsplice= uiAnimWindows "" (indexof $uiAnimWindows $ui) 2
				[.@ui.opacity]  = $curState
				[.@ui.progress] = 0
				if $curState [] [ hideui $ui ]
			]
		]
	]

	if $mainmenu [
		uiallowinput (uivisible "main")
		uigroup [
			uitarget 0 0 [
				uipress [ changeui "credits" "main" ]
				uispace $uiPad:4XL $uiPad:4XL [
					uivlist $uiPad:3XL- [
						uiFastImg "" "badge_tesseract" "" "" $uiPad:D2XL
						uivlist $uiPad:M- [
							uifontcolortext "wide.ol" "TESSERACT" $c_cyan_t 0.6
							uifontcolortext "wide.ol" "ENGINE" $c_cyan_t 0.7
						]
					]
				]
			] ; uialign- 1 1
		]
		h0.f = 0 
	] [
		if (|| [isdead $getclientnum] [&& $uiBlurBg $uiBlurring]) [
			+f= h0.f 0.2
		] [ -f= h0.f 0.2 ]
		clampf= h0.f 0 1
	]

	if $h0.f [
		uiFastImgTiled (fade $h0.f) "ui/" "ui_bg2"
		if (isdead $getclientnum) [
			uiFastImg (fade $h0.f) "hud/" "damage"
		]
		if $uiVignette [] [
			uiFastImg (fade $h0.f) "shadow"
		]
	]
	if (&& $uiVignette [! $mainmenu]) [ uiFastImg "" "shadow" ]
	uiclamp*e
] [ showui "tips" ]
