
///////////////////////////////////////////////////////////////////////////////
//  Standard Variable Definitions                                            //
///////////////////////////////////////////////////////////////////////////////

// scoreboard variables
defvarp  showkills   0  1  1
defvarp  showdeaths  0  1  1
defvarp  showip      0  0  1

// placeholders
defvar  .autosortservers   0    1    1
defvar  .crosshairsize     0    0   50
defvar  .gridpower         0    3   12
defvar  .showmaterials     0    1    1
defvar  .entediting        0    1    1
defvar  .passthroughcube   0    1    0

defsvar .name             ""

defvar  .gscalef          25  100  100
defvar  .gscalew          25  100  100
defvar  .interpolation     1    4    4

defvar  .screenw  1  $screenw  $desktopw
defvar  .screenh  1  $screenh  $desktoph

// other variables
defvarp   zoomtoggle     0  0  1
defsvarp  modelist       "edit dm tdm ctf elim lms berserk infect invasion betray"
defsvarp  mutslist       "classic insta effic tactics voosh vampire mayhem nopower noitem"
defsvarp  notepadpath    "untitled.cfg"

defvar    .followplayer   0  0    1
defvar    .followcn      -1  0  159

defsvar   .followlist  ""
defsvar   .cmc_from    ""
defsvar   .cmc_to      ""

defsvar .tut_tip ""

// UI settings
defvarp   uiSpriteAnim  0  0   1
defvarp   uiBlurBg      0  1   1
defvarp   uiClearView   0  0   1 // I forget what this was for
defvarp   uiVignette    0  0   1
defvarp   uiClickTabs   0  0   1
defvarp   uiDebug       0  0   1

defvarp   uiConfirmQuit       0  1  1
defvarp   uiConfirmMapWrite   0  1  1

deffvarp  uiScaling     0.5  1.0  1.5
defvarp   uiLanguage    0    0    4   // change if necessary
defvarp   uiAccentColor 0x0  0x2BF36E  0xFFFFFF

defvarp   uiKillDecay    1500  4000  8000
defvarp   uiKillFade       50   150   500
defvarp   uiKillCount      12    21    36 // value must be multiple of 3
defvarp   uiDeathScores     0     1     1

defvar    uiBlurring    0  0  99
defvar    uiInputOOB    0  0   1
defvarp   uiNameSet     0  0   1
defvarp   uiScrollLock  0  0   1

defvarp   uiWindowSpeed  50  125  600

nodebug [
    local defWidth maxWidth
    defWidth = (/f 1920 1080)
    maxWidth = (/f $desktopw $desktoph)

    deffvarp uiWideHUD 1 (minf $defWidth $maxWidth) $maxWidth
    // ^ it likes complaining about range sometimes,
    //   despite the values being identical...
]

defsvar   uiTipName    ""
defsvar   uiTipMenu    ""
defvar    uiTipLive     0  0   1

// HUD settings
defvarp   hudKillMillis        0  3500  5000
defvarp   hudWeaponBar         0  1     1
defvarp   hudWeaponBarMillis   0  1000  5000
defvarp   hudPickupInfoMillis  0  1500  5000

// Mandatory Keyboard Hook variables
defvar    .UI_KBcaret   -1 -1  99
defsvar   .UI_KBvarcur  ""
defsvar   .UI_KBthisui  ""

// UI specific variables
defvarp  .mm_doaction   0    1    1
defvarp  .mb_maplist    0    0    1
defsvar  .mb_selected  ""
defvarp  .mr_options    0    0    1
defvarp  .mr_resmul    10  100  100
defsvar  .mr_title     ""
defsvar  .om_advaudio   0    0    1

defsvar  .savemapname  ""
defsvar  .serverpassw  ""
defsvar  .serverhover  -1 -1  9999

defvarp  .cci_showbots   0  1  1
defvarp  .cci_showself   0  1  1
defvarp  .cci_showspec   0  1  1
defsvar  .cci_clients   ""

defvarp  .sb_mm            0     0    31
defvarp  .sb_mode          0     0  1023
defvarp  .sb_ping         55  9999  9999
defvarp  .sb_players_e     0     0     1
defvarp  .sb_players_f     0     0     1
defvarp  .sb_showfilters   0     0     1
defsvar  .sb_connectid    ""

defsvar  .kb_mode    "norm"
defsvar  .kb_selkey  ""

defvarp  .botskill      0   50  101


///////////////////////////////////////////////////////////////////////////////
//  Keybind & Combobind Definitions                                          //
///////////////////////////////////////////////////////////////////////////////

||iskeyheld = [
    local k ; k = 0
    loop+ i 1 $numargs [
        k = (+ $k (iskeyheld $[arg@i]))
    ]
    > $k 0
]




///////////////////////////////////////////////////////////////////////////////
//  Standard Texture Commands (*.tex)                                        //
///////////////////////////////////////////////////////////////////////////////

texsky      = [ setshader "stdworld" ; texture 0 "base/sky.png" ]
texerror    = [ setshader "stdworld" ; texture 0 "base/notexture.png" ]
texnorender = [ setshader "stdworld" ; texture 0 "base/norender.png" ]
texload     = [
    if (! (exec [data/texture/world/@arg1.tex] 0)) [
        error [texload error: @arg1.tex not found]
        texerror
    ]
]
decalload = [
    if (! (exec [data/texture/decal/@arg1.cfg] 0)) [
        error [decalload error: @arg1.cfg not found]
    ]
]

///////////////////////////////////////////////////////////////////////////////
//  Matrix operations for `vmatrix`                                          //
///////////////////////////////////////////////////////////////////////////////
det2x2 = [
    (-f (*f $arg1 $arg4) (*f $arg3 $arg2))
]
invert2x2 = [
    local k det
    det = (det2x2 (at $arg1 0) (at $arg1 1) (at $arg1 2) (at $arg1 3))
    if (=f $det 0) [
        result [1 0 0 1]
    ] [
        k = (divf 1 $det)
        result (concat (*f $k (at $arg1 3)) (*f $k (*f -1 (at $arg1 1))) (*f $k (*f -1 (at $arg1 2))) (*f $k (at $arg1 0)) )
    ]
]
// makes transformations work when textures are not square
sandwich2x2 = [
    local s M A B
    s = $arg1
    M = $arg2
    A = (concat $s 0 0 1)
    B = (concat (divf 1 $s) 0 0 1)
    result (multiply2x2 $B (multiply2x2 $M $A))
]
multiply2x2 = [
    result (concat (
        +f (*f (at $arg1 0) (at $arg2 0)) (*f (at $arg1 2) (at $arg2 1))
    ) (
        +f (*f (at $arg1 1) (at $arg2 0)) (*f (at $arg1 3) (at $arg2 1))
    ) (
        +f (*f (at $arg1 0) (at $arg2 2)) (*f (at $arg1 2) (at $arg2 3))
    ) (
        +f (*f (at $arg1 1) (at $arg2 2)) (*f (at $arg1 3) (at $arg2 3))
    ) )
]
